# Contributor: György Balló <ballogy@freestart.hu>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Jorge Mokross <mokross@gmail.com>

pkgname=notify-sharp
pkgver=0.4.0.3032
_pkgver=0.4.0~r3032
pkgrel=1
pkgdesc="C Sharp bindings for desktop notification"
url="http://www.ndesk.org/NotifySharp"
license=('MIT')
arch=('any')
depends=('gtk-sharp-2' 'dbus-sharp-glib') 
provides=('notify-sharp-svn=3032')
replaces=('notify-sharp-svn')
source=(https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_$_pkgver.orig.tar.gz
        02_reverse_cap_check.patch
        03-use-dbus-sharp.patch
        04-fix-app-name-derivation.patch)
md5sums=('7691cc46f04d0aa13f342a97dd33f0ac'
         '7ea06cc54c3357081a932a58b0f89ce4'
         '7871afb6ffd228179c1016b2a5074316'
         '98848a5b09e27ca47b0abf8d244ae226')

build() {
  cd "$srcdir/$pkgname-${pkgver%.*}"
  patch -Np1 -i "$srcdir/02_reverse_cap_check.patch"
  patch -Np1 -i "$srcdir/03-use-dbus-sharp.patch"
  patch -Np1 -i "$srcdir/04-fix-app-name-derivation.patch"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd "$srcdir/$pkgname-${pkgver%.*}"

  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

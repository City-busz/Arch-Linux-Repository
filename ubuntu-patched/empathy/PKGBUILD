# $Id: PKGBUILD 99876 2010-11-18 22:38:17Z ibiru $
# Maintainer: Ionut Biru <ibiru@archlinux.org>
pkgname=empathy
pkgver=2.32.2
pkgrel=1
pkgdesc="A GNOME instant messaging client using the Telepathy framework."
arch=('i686' 'x86_64')
url="http://live.gnome.org/Empathy"
license=('GPL2')
depends=('telepathy-glib>=0.12.4' 'telepathy-mission-control>=5.6.1' 'folks>=0.2.1' 'telepathy-logger>=0.1.5' 'telepathy-farsight>=0.0.15'
         'gnome-keyring>=2.32.0' 'gnutls' 'gconf>=2.32.0' 'evolution-data-server>=2.32.1' 'libcanberra>=0.25' 'libnotify' 'libunique'
         'enchant' 'iso-codes' 'libwebkit' 'dconf>=0.5.1' 'ca-certificates')
makedepends=('intltool' 'gnome-doc-utils>=0.20.1' 'nautilus-sendto' 'libindicate>=0.3.1')
optdepends=('telepathy-gabble: XMPP/Jabber support'
            'telepathy-butterfly: MSN support'
            'telepathy-idle: IRC support'
            'telepathy-salut: Link-local XMPP support'
            'telepathy-sofiasip: SIP support'
            'telepathy-haze: libpurple support'
            'libindicate: indicator support')
options=('!libtool')
groups=('gnome-extra')
install=empathy.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/2.32/${pkgname}-${pkgver}.tar.bz2
        20_libindicate.patch)
sha256sums=('f90a45955205fd8bd783a55acae4603197caab4f16a880106f8639cbbbe5e861'
            'a05db3e4396e16eab187e182e8de3ef9680a8f66fe8340b292400cc8478c47fa')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i $srcdir/20_libindicate.patch
  autoreconf -fi

  ./configure --prefix=/usr \
      --sysconfdir=/etc  \
      --libexecdir=/usr/lib/empathy \
      --disable-schemas-compile --disable-map --disable-location --with-connectivity=no
  make
  make DESTDIR="${pkgdir}" install

  mkdir -p $pkgdir/usr/share/indicators/messages/applications
  echo '/usr/share/applications/empathy.desktop' > $pkgdir/usr/share/indicators/messages/applications/empathy
}

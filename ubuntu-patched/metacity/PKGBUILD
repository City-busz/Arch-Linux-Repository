# $Id: PKGBUILD 92421 2010-09-29 20:16:48Z ibiru $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=metacity
pkgver=2.30.3
pkgrel=1.1
pkgdesc="A window manager for GNOME2"
arch=(i686 x86_64)
license=('GPL')
depends=('startup-notification>=0.10' 'gconf>=2.32.0' 'zenity>=2.32.0' 'libcanberra>=0.25' 'libgtop>=2.28.2')
makedepends=('intltool' 'pkgconfig' 'gnome-doc-utils>=0.20.2')
url="http://www.gnome.org"
groups=('gnome')
options=('!libtool' '!emptydirs')
install=metacity.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.30/${pkgname}-${pkgver}.tar.bz2
        02_restart_hint.patch
        04_support_drag_drop_with_alt_tab.patch
        11_hide_tooltip_on_decorator.patch
        13_better_support_for_button_layout.patch)
sha256sums=('08f887018fa5e447cf184d03bae3fe2c05fdb7583bed6768e3b4d66392fc18dd'
            '2d9a37d0be6d9a2f1cc9b7e0b451023d6c063fbcdea65601c01c111b86b8a86c'
            '9a9ac56b2a90482cea40fc7b97928efb5d55d7cc0a0306c559b9bea965a119d2'
            '630e4ca4263ed4c2db4948cf887700baf7aed89989e11490bdcd807a6d71e760'
            'f114fbd07c836fe9e0a795f849190e8a5312179fd7233c8a79faf1f22f47ce80')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i $srcdir/02_restart_hint.patch
  patch -Np1 -i $srcdir/04_support_drag_drop_with_alt_tab.patch
  patch -Np1 -i $srcdir/11_hide_tooltip_on_decorator.patch
  patch -Np1 -i $srcdir/13_better_support_for_button_layout.patch
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/metacity \
      --localstatedir=/var --disable-static
  make
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain metacity ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}

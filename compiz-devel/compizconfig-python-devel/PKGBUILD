# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=compizconfig-python-devel
_pkgname=compizconfig-python
pkgver=0.9.5.92
pkgrel=1
pkgdesc="Compizconfig bindings for python (development release)"
arch=('i686' 'x86_64')
url="http://www.compiz.org/"
license=('GPL')
depends=('python2' 'libcompizconfig-devel')
makedepends=('cmake' 'cython2')
provides=(compizconfig-python=$pkgver)
conflicts=(compizconfig-python)
replaces=(compizconfig-python-dev)
source=(http://releases.compiz.org/$pkgver/$_pkgname-$pkgver.tar.bz2)
md5sums=('95e42a55714a0743a32478bc56b18973')

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  sed -i 's/python /python2 /' CMakeLists.txt

  [[ -d build ]] || mkdir build
  cd build

  cmake .. -DCMAKE_INSTALL_PREFIX="$pkgdir"/usr \
           -DCMAKE_BUILD_TYPE=Release \
           -DCOMPIZ_DESTDIR="$pkgdir"
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver/build"

  make install
}

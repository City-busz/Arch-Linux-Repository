# Maintainer: György Balló <ballogy@freestart.hu>
pkgbase=compiz-devel
_pkgbase=compiz
pkgname=(compiz-core-devel compiz-decorator-gtk-devel compiz-decorator-kde-devel)
pkgver=0.9.5.92.1
pkgrel=1
arch=('i686' 'x86_64')
url="http://www.compiz.org/"
license=('MIT')
makedepends=(
  # Build depends
  'cmake' 'boost' 'automoc4' 'intltool'
  # Core depends
  'libxcomposite' 'libxrandr' 'libxinerama' 'libxslt' 'glibmm' 'startup-notification' 'mesa' 'dbus-core' 'librsvg' 'libsigc++' 'boost-libs' 'gconf'
  # GNOME depends
  'metacity'
  # KDE depends
  'kdebase-workspace')
source=(http://releases.compiz.org/$pkgver/$_pkgbase-$pkgver.tar.bz2)
md5sums=('c45dd77bd1937b75c850f80a411e46cb')

build() {
  cd "$srcdir/$_pkgbase-$pkgver"

  _default_plugins="core composite opengl compiztoolbox decor ezoom gnomecompat mousepoll move place regex resize scale session snap staticswitcher vpswitch wall workarounds"

  [[ -d build ]] || mkdir build
  cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DCMAKE_BUILD_TYPE=Release \
           -DCOMPIZ_PACKAGING_ENABLED=ON \
           -DCOMPIZ_DESTDIR="$pkgdir/$pkgname" \
           -DCOMPIZ_BUILD_WITH_RPATH=OFF \
           -DUSE_GSETTINGS=OFF \
           -DCOMPIZ_DISABLE_SCHEMAS_INSTALL=ON \
           -DCOMPIZ_DEFAULT_PLUGINS="$_default_plugins"
  make
}

package_compiz-core-devel() {
  pkgdesc="Composite manager for Aiglx and Xgl (development release)"
  depends=('glibmm' 'startup-notification' 'librsvg' 'libxcomposite' 'dbus-core' 'libsigc++' 'libxinerama' 'boost-libs' 'mesa' 'libxrandr' 'libxslt' 'gconf')
  provides=(compiz-core=$pkgver)
  conflicts=(compiz-core)
  replaces=(compiz-core-dev)
  install=compiz-core.install

  cd "$srcdir/$_pkgbase-$pkgver/build"

  make install
  make findcompiz_install

  # Merge GConf schemas
  [[ -d $srcdir/gconf ]] && rm -r "$srcdir/gconf/"
  mv "$pkgdir/usr/share/gconf" "$srcdir"
  mkdir -p "$pkgdir/usr/share/gconf/schemas"
  gconf-merge-schema "$pkgdir/usr/share/gconf/schemas/compiz-core.schemas" \
        "$srcdir"/gconf/schemas/compiz-*.schemas
  sed -i '/<schemalist\/>/d' "$pkgdir/usr/share/gconf/schemas/compiz-core.schemas"

  install -Dm644 "$srcdir/$_pkgbase-$pkgver/COPYING.MIT" "$pkgdir/usr/share/licenses/compiz-core-devel/COPYING.MIT"

  # Split GNOME and KDE stuff
  [[ -d $srcdir/gtk ]] && rm -r "$srcdir/gtk/"
  mkdir "$srcdir/gtk/"
  mv "$pkgdir"/usr/bin/gtk-window-decorator \
    "$srcdir/gtk/"
  [[ -d $srcdir/kde ]] && rm -r "$srcdir/kde/"
  mkdir "$srcdir/kde/"
  mv "$pkgdir"/usr/{bin/kde4-window-decorator,lib/compiz/libkde.so,share/compiz/kde.xml} \
    "$srcdir/kde/"
}

package_compiz-decorator-gtk-devel() {
  pkgdesc="Compiz decorator for GNOME (development release)"
  depends=("compiz-core-devel=$pkgver" 'metacity')
  provides=(compiz-decorator-gtk=$pkgver)
  conflicts=(compiz-decorator-gtk)
  replaces=(compiz-decorator-gtk-dev)
  install=compiz-decorator-gtk.install

  cd "$srcdir/gtk"
  mkdir -p "$pkgdir"/usr/{bin,share/{applications,gconf/schemas,gnome/wm-properties,gnome-control-center/keybindings}}

  mv gtk-window-decorator "$pkgdir/usr/bin/"

  # Install GConf schema
  mv "$srcdir/gconf/schemas/gwd.schemas" "$pkgdir/usr/share/gconf/schemas/compiz-decorator-gtk.schemas"

  # Install keybindings
  for i in launchers navigation screenshot system windows; do
    sed 's/wm_name=\"Metacity\"/wm_name=\"Compiz\"/' /usr/share/gnome-control-center/keybindings/50-metacity-$i.xml > "$pkgdir"/usr/share/gnome-control-center/keybindings/50-compiz-$i.xml;
  done

  # Install desktop files
  sed 's/_Name=/Name=/' "$srcdir/$_pkgbase-$pkgver/gtk/gnome/compiz.desktop.in" > "$pkgdir/usr/share/applications/compiz.desktop"
  sed 's/_Name=/Name=/' "$srcdir/$_pkgbase-$pkgver/gtk/gnome/compiz-wm.desktop.in" > "$pkgdir/usr/share/gnome/wm-properties/compiz-wm.desktop"

  install -Dm644 "$srcdir/$_pkgbase-$pkgver/COPYING.MIT" "$pkgdir/usr/share/licenses/compiz-decorator-gtk-devel/COPYING.MIT"
}

package_compiz-decorator-kde-devel() {
  pkgdesc="Compiz decorator for KDE (development release)"
  depends=("compiz-core-devel=$pkgver" 'kdebase-workspace')
  provides=(compiz-decorator-kde=$pkgver)
  conflicts=(compiz-decorator-kde)
  replaces=(compiz-decorator-kde-dev)

  cd "$srcdir/kde"
  mkdir -p "$pkgdir"/usr/{bin,lib/compiz,share/compiz}

  mv kde4-window-decorator "$pkgdir/usr/bin/"
  mv libkde.so             "$pkgdir/usr/lib/compiz/"
  mv kde.xml               "$pkgdir/usr/share/compiz/"

  install -Dm644 "$srcdir/$_pkgbase-$pkgver/COPYING.MIT" "$pkgdir/usr/share/licenses/compiz-decorator-kde-devel/COPYING.MIT"
}

# extratainer: György Balló <ballogy@freestart.hu>
pkgname=compiz-plugins-extra-devel
_pkgname=compiz-plugins-extra
pkgver=0.9.5.92
pkgrel=1
pkgdesc="Compiz extra plugins (development release)"
arch=('i686' 'x86_64')
url="http://www.compiz.org/"
license=('GPL')
depends=('compiz-plugins-main' 'libnotify')
makedepends=('cmake' 'boost')
provides=(compiz-plugins-extra=$pkgver)
conflicts=(compiz-plugins-extra compiz-fusion-plugins-extra)
replaces=(compiz-plugins-extra-dev)
install=compiz-plugins-extra.install
source=(http://releases.compiz.org/$pkgver/$_pkgname-$pkgver.tar.bz2)
md5sums=('dec14494a9b7c0f3cc37f1a7d8a340b2')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  [[ -d build ]] || mkdir build
  cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DCMAKE_BUILD_TYPE=Release \
           -DCOMPIZ_PACKAGING_ENABLED=ON \
           -DCOMPIZ_DESTDIR="$pkgdir" \
           -DCOMPIZ_BUILD_WITH_RPATH=OFF \
           -DUSE_GSETTINGS=OFF \
           -DCOMPIZ_DISABLE_SCHEMAS_INSTALL=ON
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver/build"

  make install

  # Merge GConf schemas
  [[ -d $srcdir/gconf ]] && rm -r "$srcdir/gconf/"
  mv "$pkgdir/usr/share/gconf" "$srcdir"
  mkdir -p "$pkgdir/usr/share/gconf/schemas"
  gconf-merge-schema "$pkgdir/usr/share/gconf/schemas/compiz-plugins-extra.schemas" \
        "$srcdir"/gconf/schemas/compiz-*.schemas
  sed -i '/<schemalist\/>/d' "$pkgdir/usr/share/gconf/schemas/compiz-plugins-extra.schemas"
}

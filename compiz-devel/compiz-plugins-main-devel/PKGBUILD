# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=compiz-plugins-main-devel
_pkgname=compiz-plugins-main
pkgver=0.9.5.92
pkgrel=1
pkgdesc="Compiz main plugins (development release)"
arch=('i686' 'x86_64')
url="http://www.compiz.org/"
license=('GPL')
depends=('compiz-core-devel')
makedepends=('cmake' 'boost')
provides=(compiz-plugins-main=$pkgver)
conflicts=(compiz-plugins-main compiz-fusion-plugins-main)
replaces=(compiz-plugins-main-dev)
install=compiz-plugins-main.install
source=(http://releases.compiz.org/$pkgver/$_pkgname-$pkgver.tar.bz2)
md5sums=('e7582e7132a9c18e2c0f353911b250ab')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  [[ -d build ]] || mkdir build
  cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DCMAKE_BUILD_TYPE=Release \
           -DCOMPIZ_PACKAGING_ENABLED=ON \
           -DCOMPIZ_DESTDIR="$pkgdir" \
           -DCOMPIZ_BUILD_WITH_RPATH=OFF \
           -DUSE_GSETTINGS=OFF \
           -DCOMPIZ_DISABLE_SCHEMAS_INSTALL=ON
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver/build"

  make install

  # Merge GConf schemas
  [[ -d $srcdir/gconf ]] && rm -r "$srcdir/gconf/"
  mv "$pkgdir/usr/share/gconf" "$srcdir"
  mkdir -p "$pkgdir/usr/share/gconf/schemas"
  gconf-merge-schema "$pkgdir/usr/share/gconf/schemas/compiz-plugins-main.schemas" \
        "$srcdir"/gconf/schemas/compiz-*.schemas
  sed -i '/<schemalist\/>/d' "$pkgdir/usr/share/gconf/schemas/compiz-plugins-main.schemas"
}

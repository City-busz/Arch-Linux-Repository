# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=compizconfig-backend-gconf-devel
_pkgname=compizconfig-backend-gconf
pkgver=0.9.5.92
pkgrel=1
pkgdesc="Compizconfig backend for gconf (development release)"
arch=('i686' 'x86_64')
url="http://www.compiz.org/"
license=('GPL')
depends=('libcompizconfig-devel')
makedepends=('cmake')
provides=(compizconfig-backend-gconf=$pkgver)
conflicts=(compizconfig-backend-gconf)
replaces=(compizconfig-backend-gconf-dev)
source=(http://releases.compiz.org/$pkgver/$_pkgname-$pkgver.tar.bz2
        new-api-fix.patch)
md5sums=('c298fa74921cfd0360b3eef3c0b527ef'
         'e22dca90a4fa70cd14df01c12f6e5430')

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  patch -Np1 -i "$srcdir/new-api-fix.patch"

  [[ -d build ]] || mkdir build
  cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DCMAKE_BUILD_TYPE=Release \
           -DCOMPIZ_DESTDIR="$pkgdir" \
           -DCOMPIZ_BUILD_WITH_RPATH=OFF
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver/build"

  make install
}

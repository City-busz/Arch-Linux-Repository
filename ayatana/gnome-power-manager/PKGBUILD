# $Id: PKGBUILD 91642 2010-09-27 15:18:37Z ibiru $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-power-manager
pkgver=2.32.0
pkgrel=1.1
pkgdesc="Session daemon that makes it easy to manage your laptop or desktop system."
arch=(i686 x86_64)
url="http://www.gnome.org/projects/gnome-power-manager/"
license=('GPL')
depends=('libnotify>=0.4.5' 'libunique>=1.1.6' 'libcanberra>=0.25' 'hicolor-icon-theme' 'upower>=0.9.5' 'libgnome-keyring>=2.31.92' 'indicator-application>=0.0.10')
makedepends=('intltool' 'pkgconfig' 'gnome-doc-utils>=0.20.1' 'gnome-panel-bonobo>=2.31.92' 'gnome-common')
optdepends=('gnome-panel-bonobo: panel applets')
options=(!emptydirs)
install=gnome-power-manager.install
groups=(gnome-extra)
source=(http://ftp.gnome.org/pub/GNOME/sources/gnome-power-manager/2.32/${pkgname}-${pkgver}.tar.bz2
        00git-devices-changed-signal.patch
        02-notify-osd-support.patch
        12-add-appindicators.patch)
sha256sums=('17fa301bf7e133285c0e054ae3be2b0f690c48f59b09f67e04d6ed339b330476'
            'c106932d7a62addab58fc04086d71f86e06133ca5e5405e47e6751f1f7d79981'
            '19175e0f158b93fe95a7fc3c5a26a8ece88e3a8f18d552dccea0b8eec3704e8f'
            '1180135c059e161afe1e071fa2ececda7c7b919cbbcd184616b2f54f289780d0')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "$srcdir/00git-devices-changed-signal.patch"
  patch -Np1 -i "$srcdir/02-notify-osd-support.patch"
  patch -Np1 -i "$srcdir/12-add-appindicators.patch"

  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var \
      --libexecdir=/usr/lib/gnome-power-manager \
      --disable-scrollkeeper \
      --with-dbus-services=/usr/share/dbus-1/services \
      --enable-applets
  make
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnome-power-manager ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}

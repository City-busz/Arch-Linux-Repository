# Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: Madek <gonzaloseguel@gmail.com>
# Contributor: Marcelo Cavalcante <marcelo@marcelocavalcante.net>
# Contributor: Jens Kleikamp <jens@codes-concepts.com>
# Contributor: Philipp Gildein <rmbl@openspeak-project.org>

pkgname=smuxi
pkgver=0.8
pkgrel=4
pkgdesc="The user-friendly and cross-platform IRC client for sophisticated users for GNOME/GTK+"
arch=('any')
url="http://www.smuxi.org/"
license=('GPL')
depends=('notify-sharp-svn' 'libindicate' 'log4net' 'nini')
makedepends=('intltool>=0.25')
source=(http://www.smuxi.org/jaws/data/files/$pkgname-$pkgver.tar.gz
smuxi-visibility.patch)
md5sums=('7514ddf1f831d4cd07529a81c34cc824'
'caa7f2a8b7138361415d5fb5324ca4d9')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  pushd lib/Twitterizer
  patch -p1 <$srcdir/smuxi-visibility.patch
  popd

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  mkdir -p $pkgdir/usr/share/indicators/messages/applications
  echo '/usr/share/applications/smuxi-frontend-gnome.desktop' > $pkgdir/usr/share/indicators/messages/applications/smuxi-frontend-gnome
}

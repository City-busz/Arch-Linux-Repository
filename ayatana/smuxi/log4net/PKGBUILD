# Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: Frane Bandov <frane@offbyte.com>
# Contributor: Thijs Vermeir <thijsvermeir@gmail.com>

pkgname=log4net
pkgver=1.2.10
pkgrel=3
pkgdesc="A tool to help the programmer output log statements to a variety of output targets"
arch=('any')
url="http://logging.apache.org/log4net/"
license=('APACHE')
depends=('mono')
makedepends=('unzip')
source=(http://archive.apache.org/dist/incubator/$pkgname/$pkgver/incubating-$pkgname-$pkgver.zip
        $pkgname.pc)
md5sums=('0085970684888a5dfb1d59b2831f8d7e'
         '4e8f14ac075fcaaf642df5d11fcf2193')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  sn -k $pkgname.snk
  gmcs -t:library -out:bin/$pkgname.dll -keyfile:$pkgname.snk \
       -r:System,System.Data,System.Xml,System.Web \
       $(find src -name "*.cs")
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  gacutil -i bin/$pkgname.dll -package $pkgname -root $pkgdir/usr/lib/
  install -Dm644 $srcdir/$pkgname.pc $pkgdir/usr/lib/pkgconfig/$pkgname.pc
}

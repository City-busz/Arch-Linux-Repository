# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=indicator-battery-status
_pkgname=battery-status
pkgver=0.1.1
pkgrel=1
pkgdesc="An applet for the GNOME panel and indicator for the Indicator Applets, which provides usability way for getting power information about battery"
arch=('any')
url="https://launchpad.net/battery-status"
license=('GPL')
depends=('indicator-application' 'dbus-python' 'python2-gconf' 'python-gnomeapplet' 'hicolor-icon-theme' 'xdg-utils')
install=$pkgname.install
source=(http://launchpad.net/$_pkgname/0.x/$pkgver/+download/${_pkgname}_$pkgver.tar.gz
        indicator-battery-status.desktop)
md5sums=('6e09d265d59f0fd4dec577c6117324f0'
         '6f5e76defe089b0b369f7b3b43cbfc97')

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' battery-status

  python2 setup.py build
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  python2 setup.py install --root=$pkgdir/ --optimize=1

  # Install autostart file
  install -Dm644 "$srcdir/$pkgname.desktop" "$pkgdir/etc/xdg/autostart/$pkgname.desktop"
}

# $Id: PKGBUILD 112935 2011-03-07 19:28:46Z ibiru $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=vino
pkgver=2.32.1
pkgrel=1.1
pkgdesc="a VNC server for the GNOME desktop"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libnotify>=0.7.0' 'libxtst' 'libunique>=1.1.6' 'libsm' 'gconf>=2.32.0' 'libsoup>=2.32.1' 'telepathy-glib>=0.12.4' 'gtk2>=2.22.1' 
         'libgnome-keyring>=2.32' 'avahi>=0.6.27' 'indicator-application>=0.0.7')
makedepends=('intltool' 'networkmanager' 'gnome-common')
groups=('gnome-extra')
url="http://www.gnome.org"
options=(!emptydirs)
install=vino.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.32/${pkgname}-${pkgver}.tar.bz2
        12_app-indicators-only.patch)
sha256sums=('8ec3cb84216323660d6301e02f5853f928789c20be68ded538dd849eafac4087'
            '97668c094008414703a610ec647a99fa25e305a9101c2b60592bc138d9460293')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "$srcdir/12_app-indicators-only.patch"
  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/vino \
      --localstatedir=/var \
      --enable-gnome-keyring \
      --enable-avahi
  make
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain vino ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}

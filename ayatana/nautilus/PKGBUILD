# $Id: PKGBUILD 104094 2010-12-28 19:26:26Z ibiru $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=nautilus
pkgver=2.32.2.1
pkgrel=1.1
pkgdesc="The GNOME shell and file manager"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libexif>=0.6.19' 'gnome-desktop>=2.32.1' 'exempi>=2.1.1' 'libunique>=1.1.6' 'hicolor-icon-theme' 'gvfs>=1.6.6' 'desktop-file-utils' 'indicator-application')
makedepends=('intltool' 'gobject-introspection' 'namcap')
replaces=('gnome-volume-manager')
url="http://www.gnome.org"
groups=('gnome')
options=('!libtool' '!emptydirs')
install=nautilus.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.32/${pkgname}-${pkgver}.tar.bz2
        nautilus-condrestart.patch
        89_use_application_indicator.patch)
sha256sums=('802dacc98c697afea2e6201dab1c8c800809f235fff39857983efb4340ecc303'
            'f825292ef26274d3044272ca9f25095391cc49d30fb0056e7ed7346a1b4d106c'
            '0b0a756430412735c34ce3878900f9a3dc7486ff77d003dd1fbd40fdeaa8bba5')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/nautilus-condrestart.patch"
  patch -Np1 -i "${srcdir}/89_use_application_indicator.patch"
  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libexecdir=/usr/lib/nautilus \
      --disable-update-mimedb \
      --disable-packagekit
  make
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain nautilus ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}

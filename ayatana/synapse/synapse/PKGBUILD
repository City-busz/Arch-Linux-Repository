# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=synapse
pkgver=0.2.4.1
pkgrel=1
pkgdesc="A semantic launcher written in Vala that you can use to start applications as well as find and access relevant documents and files by making use of the Zeitgeist engine"
arch=('i686' 'x86_64')
url="https://launchpad.net/synapse-project"
license=('GPL')
depends=('gtkhotkey' 'libgee>=0.5.2' 'libnotify>=0.7.0' 'libunique' 'indicator-application>=0.0.7' 'librest' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('intltool>=0.35' 'vala>=0.10.4')
install=$pkgname.install
source=(http://launchpad.net/synapse-project/0.2/$pkgver/+download/$pkgname-$pkgver.tar.gz
        zeitgeist-fix.patch)
md5sums=('f8692bb67aff43b55b8421d1ab3d09c9'
         'ef0cba272881d8b63095298808b158f0')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np0 -i "$srcdir/zeitgeist-fix.patch"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --disable-zeitgeist
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=xnoise
pkgver=0.1.16
pkgrel=1
pkgdesc="Media player for Gtk+ with a slick GUI, great speed and lots of features"
arch=('i686' 'x86_64')
url="http://xnoise-media-player.com/"
license=('GPL')
depends=('gstreamer0.10-base-plugins' 'taglib>=1.5.0' 'libunique' 'libsoup>=2.26.0' 'libnotify>=0.7.0' 'libindicate>=0.4.2' 'desktop-file-utils' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('vala>=0.11.5' 'intltool>=0.35.0')
optdepends=('gstreamer0.10-good-plugins: Extra media codecs'
            'gstreamer0.10-ugly-plugins: Extra media codecs'
            'gstreamer0.10-bad-plugins: Extra media codecs'
            'gstreamer0.10-ffmpeg: Extra media codecs')
options=(!libtool)
install=$pkgname.install
source=(http://xnoise.googlecode.com/files/$pkgname-$pkgver.tar.bz2
        xnoise-0.1.16-libnotify-0.7.patch)
md5sums=('b99a610d4ee73399b20cf87c420c12e1'
         '80d3da0cc4091bdcbbabc5bb51358283')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i "$srcdir/xnoise-0.1.16-libnotify-0.7.patch"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

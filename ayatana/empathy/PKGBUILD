# $Id: PKGBUILD 112778 2011-03-06 17:05:25Z ibiru $
# Maintainer: Ionut Biru <ibiru@archlinux.org>
pkgname=empathy
pkgver=2.32.2
pkgrel=2.1
pkgdesc="A GNOME instant messaging client using the Telepathy framework."
arch=('i686' 'x86_64')
url="http://live.gnome.org/Empathy"
license=('GPL2')
depends=('telepathy-glib>=0.12.4' 'telepathy-mission-control>=5.6.1' 'folks>=0.2.1' 'telepathy-logger>=0.1.5' 'telepathy-farsight>=0.0.15'
         'gnome-keyring>=2.32.0' 'gnutls' 'gconf>=2.32.0' 'evolution-data-server>=2.32.1' 'libcanberra>=0.25' 'libnotify>=0.7.0' 'libunique'
         'enchant' 'iso-codes' 'libwebkit' 'dconf>=0.5.1' 'ca-certificates' 'libindicate>=0.3.1')
makedepends=('intltool' 'gnome-doc-utils>=0.20.1' 'nautilus-sendto')
optdepends=('telepathy-gabble: XMPP/Jabber support'
            'telepathy-butterfly: MSN support'
            'telepathy-idle: IRC support'
            'telepathy-salut: Link-local XMPP support'
            'telepathy-sofiasip: SIP support'
            'telepathy-haze: libpurple support')
options=('!libtool')
groups=('gnome-extra')
install=empathy.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/2.32/${pkgname}-${pkgver}.tar.bz2
        empathy-2.32.2-libnotify-0.7.patch
        20_libindicate.patch
        21_login_indicators.patch
        22_dont_display_notifications_for_pending_messages.patch
        22_tell_if_the_message_is_a_pending_one_or_not.patch
        31_really_raise_window.patch
        34_start_raised_execpt_in_session.patch)
sha256sums=('f90a45955205fd8bd783a55acae4603197caab4f16a880106f8639cbbbe5e861'
            'd3bcf1af732f0208bc0857efefd334a2df3f0be602690b4bf4b8547d73362f7b'
            'ffbe6458033a3565c670a36783258f347074df3b7bcf07d7c8f023be69d82dd0'
            '134526e062876825a1115a4562f70f40a0c78fe56f5eedac97994a4419a80710'
            'da17e298240a23a46fe95c8b7cc62caaa1d3b92d0475d26721915f52432e70cf'
            'b2ac71cc39c8b81648c6a821269b58121d4d440fceae45d684821e44b5482cc1'
            '4d8ce9232f5ef08a56e8e4cd9a0fd81c32a0522753cc4d84765d39fff58ce8fa'
            'cb74cc6496e571b084d259f3545bbc9ce83e55e0361cf22107292f11a193cf78')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np1 -i "$srcdir/empathy-2.32.2-libnotify-0.7.patch"
  patch -Np1 -i "$srcdir/20_libindicate.patch"
  patch -Np1 -i "$srcdir/21_login_indicators.patch"
  patch -Np1 -i "$srcdir/22_dont_display_notifications_for_pending_messages.patch"
  patch -Np1 -i "$srcdir/22_tell_if_the_message_is_a_pending_one_or_not.patch"
  patch -Np1 -i "$srcdir/31_really_raise_window.patch"
  patch -Np1 -i "$srcdir/34_start_raised_execpt_in_session.patch"
  autoreconf -fi
  ./configure --prefix=/usr \
      --sysconfdir=/etc  \
      --libexecdir=/usr/lib/empathy \
      --disable-schemas-compile --disable-map --disable-location --with-connectivity=no
  make
  make DESTDIR="${pkgdir}" install

  mkdir -p $pkgdir/usr/share/indicators/messages/applications
  echo '/usr/share/applications/empathy.desktop' > $pkgdir/usr/share/indicators/messages/applications/empathy
}

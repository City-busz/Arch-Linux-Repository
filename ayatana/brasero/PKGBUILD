# $Id: PKGBUILD,v 1.31 2009/03/14 02:03:57 allan Exp $
# Maintainer: Allan McRae <allan@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=brasero
pkgver=2.32.1
pkgrel=1.1
pkgdesc="A disc burning application for Gnome"
arch=('i686' 'x86_64')
url="http://www.gnome.org/projects/brasero"
license=('GPL')
options=('!libtool' '!emptydirs')
depends=('gstreamer0.10-base-plugins>=0.10.30' 'totem-plparser>=2.32.0' 'libbeagle>=0.3.9' 'cdrkit' 'cdrdao' 'desktop-file-utils' 'hicolor-icon-theme' 'libunique>=1.1.6' 'libcanberra>=0.25' 'dvd+rw-tools' 'dconf' 'indicator-application>=0.0.7')
makedepends=('gnome-doc-utils>=0.20.1' 'intltool' 'libburn' 'libisofs' 'nautilus' 'gobject-introspection' 'namcap' 'gtk-doc' 'gnome-common')
optdepends=('libburn: alternative back-end'
            'libisofs: libburn back-end'
            'dvdauthor: video project'
            'gstreamer0.10-bad-plugins: video project'
            'vcdimager: video project')
replaces=('nautilus-cd-burner')
install=brasero.install
source=(http://ftp.gnome.org/pub/gnome/sources/brasero/2.32/brasero-${pkgver}.tar.bz2
        012_appindicator.patch)
sha256sums=('7b1056b6f19398912d51e3e583bc1b4d2a75ce2a8e15a96e1514fe6edb79df3b'
            'f4d362bf18b23d1bfe6e473cbf3548e4693f186385cad019380296d2b943273d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "$srcdir/012_appindicator.patch"
  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-caches \
      --disable-schemas-compile
  make
  make DESTDIR=${pkgdir} install
}

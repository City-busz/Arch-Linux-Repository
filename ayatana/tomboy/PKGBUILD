# $Id: PKGBUILD 103907 2010-12-23 17:41:14Z ibiru $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=tomboy
pkgver=1.4.2
pkgrel=1.1
pkgdesc="Desktop note-taking application for Linux and Unix"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org/projects/tomboy/"
depends=('gtkspell' 'gmime>=2.4.21' 'ndesk-dbus-glib>=0.4.1' 'gnome-sharp>=2.24.1' 'mono-addins>=0.5' 'libsm' 'gnome-desktop-sharp>=2.26.0' 'hicolor-icon-theme' 'indicator-application')
makedepends=('intltool' 'pkgconfig' 'gnome-panel-bonobo' 'gnome-doc-utils>=0.20.0')
optdepends=('gnome-panel-bonobo: applet support')
options=('!libtool' '!emptydirs' '!makeflags')
groups=('gnome-extra')
install=tomboy.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/1.4/${pkgname}-${pkgver}.tar.bz2
        04_app_indicator.patch)
sha256sums=('823270473c819d6592074a0dcd41c29fd857b29a0318061aaabfa38fff3cc7e7'
            '6c4a0a3214dc19e8ead40f155711d6f43bed73a0625bf22c14b0e11c931d748a')

build() {
  export MONO_SHARED_DIR="${srcdir}/.wabi"
  mkdir -p "${MONO_SHARED_DIR}"

  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "$srcdir/04_app_indicator.patch"
  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-scrollkeeper \
      --disable-update-mimedb --enable-panel-applet
  make
  make DESTDIR="${pkgdir}" GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain tomboy ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}

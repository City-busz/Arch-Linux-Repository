# $Id: PKGBUILD 112788 2011-03-06 19:49:19Z ibiru $
# Maintainer:
# Contributor: Roman Kyrylych <roman@archlinux.org>

pkgname=gnome-bluetooth
pkgver=2.32.0
pkgrel=2.1
pkgdesc="The GNOME Bluetooth Subsystem"
arch=('i686' 'x86_64')
url="http://live.gnome.org/GnomeBluetooth"
license=('GPL' 'LGPL')
depends=('libunique>=1.1.6' 'libnotify>=0.7.1' 'hicolor-icon-theme' 'gvfs-obexftp>=1.6.3' 'obexd-client>=0.33' 'dconf' 'indicator-application>=0.0.7')
makedepends=('intltool' 'pygtk>=2.22' 'gnome-doc-utils>=0.20.1' 'nautilus-sendto>=2.28.4' 'gobject-introspection>=0.9.6' 'gtk-doc' 'gnome-common')
replaces=('bluez-gnome')
conflicts=('bluez-gnome')
options=('!libtool' '!emptydirs' '!makeflags')
install=gnome-bluetooth.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/2.32/${pkgname}-${pkgver}.tar.bz2
        61-gnome-bluetooth-rfkill.rules
        gnome-bluetooth-2.32.0-libnotify-0.7.patch
        gir-fix.patch
        01_use_app_indicator.patch)
sha256sums=('57b1f06c96a1b85e1c19ff919d708cc38e95edae658881ed99968c325839a973'
            'b8acb8ea2e7f3588575cffd8ea14ec50c8641f518f2ea899771a508b299ea474'
            '0e3313ede6901f882564ddfa8a08747044ec9b4e807a3cba734510a1d5387310'
            '47fb722c8ec433141c5337e78f84397e41be80660861c1621d1774367096bb51'
            '62ee719dfaa5a47a2da0cb85dd97c5b93c0e260df788eea0f8180e235f0257d8')
build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "$srcdir/gir-fix.patch"
  patch -Np1 -i "$srcdir/gnome-bluetooth-2.32.0-libnotify-0.7.patch"
  patch -Np1 -i "$srcdir/01_use_app_indicator.patch"
  autoreconf -fi
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-desktop-update \
    --disable-icon-update \
    --disable-schemas-compile
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m644 -D "${srcdir}/61-gnome-bluetooth-rfkill.rules" \
                     "${pkgdir}/lib/udev/rules.d//61-gnome-bluetooth-rfkill.rules"
}

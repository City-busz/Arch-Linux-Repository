# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=evolution-indicator
pkgver=0.2.10
pkgrel=1
pkgdesc="A plugin for Evolution that uses libindicate and libnotify to provide additional information to the shell about Evolution's state."
arch=('i686' 'x86_64')
url="https://launchpad.net/evolution-indicator"
license=('GPL')
groups=('ayatana')
depends=('libindicate>=0.3.0' 'evolution>=2.32.0')
makedepends=('intltool')
options=('!libtool')
install=$pkgname.install
source=(http://launchpad.net/$pkgname/${pkgver%.*}.0/$pkgver/+download/$pkgname-$pkgver.tar.gz
        launchpad-export.tar.gz)
md5sums=('0eb41453c4952bff84cfe31e9adc493f'
         'c229e7cd84eb80f59764da5de5e700de')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  rename $pkgname- '' ../po/$pkgname-*.po
  mv -f -t po ../po/*
  echo 'af am an ar ast be bg bn br bs ca cv cy cs csb da de el en_AU en_CA en_GB eo es et eu fa fi fr gl gu he hi hr hu id is it ja ka kk km kn ko ku lt lv mk ml mr ms nb ne nl nn oc pl pt pt_BR ro ru sc sd si sk sl sq sr sv tet th tr ug uk vi zh_CN zh_HK zh_TW' >po/LINGUAS

  sed -i 's/gtkhtml-editor/gtkhtml-editor-3.14/' configure configure.ac

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib \
              --disable-static --disable-schemas-install \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  mkdir -p $pkgdir/usr/share/indicators/messages/applications
  echo '/usr/share/applications/evolution.desktop' > $pkgdir/usr/share/indicators/messages/applications/evolution
}

# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=indicator-keylock
pkgver=1.3.0
pkgrel=2
pkgdesc="An indicator that displays the status of the keyboard lock keys"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-keylock"
license=('GPL')
depends=('gconf>=2.28.0' 'indicator-application>=0.0.19' 'libunique>=1.1.6' 'libnotify>=0.7.0' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('intltool>=0.23')
install=$pkgname.install
source=(https://launchpad.net/~tsbarnes/+archive/$pkgname/+files/${pkgname}_$pkgver-0~ppa0~maverick1.tar.gz
        https://launchpad.net/~tsbarnes/+archive/$pkgname/+files/$pkgname-icons_2~0ppa0.tar.gz
        indicator-keylock-1.3.0-libnotify-0.7.patch)
md5sums=('53ccfb5414c5f3aea00d8eb78bda5372'
         '0a9ecd6ec1ec9a491e7768f53044b8ad'
         '1a85b9d48fd13ee4d4bd9d0a115de895')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i "$srcdir/indicator-keylock-1.3.0-libnotify-0.7.patch"

  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-schemas-install \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # Install more icons
  cd "$srcdir/$pkgname-icons-2~0ppa0"
  make DESTDIR="$pkgdir/usr" install
}

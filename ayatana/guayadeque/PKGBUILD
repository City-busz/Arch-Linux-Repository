# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=guayadeque
pkgver=0.2.9
pkgrel=1
pkgdesc="Music management program designed for all music enthusiasts"
arch=('i686' 'x86_64')
url="http://guayadeque.org/"
license=('GPL')
depends=('gstreamer0.10-base-plugins' 'curl' 'taglib>=1.6.1' 'flac' 'libgpod' 'libindicate' 'wxgtk')
makedepends=('cmake')
optdepends=('gstreamer0.10-good-plugins: Extra media codecs'
            'gstreamer0.10-ugly-plugins: Extra media codecs'
            'gstreamer0.10-bad-plugins: Extra media codecs'
            'gstreamer0.10-ffmpeg: Extra media codecs')
install=$pkgname.install
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('30f0d5fc1ce950f4e9eb697a48dcf025')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

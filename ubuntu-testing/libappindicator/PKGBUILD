# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=libappindicator
pkgver=0.2.91.1
pkgrel=1
pkgdesc="A library to allow applications to export a menu into the Unity Menu bar"
arch=('i686' 'x86_64')
url="https://launchpad.net/libappindicator"
license=('LGPL')
depends=('libindicator>=0.3.5' 'libdbusmenu>=0.3.90')
makedepends=('gobject-introspection>=0.10' 'vala' 'gtk-sharp-2>=2.12.1' 'pygtk>=2.14.0')
conflicts=('indicator-application<0.2.91')
options=('!libtool')
source=(http://launchpad.net/$pkgname/0.3/${pkgver%.*}/+download/$pkgname-$pkgver.tar.gz)
md5sums=('08905f7c4cad1b6073a63cc7882b6568')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's/nunit >= 2\.4\.7/mono-nunit >= 2\.4\.7/' configure{,.ac}
  sed -i 's/-lpyglib-2\.0-python2\.6/-lpyglib-2\.0-python2/' bindings/python/Makefile{.am,.in}

  #fix mono bindings location
  sed -i 's|assemblydir = $(libdir)/cli/appindicator-sharp-0.1|assemblydir = $(libdir)/appindicator-sharp-0.1|' bindings/mono/Makefile{.am,.in}
  sed -i 's|assemblies_dir=${libdir}/cli/appindicator-sharp-0.1|assemblies_dir=${libdir}/appindicator-sharp-0.1|' bindings/mono/appindicator-sharp-0.1.pc.in

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static \
              PYTHON=python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

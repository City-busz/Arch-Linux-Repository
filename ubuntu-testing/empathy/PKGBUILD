# $Id: PKGBUILD 99876 2010-11-18 22:38:17Z ibiru $
# Maintainer: Ionut Biru <ibiru@archlinux.org>
pkgname=empathy
pkgver=2.32.2
pkgrel=1.3
pkgdesc="A GNOME instant messaging client using the Telepathy framework."
arch=('i686' 'x86_64')
url="http://live.gnome.org/Empathy"
license=('GPL2')
depends=('telepathy-glib>=0.12.4' 'telepathy-mission-control>=5.6.1' 'folks>=0.2.1' 'telepathy-logger>=0.1.5' 'telepathy-farsight>=0.0.15'
         'gnome-keyring>=2.32.0' 'gnutls' 'gconf>=2.32.0' 'evolution-data-server>=2.32.1' 'libcanberra>=0.25' 'libnotify' 'libunique'
         'enchant' 'iso-codes' 'libwebkit' 'dconf>=0.5.1' 'ca-certificates' 'libindicate>=0.4.91')
makedepends=('intltool' 'gnome-doc-utils>=0.20.1' 'nautilus-sendto')
optdepends=('telepathy-gabble: XMPP/Jabber support'
            'telepathy-butterfly: MSN support'
            'telepathy-idle: IRC support'
            'telepathy-salut: Link-local XMPP support'
            'telepathy-sofiasip: SIP support'
            'telepathy-haze: libpurple support')
options=('!libtool')
groups=('gnome-extra')
install=empathy.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/2.32/${pkgname}-${pkgver}.tar.bz2
        20_libindicate.patch
        21_login_indicators.patch
        22_dont_display_notifications_for_pending_messages.patch
        22_tell_if_the_message_is_a_pending_one_or_not.patch
        31_really_raise_window.patch
        34_start_raised_execpt_in_session.patch)
sha256sums=('f90a45955205fd8bd783a55acae4603197caab4f16a880106f8639cbbbe5e861'
            '4f892162226d95144c1928ed13a049d6460d73aab7c035967be0a9c679c3ba53'
            '134526e062876825a1115a4562f70f40a0c78fe56f5eedac97994a4419a80710'
            'da17e298240a23a46fe95c8b7cc62caaa1d3b92d0475d26721915f52432e70cf'
            'b2ac71cc39c8b81648c6a821269b58121d4d440fceae45d684821e44b5482cc1'
            'a8929d1da61a894d29bbced3f42fb19e44f70347762516bc97cc06a9d38bdc02'
            'cb74cc6496e571b084d259f3545bbc9ce83e55e0361cf22107292f11a193cf78')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i "$srcdir/20_libindicate.patch"
  patch -p1 -i "$srcdir/21_login_indicators.patch"
  patch -p1 -i "$srcdir/22_dont_display_notifications_for_pending_messages.patch"
  patch -p1 -i "$srcdir/22_tell_if_the_message_is_a_pending_one_or_not.patch"
  patch -p1 -i "$srcdir/31_really_raise_window.patch"
  patch -p1 -i "$srcdir/34_start_raised_execpt_in_session.patch"

  autoreconf -fi
  ./configure --prefix=/usr \
      --sysconfdir=/etc  \
      --libexecdir=/usr/lib/empathy \
      --disable-schemas-compile --disable-map --disable-location --with-connectivity=no
  make
  make DESTDIR="${pkgdir}" install

  mkdir -p $pkgdir/usr/share/indicators/messages/applications
  echo '/usr/share/applications/empathy.desktop' > $pkgdir/usr/share/indicators/messages/applications/empathy
}

# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=compiz-core-glibmainloop-git
pkgver=20110131
pkgrel=1
pkgdesc="Composite manager for Aiglx and Xgl"
arch=('i686' 'x86_64')
url="http://www.compiz.org/"
license=('GPL' 'LGPL' 'MIT')
depends=('boost-libs' 'mesa' 'gnome-control-center' 'libwnck' 'libxslt' 'gnome-desktop' 'glibmm' 'libsigc++')
makedepends=('git' 'cmake' 'boost' 'intltool')
provides=('compiz-core' 'compiz-core-glibmainloop')
conflicts=('compiz-core' 'compiz-core-glibmainloop')
install=$pkgname.install

_gitroot="git://anongit.compiz.org/users/dbo/compiz-with-glib-mainloop"
_gitname="compiz-with-glib-mainloop"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  #
  # BUILD HERE
  #

  mkdir build
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -DCOMPIZ_DESTDIR="$pkgdir/"
  make
}

package() {
  cd "$srcdir/$_gitname-build/build"

  make findcompiz_install
  make install

  gconf-merge-schema "$srcdir/compiz-core.schemas" "$pkgdir/usr/share/gconf/schemas/*.schemas"
  sed -i 's|<schemalist/>||g' "$srcdir/compiz-core.schemas"

  rm "$pkgdir"/usr/share/gconf/schemas/*.schemas
  mv "$srcdir/compiz-core.schemas" "$pkgdir/usr/share/gconf/schemas"

  install -Dm644 "$srcdir/compiz-with-glib-mainloop-build/COPYING.MIT" "$pkgdir/usr/share/licenses/$pkgname/COPYING.MIT"
}

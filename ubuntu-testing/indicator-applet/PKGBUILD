# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=indicator-applet
pkgver=0.4.9
pkgrel=1
pkgdesc="A small applet to display information from various applications consistently in the panel"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-applet"
license=('GPL')
groups=('ayatana')
depends=('gnome-panel' 'libindicator>=0.3.18' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('intltool>=0.35.0')
install=$pkgname.install
source=(http://launchpad.net/$pkgname/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.gz)
md5sums=('6f0c1c402c40560ed0d6f854b3586353')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  #rename FastUserSwitchApplet to avoid conflict with GDM
  mv data/org.ayatana.panel.FastUserSwitchApplet.panel-applet.in.in data/org.ayatana.panel.IndicatorAppletSession.panel-applet.in.in
  mv data/org.gnome.panel.applet.FastUserSwitchAppletFactory.service.in data/org.gnome.panel.applet.IndicatorAppletSessionFactory.service.in
  find -type f | xargs sed -i 's/FastUserSwitchApplet/IndicatorAppletSession/'

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib \
              --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=evolution-indicator
pkgver=0.2.14
pkgrel=1
pkgdesc="A plugin for Evolution that uses libindicate and libnotify to provide additional information to the shell about Evolution's state"
arch=('i686' 'x86_64')
url="https://launchpad.net/evolution-indicator"
license=('GPL')
groups=('ayatana')
depends=('libindicate>=0.4.91' 'evolution>=2.32.0')
makedepends=('intltool>=0.34')
options=('!libtool')
install=$pkgname.install
source=(http://launchpad.net/$pkgname/${pkgver%.*}.0/$pkgver/+download/$pkgname-$pkgver.tar.gz)
md5sums=('aa6423e3938dc0435818648aed8eb056')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib \
              --disable-static --disable-schemas-install \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  mkdir -p $pkgdir/usr/share/indicators/messages/applications
  echo '/usr/share/applications/evolution.desktop' > $pkgdir/usr/share/indicators/messages/applications/evolution
}

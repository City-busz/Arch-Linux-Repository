# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=indicator-network
pkgver=0.3.4
pkgrel=1
pkgdesc="Network Menu for controlling network connections. Uses Connection Manager."
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-network"
license=('GPL')
groups=('ayatana')
depends=('libindicator>=0.3.0' 'libdbusmenu>=0.3.90' 'libnotify>=0.4.5' 'connman' 'dbus-python' 'pygtk')
makedepends=('intltool>=0.35.0')
options=(!libtool)
source=(http://launchpad.net/$pkgname/trunk/$pkgver/+download/$pkgname-$pkgver.tar.gz)
md5sums=('a0e8b7683c6d582db1e58e7398494a27')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's@^#!.*python$@#!/usr/bin/python2@'  scripts/*

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib \
              --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

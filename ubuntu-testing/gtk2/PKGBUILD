# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=gtk2
pkgname=('gtk2' 'gtk-update-icon-cache')
pkgver=2.24.0
pkgrel=1
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
makedepends=('atk' 'pango' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage' 'heimdal' 'gnutls'
             'shared-mime-info' 'cairo' 'libcups' 'gdk-pixbuf2' 'gobject-introspection' 'gtk-doc' 'namcap')
options=('!libtool' '!docs')
license=('LGPL')
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/2.24/gtk+-${pkgver}.tar.bz2
        xid-collision-debug.patch old-icon-symlinks.patch
        012_ubuntu-set-grab-add.patch
        043_ubuntu_menu_proxy.patch
        065_gir_set_packages.patch
        072_indicator_menu_update.patch
        091_bugzilla_tooltip_refresh.patch)
sha256sums=('cbed1a7b8cd1e471388a00f22557dd061334698a0c1aece11b7ed6541d115606'
            'd758bb93e59df15a4ea7732cf984d1c3c19dff67c94b957575efea132b8fe558'
            '1298e4103f71d0304378f1e7503011150f6f68398ae8ebae5522a48feaea7c0d'
            '4d0c741461b55e70bee78fe54b7a15025a2617d07158f53d3ba49b41ac014ab3'
            '9cfffffcad029eb56786bf83a93d4fefc872d58d14d61b659989f26340358f9d'
            '8fd465260c814071439f49ebbcf24129615bf2ed97077a010701840d7c23a3ed'
            '2e288dd89adf5b940b7284465a8913025f41dfda4d032a1c25fab9d2e9192ed0'
            'b459a74d3f6dc59d6e2cf03d136a6fa8038e604eddf0b5679d00bb771b6639b3')

build() {
    cd "${srcdir}/gtk+-${pkgver}"
    patch -Np1 -i "${srcdir}/xid-collision-debug.patch"
    patch -Np1 -i "${srcdir}/old-icon-symlinks.patch"
    patch -Np1 -i "${srcdir}/012_ubuntu-set-grab-add.patch"
    patch -Np1 -i "${srcdir}/043_ubuntu_menu_proxy.patch"
    patch -Np1 -i "${srcdir}/065_gir_set_packages.patch"
    patch -Np1 -i "${srcdir}/072_indicator_menu_update.patch"
    patch -Np1 -i "${srcdir}/091_bugzilla_tooltip_refresh.patch"

    automake
    CXX=/bin/false ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-xinput=yes
    make
}
package_gtk2() {
    pkgdesc="The GTK+ Toolkit (v2)"
    install=gtk2.install
    depends=('atk' 'pango' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage' 'heimdal' 'gnutls' 'shared-mime-info' 'cairo' 'libcups' 'gtk-update-icon-cache')
    backup=(etc/gtk-2.0/gtkrc)

    cd "${srcdir}/gtk+-${pkgver}"

    make DESTDIR="${pkgdir}" install
    sed -i "s#env python#env python2#" $pkgdir/usr/bin/gtk-builder-convert
    echo 'gtk-fallback-icon-theme = "gnome"' > "${pkgdir}/etc/gtk-2.0/gtkrc"
    #split this out to use with gtk3 too
    rm ${pkgdir}/usr/bin/gtk-update-icon-cache
}
package_gtk-update-icon-cache() {
    pkgdesc="The GTK+ update icon cache tool"
    depends=('gdk-pixbuf2')

    cd "${srcdir}/gtk+-${pkgver}/gtk"

    install -D -m755 gtk-update-icon-cache ${pkgdir}/usr/bin/gtk-update-icon-cache
}

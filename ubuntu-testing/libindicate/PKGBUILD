# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=libindicate
pkgver=0.4.93
pkgrel=1
pkgdesc="A small library for applications to raise "flags" on DBus for other components of the desktop to pick up and visualize"
arch=('i686' 'x86_64')
url="https://launchpad.net/libindicate"
license=('LGPL')
depends=('libdbusmenu>=0.3.90' 'dbus-glib>=0.76')
makedepends=('vala' 'gtk-sharp-2>=2.12.1' 'pygtk>=2.14.0' 'gobject-introspection>=0.6.7')
options=(!libtool)
source=(http://launchpad.net/$pkgname/0.5/$pkgver/+download/$pkgname-$pkgver.tar.gz)
md5sums=('c01d18d1b0e75358e113e1934150e97e')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  sed -i 's/-lpyglib-2\.0-python$PYTHON_VERSION/-lpyglib-2\.0-python2/' configure{,.ac}
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' examples/*

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib \
              --disable-static \
              PYTHON=python2 PYTHON_VERSION=2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

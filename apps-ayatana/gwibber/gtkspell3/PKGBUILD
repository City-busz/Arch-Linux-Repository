# Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Ben <ben@benmazer.net>

pkgname=gtkspell3
_pkgname=gtkspell
pkgver=2.0.16
pkgrel=1
url="http://gtkspell.sourceforge.net/"
pkgdesc="GtkSpell provides word-processor-style highlighting and replacement of misspelled words in a GtkTextView widget"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtk3>=3.0' 'gtkspell')
makedepends=('intltool' 'gtk-doc')
options=('!libtool')
source=(http://gtkspell.sourceforge.net/download/$_pkgname-$pkgver.tar.gz
        gtk3_support.patch)
md5sums=('f75dcc9338f182c571b321d37c606a94'
         'b9a12bf6e0340dec1764b9ae6d5d4f13')

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  patch -Np1 -i $srcdir/gtk3_support.patch

  autoreconf -fi
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  make DESTDIR="$pkgdir" install-exec install-data-am
}

# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=audio-recorder
pkgver=0.6.2
pkgrel=1
pkgdesc="Audio recorder that allows you to record your favourite music and audio to a file"
arch=('i686' 'x86_64')
url="https://launchpad.net/audio-recorder"
license=(GPL)
depends=(gstreamer0.10-base-plugins libpulse libgnome-media-profiles dbus-glib libappindicator3 dconf hicolor-icon-theme xdg-utils)
makedepends=('intltool')
optdepends=('gstreamer0.10-good-plugins: Extra media codecs'
            'gstreamer0.10-ugly-plugins: Extra media codecs'
            'gstreamer0.10-bad-plugins: Extra media codecs'
            'gstreamer0.10-ffmpeg: Extra media codecs')
install=$pkgname.install
source=(https://launchpad.net/~osmoma/+archive/$pkgname/+files/${pkgname}_${pkgver}~oneiric.tar.gz
        no-autostart.patch)
md5sums=(8bf5e6e2d9236110b96c93e121abf675
         9f13895d60ece28484299367524fe3e6)

build() {
  cd "$srcdir/$pkgname"
  patch -Np1 -i "$srcdir/no-autostart.patch"

  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd "$srcdir/$pkgname"

  make DESTDIR="$pkgdir/" install
}

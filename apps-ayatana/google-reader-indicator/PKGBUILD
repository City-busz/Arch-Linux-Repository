# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=google-reader-indicator
pkgver=0.1.1.2
pkgrel=1
_pkgrel=1ubuntu1
pkgdesc="A google reader indicator"
arch=('any')
url="https://launchpad.net/google-reader-indicator"
license=('GPL')
depends=('dbus-python' 'python2-gobject' 'python-gnomekeyring' 'python-simplejson' 'python-lxml' 'libappindicator3' 'libnotify' 'hicolor-icon-theme' 'xdg-utils')
install=$pkgname.install
source=(https://launchpad.net/~atareao/+archive/atareao/+files/${pkgname}_$pkgver-$_pkgrel.tar.gz)
md5sums=('15583faae6e3ff7a5578181517e3b9de')

build() {
  cd "$srcdir/$pkgname"

  sed -i 's@^#!.*python$@#!/usr/bin/python2@' src/*.py
  sed -i "s|LANGDIR =.*|LANGDIR = '/usr/share/locale'|" src/comun.py
  sed -i 's/lsb_release -a/uname -sr/' src/machine_information.py
}

package() {
  cd "$srcdir/$pkgname"
  # Build the sctructure
  mkdir -p "$pkgdir"/usr/{bin,share/{google-reader-indicator,applications,icons/hicolor/scalable/status,pixmaps}}
  mkdir -p "$pkgdir"/usr/share/locale/{en,fr,de,it,gl,es}/LC_MESSAGES

  # Copy files
  cp bin/* "$pkgdir/usr/bin"
  cp src/* "$pkgdir/usr/share/google-reader-indicator"
  cp data/google-reader-indicator-autostart.desktop "$pkgdir/usr/share/google-reader-indicator"
  cp data/Google-Reader-Indicator.desktop "$pkgdir/usr/share/applications"
  cp data/icons/* "$pkgdir/usr/share/icons/hicolor/scalable/status"
  cp data/icons/google-reader-indicator.svg "$pkgdir/usr/share/pixmaps/"

  # Build language files
  msgfmt template1/en.po -o "$pkgdir/usr/share/locale/en/LC_MESSAGES/google-reader-indicator.mo"
  msgfmt template1/fr.po -o "$pkgdir/usr/share/locale/fr/LC_MESSAGES/google-reader-indicator.mo"
  msgfmt template1/de.po -o "$pkgdir/usr/share/locale/de/LC_MESSAGES/google-reader-indicator.mo"
  msgfmt template1/it.po -o "$pkgdir/usr/share/locale/it/LC_MESSAGES/google-reader-indicator.mo"
  msgfmt template1/gl.po -o "$pkgdir/usr/share/locale/gl/LC_MESSAGES/google-reader-indicator.mo"
  msgfmt template1/es.po -o "$pkgdir/usr/share/locale/es/LC_MESSAGES/google-reader-indicator.mo"
}

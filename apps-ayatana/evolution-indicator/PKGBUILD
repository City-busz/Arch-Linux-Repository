# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=evolution-indicator
pkgver=0.2.20
pkgrel=1
pkgdesc="A plugin for Evolution that uses libindicate and libnotify to provide additional information to the shell about Evolution's state"
arch=('i686' 'x86_64')
url="https://launchpad.net/evolution-indicator"
license=('GPL')
depends=('evolution>=3.2' 'libindicate>=0.5.90' 'libunity')
makedepends=('intltool>=0.34')
options=('!libtool')
install=$pkgname.install
source=(http://launchpad.net/$pkgname/${pkgver%.*}.0/$pkgver/+download/$pkgname-$pkgver.tar.gz)
md5sums=('9902c23b21bb8ea6c7ec42d58a2c6eb8')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static --disable-schemas-install \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  mkdir -p $pkgdir/usr/share/indicators/messages/applications
  echo '/usr/share/applications/evolution.desktop' > $pkgdir/usr/share/indicators/messages/applications/evolution
}

# Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: Eric Bélanger <eric@archlinux.org>

_pkgname=liferea
pkgname=liferea-unity
pkgver=1.6.6b
pkgrel=1
pkgdesc="A desktop news aggregator for online news feeds and weblogs with Unity integration"
arch=('i686' 'x86_64')
url="http://liferea.sourceforge.net/"
license=('GPL')
depends=('gconf' 'libwebkit' 'libglade' 'networkmanager' 'libindicate-gtk2' 'libunity' 'hicolor-icon-theme')
makedepends=('pkg-config' 'intltool' 'lua' 'libnotify')
optdepends=('lua: for lua scripting support'
            'libnotify: for notification support')
provides=("liferea=$pkgver")
conflicts=('liferea')
options=('!libtool' '!emptydirs')
install=liferea.install
source=(http://downloads.sourceforge.net/sourceforge/liferea/liferea-${pkgver}.tar.gz
        fix_systray_behavior
        notification-append
        gtk-status-icon.patch
        libindicate.patch
        libunity.patch
        libnotify.patch)
md5sums=('1db10281f5ade2fa350f2a245733a74e'
         '5398836c10135da8903c26f19666e299'
         'f1b8fc2afe1642da70f13275c4043d9e'
         '84a7e8a4ee8ba787995751c988174f4f'
         'fa28781f1ec3fa406531d722fb6dca70'
         'b1c5b8de8aa87576ed36ab443a8dad35'
         '8bb78e9021681874207117d3a3293aa7')

build() {
  cd "${srcdir}/liferea-${pkgver}"
  patch -Np1 -i "$srcdir/fix_systray_behavior"
  patch -Np1 -i "$srcdir/notification-append"
  patch -Np1 -i "$srcdir/gtk-status-icon.patch"
  patch -Np1 -i "$srcdir/libindicate.patch"
  patch -Np1 -i "$srcdir/libunity.patch"
  patch -Np1 -i "$srcdir/libnotify.patch"

  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --disable-schemas-install
  make
}

package() {
  cd "${srcdir}/liferea-${pkgver}"
  make DESTDIR="${pkgdir}" install
  
  install -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${_pkgname}.schemas" "${pkgdir}"/etc/gconf/schemas/*.schemas
  rm -f "${pkgdir}"/etc/gconf/schemas/*.schemas

  mkdir -p "$pkgdir/usr/share/indicators/messages/applications"
  echo '/usr/share/applications/liferea.desktop' > "$pkgdir/usr/share/indicators/messages/applications/liferea"
}

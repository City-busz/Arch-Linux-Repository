# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=indicator-keylock
pkgver=1.6.1
pkgrel=1
pkgdesc="An indicator that displays the status of the keyboard lock keys"
arch=(i686 x86_64)
url="https://launchpad.net/indicator-keylock"
license=(GPL)
depends=(gconf libappindicator libunique libnotify hicolor-icon-theme xdg-utils)
makedepends=(intltool)
install=$pkgname.install
source=(https://launchpad.net/~tsbarnes/+archive/$pkgname/+files/${pkgname}_$pkgver-1~ppa0.tar.gz
        https://launchpad.net/~tsbarnes/+archive/$pkgname/+files/$pkgname-icons_2~0ppa1~natty.tar.gz
        indicator-keylock-1.3.0-libnotify-0.7.patch)
md5sums=(7f7527194a7e82c2a645b59472e39f46
         59bd1925e2aef984a09e55b66d38ad67
         d5dca623b7e8470842fafa3b1a77b97f)

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i "$srcdir/indicator-keylock-1.3.0-libnotify-0.7.patch"

  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-schemas-install \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # Install more icons
  cd "$srcdir/$pkgname-icons-2~0ppa1~natty"
  make DESTDIR="$pkgdir/usr" install
}

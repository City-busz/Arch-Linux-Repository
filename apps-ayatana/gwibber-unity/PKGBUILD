# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=gwibber-unity
_pkgname=gwibber
pkgver=3.2.1
pkgrel=2
pkgdesc="Microblogging client for GNOME, which supports Twitter, Identi.ca, StatusNet, Facebook, Flickr, Digg, FriendFeed and Qaiku with Unity integration"
arch=(i686 x86_64)
url="http://gwibber.com/"
license=(GPL)
depends=(gtkspell3 'libunity>=4.0.0' dbus-python gnome-keyring python-gnomekeyring python-notify python-wnck python-egenix-mx-base python2-oauth python-imaging python-pycurl python-simplejson pywebkitgtk pyxdg xdg-utils)
makedepends=('vala>=0.14' 'intltool>=0.35.0' 'gobject-introspection>=0.10')
optdepends=('libindicate: indicator support')
provides=("gwibber=$pkgver")
conflicts=('gwibber')
options=(!libtool)
install=$_pkgname.install
source=(http://launchpad.net/$_pkgname/3.2/$pkgver/+download/$_pkgname-$pkgver.tar.gz
        launchpad-export.tar.gz
        lp_861903.patch
        lp_882633.patch
        lp_884831.patch
        gwibber-vala-0.14.patch)
md5sums=(8e245072f8b43852ef2b801c8b307179
         e2461293ef2eba418ffea75af64c41c7
         f5b71a79b7ada0465fafdbbb4d298899
         8e0cbaff485de700e69a8a05f712600d
         728b6c86f604cfbbfc77d999fe21868f
         94fd3204798aad5a464ab40112f7819b)

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  find . -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'

  # Upstream fixes
  patch -Np1 -i "$srcdir/lp_861903.patch"
  patch -Np1 -i "$srcdir/lp_882633.patch"
  patch -Np1 -i "$srcdir/lp_884831.patch"

  # Update to vala-0.14
  patch -Np0 -i "$srcdir/gwibber-vala-0.14.patch"

  # Fix lens icon path
  sed -i 's|Icon=applications-microblogging-panel|Icon=/usr/share/gwibber/unity/applications-microblogging-panel.svg|' lens/data/gwibber.lens.in.in

  # Install updated language files
  echo 'af an ar ast az be bg bn br bs ca ca@valencia cy cs da de dv el en_AU en_CA en_GB eo es et eu fa fi fil fr fy ga gl gu he hi hr hu hy ia id is it ja ka kk km kn ko ku lb lo lt lv mg mk ml mn mr ms nb nl nn oc pa pl pt pt_BR ro ru si sk sl sq sr sv ta te th tr tt ug uk vi yo zh_CN zh_HK zh_TW zza' >po/LINGUAS
  rename $_pkgname- '' ../po/$_pkgname-*.po
  mv -f -t po ../po/*

  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$_pkgname \
              --disable-static --disable-schemas-compile
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

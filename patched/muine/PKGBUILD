# $Id: PKGBUILD 58497 2009-11-08 20:37:26Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Ben Mazer <blm@groknil.org>

pkgname=muine
pkgver=0.8.11
pkgrel=1.1
pkgdesc="A music player written in C#"
arch=(i686 x86_64)
license=('GPL')
url="http://www.muine-player.org/"
depends=('gnome-icon-theme' 'gstreamer0.10-base-plugins' 'gstreamer0.10-good-plugins' 'libgnome-sharp' 'gconf-sharp' 'taglib-sharp' 'notify-sharp-svn' 'desktop-file-utils')
makedepends=('intltool' 'gnome-common')
optdepends=('gstreamer0.10-ugly-plugins: Extra media codecs'
            'gstreamer0.10-bad-plugins: Extra media codecs'
            'gstreamer0.10-ffmpeg: Extra media codecs')
options=('!libtool')
install=muine.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/0.8/$pkgname-$pkgver.tar.bz2
        fix-multimedia-key-support.patch
        use-notify-sharp.patch
        replace-deprecated-gtk-functions.patch)
md5sums=('f2a74519d9e9c6c4fca9c5217a13964a'
         '983072d2aa9b64baa8fc185e5bc6459b'
         'e5b6c59e06f7f438ed091e8c1da726de'
         '47bc1b2d3e154a065d076904f5eda06a')

build() {
  export MONO_SHARED_DIR="$srcdir/.wabi"
  mkdir -p "$MONO_SHARED_DIR"
 
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i "$srcdir/fix-multimedia-key-support.patch"
  patch -Np1 -i "$srcdir/use-notify-sharp.patch"
  patch -Np1 -i "$srcdir/replace-deprecated-gtk-functions.patch"

  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --disable-schemas-install \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas \
              CFLAGS="$CFLAGS -Wno-error"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

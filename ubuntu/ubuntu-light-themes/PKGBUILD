# Maintainer: György Balló <ballogy@freestart.hu>
pkgbase=ubuntu-light-themes
_pkgbase=light-themes
pkgname=('gnome-theme-ambiance' 'gnome-theme-radiance')
pkgver=0.1.8.25
pkgrel=2
pkgdesc="Includes Ambiance and Radiance themes"
arch=('any')
url="https://launchpad.net/light-themes"
license=('custom:Creative Commons Attribution-ShareAlike')
depends=('gtk-engine-murrine' 'gtk-engine-unico')
optdepends=('ubuntu-mono-icon-theme: default icon theme'
            'xcursor-vanilla-dmz: default xcursor theme')
source=(https://launchpad.net/ubuntu/+archive/primary/+files/${_pkgbase}_$pkgver.tar.gz)
md5sums=('2c229c3444020f2db01607102aca5533')

build() {
  cd "$srcdir/$_pkgbase-$pkgver"

  sed -i 's/DMZ-White/Vanilla-DMZ/' */index.theme
	sed -i -e "/<shadow/ d
                   /<padding/ d" */metacity-1/metacity-theme-1.xml
}

package_gnome-theme-ambiance() {
  pkgdesc="A light-on-dark theme"
  cd "$srcdir/$_pkgbase-$pkgver"

  mkdir -p $pkgdir/usr/share/themes
  cp -r Ambiance* $pkgdir/usr/share/themes
  install -Dm644 debian/copyright "$pkgdir/usr/share/licenses/gnome-theme-ambiance/copyright"
}

package_gnome-theme-radiance() {
  pkgdesc="A dark-on-light theme"
  cd "$srcdir/$_pkgbase-$pkgver"

  mkdir -p $pkgdir/usr/share/themes
  cp -r Radiance $pkgdir/usr/share/themes
  install -Dm644 debian/copyright "$pkgdir/usr/share/licenses/gnome-theme-radiance/copyright"
}

# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=ubuntu-mono-icon-theme
_pkgname=ubuntu-mono
pkgver=0.0.37
pkgrel=2
pkgdesc="Icons for the panel, designed in a simplified monochrome style"
arch=(any)
url="https://launchpad.net/ubuntu-mono"
license=('custom:Creative Commons Attribution-ShareAlike')
depends=(humanity-icon-theme)
makedepends=(python2)
options=(!strip)
source=(https://launchpad.net/ubuntu/+archive/primary/+files/${_pkgname}_$pkgver.tar.gz)
md5sums=('2ab6ec73482eba570671b4b1344e8724')

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  sed -i 's|^#!/usr/bin/env python$|#!/usr/bin/env python2|' scavenge.py

  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  mkdir -p "$pkgdir/usr/share/icons"
  cp -r ubuntu-mono-dark ubuntu-mono-light LoginIcons "$pkgdir/usr/share/icons"
  find "$pkgdir" -type f -exec chmod 644 {} \;
  find "$pkgdir" -type d -exec chmod 755 {} \;
  install -Dm644 debian/copyright "$pkgdir/usr/share/licenses/$pkgname/copyright"
}

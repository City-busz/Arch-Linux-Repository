# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=ubuntuone-client
pkgver=2.0.0
pkgrel=1
pkgdesc="Ubuntu One helps you store, sync and share files. Sync your files, share your work with others or work remotely, all with your Ubuntu computer"
arch=(i686 x86_64)
url="https://launchpad.net/ubuntuone-client"
license=(GPL)
depends=(python-configglue ubuntu-sso-client python-notify python2-pyinotify python-ubuntuone-storageprotocol xdg-utils)
makedepends=(intltool imake gobject-introspection vala)
options=(!libtool)
install=$pkgname.install
source=(http://launchpad.net/ubuntuone-client/trunk/$pkgver/+download/$pkgname-$pkgver.tar.gz)
md5sums=(e2733da8ce76c07fdf47fa81857fc5fa)

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' bin/*

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/ubuntuone-client \
              --disable-static \
              PYTHON=python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # We don't have Apport in Arch Linux
  rm -r "$pkgdir"/{etc/apport,usr/share/apport}
}

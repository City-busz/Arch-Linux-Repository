# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=indicator-datetime
pkgver=0.3.1
pkgrel=6
pkgdesc="A very, very simple clock"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-datetime"
license=('GPL')
depends=('libindicator3>=0.3.0' 'libdbusmenu-gtk3>=0.3.94' 'ido3>=0.2.2' 'geoclue>=0.12.0' 'evolution-data-server>=3.2' 'dconf')
makedepends=('intltool>=0.41.0')
options=(!libtool !emptydirs)
install=$pkgname.install
source=(http://launchpad.net/$pkgname/0.3/$pkgver/+download/$pkgname-$pkgver.tar.gz
        launchpad-export.tar.gz
        make-ccpanel-optional.patch)
md5sums=('d566d240c3e4312e979381d9d8dd4b9a'
         'a2e0615d2db8156425e9825a0a0ef99f'
         '414b90c7af6d5ccd5f29e7ffdef121fd')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i "$srcdir/make-ccpanel-optional.patch"

  # Install updated language files
  echo 'ace af am an ar ast az be bg bn bo bs ca ca@valencia crh cy cs da de dv el en_AU en_CA en_GB eo es et eu fi fil fr fur fy gd gl gu gv he hi hr hu hy id is it ja jv ka kk kl km kn ko ku ky lb lt lv mg mhr mk ml mr ms my nb nds ne nl nn oc os pa pl pmy ps pt pt_BR ro ru sd si sk sl sq sr sv ta te th tr ug uk ur uz vec vi wae zh_CN zh_HK zh_TW' >po/LINGUAS
  rename $pkgname- '' ../po/$pkgname-*.po
  mv -f -t po ../po/*

  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static --disable-schemas-compile
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

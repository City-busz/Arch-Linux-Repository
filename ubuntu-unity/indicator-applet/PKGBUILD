# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=indicator-applet
pkgver=0.4.14
pkgrel=1
pkgdesc="A small applet to display information from various applications consistently in the panel"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-applet"
license=('GPL')
depends=('gnome-panel>=3.0' 'libindicator3>=0.3.92' 'xdg-utils')
makedepends=('intltool>=0.35.0')
optdepends=('indicator-application: take menus from applications and place them in the panel'
            'indicator-appmenu: host the menus from an application'
            'indicator-datetime: a very, very simple clock'
            'indicator-messages: a place on the users desktop that collects messages that need a response'
            'indicator-power: show the power status of your devices'
            'indicator-session: change your status, switch users'
            'indicator-sound: a unified sound menu')
install=$pkgname.install
source=(https://launchpad.net/~jconti/+archive/gnome3/+files/${pkgname}_$pkgver.orig.tar.gz)
md5sums=('34b98ac46bda4dc2f9ee4fa17f27b41d')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

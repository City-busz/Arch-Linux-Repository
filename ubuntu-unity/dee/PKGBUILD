# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=dee
pkgver=0.5.22
pkgrel=1
pkgdesc="A library that uses DBus to provide objects allowing you to create Model-View-Controller type programs across DBus"
arch=('i686' 'x86_64')
url="https://launchpad.net/dee"
license=('LGPL')
depends=('glib2>=2.26')
makedepends=('python2' 'gobject-introspection>=0.6.3' 'vala')
options=(!libtool)
source=(http://launchpad.net/$pkgname/0.5/$pkgver/+download/$pkgname-$pkgver.tar.gz)
md5sums=('061b8e73c61af1316316cff3c8285d03')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Disable building tests
  sed -i '/^SUBDIRS/ s/tests //' Makefile.in

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static \
              PYTHON=python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=unity-lens-files
pkgver=0.6.12
pkgrel=2
pkgdesc="A daemon exposing your files and file history to Unity"
arch=('i686' 'x86_64')
url="https://launchpad.net/unity-place-files"
license=('GPL')
depends=('libzeitgeist>=0.3.7' 'libunity>=3.6.3' 'zeitgeist-extensions')
makedepends=('vala>=0.11.3' 'intltool>=0.40.0')
source=(http://launchpad.net/$pkgname/trunk/$pkgver/+download/$pkgname-$pkgver.tar.gz
        launchpad-export.tar.gz)
md5sums=('e648069e6765757a979315e75a0750c1'
         '342400d7f3e9f334be31946f38852548')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Install language files
  echo 'am an ar ast az be bg bn bo bs ca ca@valencia cy cs da de el en_AU en_GB eo es et eu fi fil fr fy gd gl gu he hi hr hu id it ja kk km ko lb lt lv mg mhr ml mn ms my nb nl nn oc pa pl ps pt pt_BR ro ru sd si sk sl sq sr sv ta te th ti tr ug uk uz vi zh_CN zh_HK zh_TW' >po/LINGUAS
  rename $pkgname- '' ../po/$pkgname-*.po
  mv -f -t po ../po/*

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=unity-lens-files
pkgver=5.2.0
pkgrel=1
pkgdesc="A daemon exposing your files and file history to Unity"
arch=('i686' 'x86_64')
url="https://launchpad.net/unity-lens-files"
license=('GPL')
depends=('libzeitgeist' 'libunity' 'zeitgeist-extensions')
makedepends=('vala' 'intltool')
source=(http://launchpad.net/$pkgname/5.0/$pkgver/+download/$pkgname-$pkgver.tar.gz
        launchpad-export.tar.gz)
md5sums=('072fc109edecd88b061a69100cac2092'
         '342400d7f3e9f334be31946f38852548')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Install language files
  echo 'am an ar ast az be bg bn bo bs ca ca@valencia cy cs da de el en_AU en_GB eo es et eu fi fil fr fy gd gl gu he hi hr hu id it ja kk km ko lb lt lv mg mhr ml mn ms my nb nl nn oc pa pl ps pt pt_BR ro ru sd si sk sl sq sr sv ta te th ti tr ug uk uz vi zh_CN zh_HK zh_TW' >po/LINGUAS
  rename $pkgname- '' ../po/$pkgname-*.po
  mv -f -t po ../po/*

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

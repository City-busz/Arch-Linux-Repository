# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=libunity
pkgver=4.0.6
pkgrel=2
pkgdesc="A library for instrumenting and integrating with all aspects of the Unity shell"
arch=('i686' 'x86_64')
url="https://launchpad.net/libunity"
license=('LGPL')
depends=('libgee>=0.6.0' 'dee>=0.5.12' 'libdbusmenu>=0.3.93' 'dbus-glib')
makedepends=('vala>=0.11.3' 'python2' 'gobject-introspection>=0.10.0')
options=(!libtool)
source=(http://launchpad.net/$pkgname/4.0/$pkgver/+download/$pkgname-$pkgver.tar.gz
        fix-search-finished-signal.patch)
md5sums=('7184633c82f992a75087ab1203c70c67'
         'ba07cc809526d9a7a99084521dcf19da')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np0 -i "$srcdir/fix-search-finished-signal.patch"

  # Disable building tests
  sed -i '/test \\/ d' Makefile.in

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static \
              PYTHON=python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

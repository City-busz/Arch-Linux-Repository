# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=libunity
pkgver=5.2.0
pkgrel=1
pkgdesc="A library for instrumenting and integrating with all aspects of the Unity shell"
arch=('i686' 'x86_64')
url="https://launchpad.net/libunity"
license=('LGPL')
depends=('libgee' 'dee' 'libdbusmenu' 'dbus-glib')
makedepends=('vala' 'python2' 'gobject-introspection')
options=('!libtool')
source=(http://launchpad.net/$pkgname/5.0/$pkgver/+download/$pkgname-$pkgver.tar.gz)
md5sums=('844ef861e5e34050e0796f39f0e48750')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Disable building tests
  sed -i '/test \\/ d' Makefile.in

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static \
              PYTHON=python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

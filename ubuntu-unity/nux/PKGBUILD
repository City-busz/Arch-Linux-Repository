# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=nux
pkgver=2.6.0
pkgrel=1
pkgdesc="Graphical user interface toolkit for applications that mixes OpenGL hardware acceleration with high quality visual rendering"
arch=('i686' 'x86_64')
url="https://launchpad.net/nux"
license=('LGPL')
depends=('glew' 'libsigc++' 'ibus')
makedepends=('boost')
options=('!libtool' '!emptydirs')
source=(http://launchpad.net/$pkgname/2.0/${pkgver%.*}/+download/$pkgname-$pkgver.tar.gz)
md5sums=('c0ddc87c95cd1d623d37c97613a89e6f')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's/libutouch-geis//' configure NuxGraphics/nux-graphics.pc.in

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static --disable-tests --disable-examples --disable-gputests
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

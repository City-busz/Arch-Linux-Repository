# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=indicator-appmenu
pkgver=0.3.2
pkgrel=2
pkgdesc="An indicator to host the menus from an application"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-appmenu"
license=('GPL')
depends=('libindicator3>=0.3.19' 'libdbusmenu-gtk3>=0.3.3' 'bamf>=0.2.53')
makedepends=('gtk-doc')
optdepends=('appmenu-gtk2: support for GTK+ 2 apps'
            'appmenu-gtk3: support for GTK+ 3 apps'
            'appmenu-qt: support for Qt apps'
            'libreoffice-extension-menubar: support for LibreOffice')
options=(!libtool)
source=(http://launchpad.net/$pkgname/0.3/$pkgver/+download/$pkgname-$pkgver.tar.gz
        make-tests-optional.patch)
md5sums=('69e62c37565dfc8e8bd249801bc21dd7'
         'd81424dffda459b9fa1ac865f2f0302d')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i "$srcdir/make-tests-optional.patch"

  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

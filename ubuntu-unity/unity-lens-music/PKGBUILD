# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=unity-lens-music
pkgver=5.0.0
pkgrel=1
pkgdesc="Music, in the dash"
arch=('i686' 'x86_64')
url="https://launchpad.net/unity-lens-music"
license=('GPL')
depends=('json-glib' 'libunity' 'banshee')
makedepends=('vala' 'intltool')
source=(http://launchpad.net/$pkgname/5.0/$pkgver/+download/$pkgname-$pkgver.tar.gz
        launchpad-export.tar.gz)
md5sums=('3c2b1086735f37f27a7182f753e072a6'
         'd32f36dae53e43619936a59e0825a87b')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Install language files
  echo 'am ar ast az be bg bn bo bs ca ca@valencia cs da de el en_AU en_GB eo es et eu fi fil fr fy gd gl gu he hi hr hu id it ja kk km ko lb lt lv mg mhr ml ms my nb nl nn oc pl ps pt pt_BR ro ru sd si sk sl sq sr sv ta te th ti tr ug uk uz vi zh_CN zh_HK zh_TW' >po/LINGUAS
  rename $pkgname- '' ../po/$pkgname-*.po
  mv -f -t po ../po/*

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=unity-lens-applications
pkgver=5.0.0
pkgrel=1
pkgdesc="A daemon that exposes you applications with their usage statistics and status to Unity"
arch=('i686' 'x86_64')
url="https://launchpad.net/unity-lens-applications"
license=('GPL')
depends=('libzeitgeist' 'libunity' 'gnome-menus2' 'zeitgeist-extensions' 'dconf')
makedepends=('vala' 'intltool' 'gconf')
install=$pkgname.install
source=(http://launchpad.net/$pkgname/5.0/$pkgver/+download/$pkgname-$pkgver.tar.gz
        launchpad-export.tar.gz
        no-db4.8.patch)
md5sums=('9f23fa8a64ddf87f32d7d03638eed25c'
         '4bc072c6980172b7fb8b584b6401e295'
         '3e1713c082c00642dbd6d80b33d63708')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i "$srcdir/no-db4.8.patch"

  # Install language files
  echo 'af am an ar ast az be bg bn bo bs ca ca@valencia cy cs da de el en_AU en_GB eo es et eu fi fil fr fy gd gl gu he hi hr hu id it ja kk km ko ku lb lt lv mg mhr ms nb nl nn oc pa pl ps pt pt_BR ro ru sd si sk sl sq sr sv ta te th ti tr ug uk uz vi zh_CN zh_HK zh_TW' >po/LINGUAS
  rename $pkgname- '' ../po/$pkgname-*.po
  mv -f -t po ../po/*

  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static --disable-schemas-compile
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

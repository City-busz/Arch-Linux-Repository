# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=unity-lens-applications
pkgver=0.4.12
pkgrel=2
pkgdesc="A daemon that exposes you applications with their usage statistics and status to Unity"
arch=('i686' 'x86_64')
url="https://launchpad.net/unity-lens-applications"
license=('GPL')
depends=('gconf' 'libzeitgeist>=0.3.8' 'libunity>=4.0.5' 'gnome-menus2' 'zeitgeist-extensions')
makedepends=('vala>=0.11.3' 'intltool>=0.40.0')
source=(http://launchpad.net/$pkgname/trunk/$pkgver/+download/$pkgname-$pkgver.tar.gz
        launchpad-export.tar.gz
        no-db4.8.patch)
md5sums=('ecb6d80f6216fb146b7762c587f489c8'
         '4bc072c6980172b7fb8b584b6401e295'
         '3e1713c082c00642dbd6d80b33d63708')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i "$srcdir/no-db4.8.patch"

  # Install language files
  echo 'af am an ar ast az be bg bn bo bs ca ca@valencia cy cs da de el en_AU en_GB eo es et eu fi fil fr fy gd gl gu he hi hr hu id it ja kk km ko ku lb lt lv mg mhr ms nb nl nn oc pa pl ps pt pt_BR ro ru sd si sk sl sq sr sv ta te th ti tr ug uk uz vi zh_CN zh_HK zh_TW' >po/LINGUAS
  rename $pkgname- '' ../po/$pkgname-*.po
  mv -f -t po ../po/*

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

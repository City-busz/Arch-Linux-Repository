# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=bamf
pkgver=0.2.106
pkgrel=1
pkgdesc="Removes the headache of applications matching into a simple DBus daemon and c wrapper library"
arch=('i686' 'x86_64')
url="https://launchpad.net/bamf"
license=('GPL')
depends=('dbus-glib' 'libwnck3' 'libgtop')
makedepends=('libwnck')
optdepends=('gtk2: GTK+ 2 library')
options=(!libtool)
source=(http://launchpad.net/$pkgname/0.2/0.2.106/+download/$pkgname-$pkgver.tar.gz)
md5sums=('5bb1d1f9cf5657b2f62b25b138edfcb6')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Disable building tests
  sed -i '/tests/ d' Makefile.in

  [[ -d build-gtk3 ]] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgbase \
              --disable-static
  make
  popd

  [[ -d build-gtk2 ]] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgbase \
              --disable-static --with-gtk=2
  make -C lib/libbamf
  popd
}

package() {
  cd "$srcdir/$pkgname-$pkgver/build-gtk3"
  make DESTDIR="$pkgdir/" install

  cd "$srcdir/$pkgbase-$pkgver/build-gtk2"
  make -C lib/libbamf DESTDIR="$pkgdir" install
}

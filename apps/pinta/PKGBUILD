# Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: Konrad <konrad AT knauber DOT name>

pkgname=pinta
pkgver=1.1
pkgrel=1
pkgdesc="A drawing/editing program modeled after Paint.NET. It's goal is to provide a simplified alternative to GIMP for casual users"
arch=('any')
url="http://pinta-project.com/"
license=('MIT')
depends=('gtk-sharp-2' 'desktop-file-utils' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('intltool')
install=$pkgname.install
source=(https://github.com/downloads/PintaProject/Pinta/$pkgname-$pkgver.tar.gz)
md5sums=('fc1848072e642111f871aa27e36fafb0')

build() {
  cd "$srcdir"/$pkgname-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver

  make DESTDIR="$pkgdir/" install

  install -dm755 "$pkgdir"/usr/share/licenses/$pkgname
  install -m644 license-*.txt readme.md "$pkgdir"/usr/share/licenses/$pkgname/
}

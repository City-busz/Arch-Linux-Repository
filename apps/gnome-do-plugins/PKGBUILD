# Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Doehni <dohny@gmx.de>

pkgname=gnome-do-plugins
pkgver=0.8.4
pkgrel=2
pkgdesc="Plugins for gnome-do"
arch=('any')
url="http://do.davebsd.com/"
license=('GPL3')
depends=('gnome-do>=0.8.4')
makedepends=('intltool' 'banshee' 'flickrnet' 'libgnome-sharp' 'gnome-vfs-sharp' 'gdata-sharp' 'wnck-sharp')
optdepends=('banshee: Banshee plugin'
            'flickrnet: Flickr plugin'
            'gnome-vfs-sharp: Disk Mounter plugin'
            'gdata-sharp: Google plugins'
            'wnck-sharp: Window Manager plugin')
source=(http://launchpad.net/do-plugins/trunk/$pkgver/+download/$pkgname-$pkgver.tar.gz
        flickrnet-3.2.patch
        use-dbus-sharp.patch)
md5sums=('092b51b27463dc2beee65779d95a0ff1'
         '8222b36baf39657d8f4e4c214f124093'
         'eeaf4ab3521bbd6084da95f08e6d14d8')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i "$srcdir/flickrnet-3.2.patch"
  patch -Np1 -i "$srcdir/use-dbus-sharp.patch"

  autoreconf -fi
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

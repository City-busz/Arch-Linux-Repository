# Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Doehni <dohny@gmx.de>

pkgname=gnome-do-plugins
pkgver=0.8.4
pkgrel=1
pkgdesc="Plugins for gnome-do"
arch=('any')
url="http://do.davebsd.com/"
license=('GPL3')
depends=('gnome-do>=0.8.4')
makedepends=('intltool' 'banshee' 'dbus-sharp-glib' 'flickrnet' 'gdata-sharp' 'wnck-sharp')
optdepends=('banshee: Banshee plugin'
            'dbus-sharp-glib: Banshee and Empathy plugin'
            'flickrnet: Flickr plugin'
            'gdata-sharp: Google plugins'
            'wnck-sharp: Window Manager plugin')
source=(http://launchpad.net/do-plugins/trunk/$pkgver/+download/$pkgname-$pkgver.tar.gz
        flickrnet-3.2.patch)
md5sums=('092b51b27463dc2beee65779d95a0ff1'
         '8222b36baf39657d8f4e4c214f124093')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i "$srcdir/flickrnet-3.2.patch"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

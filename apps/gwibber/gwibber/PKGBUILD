# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=gwibber
pkgver=3.2.1
pkgrel=1
pkgdesc="Microblogging client for GNOME, which supports Twitter, Identi.ca, StatusNet, Facebook, Flickr, Digg, FriendFeed and Qaiku"
arch=(i686 x86_64)
url="http://gwibber.com/"
license=(GPL)
depends=(libgee 'dee>=0.5.18-2' json-glib gtkspell3 dbus-python gnome-keyring python-gnomekeyring python-notify python-wnck python-egenix-mx-base python2-oauth python-imaging python-pycurl python-simplejson pywebkitgtk pyxdg xdg-utils)
makedepends=('vala>=0.14' 'intltool>=0.35.0' 'gobject-introspection>=0.10')
optdepends=('libindicate: indicator support')
options=(!libtool)
install=$pkgname.install
source=(http://launchpad.net/$pkgname/3.2/$pkgver/+download/$pkgname-$pkgver.tar.gz
        gwibber-no-unity.patch
        gwibber-vala-0.14.patch
        launchpad-export.tar.gz)
md5sums=(8e245072f8b43852ef2b801c8b307179
         aa21539a23f68566d25d1f2937577d27
         94fd3204798aad5a464ab40112f7819b
         e2461293ef2eba418ffea75af64c41c7)

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Really disable Unity
  patch -Np1 -i "$srcdir/gwibber-no-unity.patch"

  # Update to vala-0.14
  patch -Np0 -i "$srcdir/gwibber-vala-0.14.patch"

  # Install updated language files
  echo 'af an ar ast az be bg bn br bs ca ca@valencia cy cs da de dv el en_AU en_CA en_GB eo es et eu fa fi fil fr fy ga gl gu he hi hr hu hy ia id is it ja ka kk km kn ko ku lb lo lt lv mg mk ml mn mr ms nb nl nn oc pa pl pt pt_BR ro ru si sk sl sq sr sv ta te th tr tt ug uk vi yo zh_CN zh_HK zh_TW zza' >po/LINGUAS
  rename $pkgname- '' ../po/$pkgname-*.po
  mv -f -t po ../po/*

  # python2 fix
  find . -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'

  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --disable-schemas-compile --disable-unity
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

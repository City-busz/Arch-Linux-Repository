# Maintainer: Balló György <ballogyor+arch at gmail dot com>
pkgname=bluegriffon
pkgver=1.4.1
_mozilla_ver=023130d28f50
pkgrel=1
pkgdesc="The next-generation Web Editor based on the rendering engine of Firefox"
arch=('i686' 'x86_64')
url="http://bluegriffon.org/"
license=('MPL' 'GPL' 'LGPL')
depends=('gtk2' 'libnotify' 'dbus-glib' 'alsa-lib' 'nspr' 'libevent' 'hunspell' 'startup-notification' 'libvpx' 'sqlite3' 'mozilla-common' 'desktop-file-utils' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('subversion' 'autoconf2.13' 'zip' 'unzip' 'yasm' 'libidl2' 'python2' 'wireless_tools' 'mesa')
options=('!emptydirs')
install=$pkgname.install
source=(http://hg.mozilla.org/mozilla-central/archive/$_mozilla_ver.tar.bz2
        mozconfig
        bluegriffon.desktop)
md5sums=('0a2f63cb614f1fd25b98c3dc8bcfee69'
         '25e363ed65b09ea33526e98b71f5b370'
         'b508d3a028380595d25c8039a217768b')

_bluegriffon_svntrunk=http://sources.disruptive-innovations.com/bluegriffon/tags/$pkgver
_bluegriffon_svnmod=bluegriffon
_bluegriffon_l10n_svntrunk=http://sources.disruptive-innovations.com/bluegriffon-l10n/tags/$pkgver
_bluegriffon_l10n_svnmod=locales

build() {
  msg "Getting bluegriffon $pkgver"

  cd "$srcdir"
  msg "Connecting to SVN server...."

  if [[ -d "$_bluegriffon_svnmod/.svn" ]]; then
    (cd "$_bluegriffon_svnmod" && svn up)
  else
    svn co "$_bluegriffon_svntrunk" --config-dir ./ "$_bluegriffon_svnmod"
  fi

  msg "SVN checkout done or server timeout"

  msg "Getting bluegriffon $pkgver locales"

  cd "$srcdir"
  msg "Connecting to SVN server...."

  if [[ -d "$_bluegriffon_l10n_svnmod/.svn" ]]; then
    (cd "$_bluegriffon_l10n_svnmod" && svn up)
  else
    svn co "$_bluegriffon_l10n_svntrunk" --config-dir ./ "$_bluegriffon_l10n_svnmod"
  fi

  msg "SVN checkout done or server timeout"

  msg "Starting build..."

  cd "$srcdir/mozilla-central-$_mozilla_ver"
  cp -r "$srcdir/$_bluegriffon_svnmod" "$_bluegriffon_svnmod"
  cp -r "$srcdir/$_bluegriffon_l10n_svnmod" "$_bluegriffon_svnmod/$_bluegriffon_l10n_svnmod"

  patch -p 1 < bluegriffon/config/content.patch
  cp "$srcdir/mozconfig" .mozconfig

  find . -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'
  export PYTHON=python2

  make -f client.mk build_all
}

package() {
  cd "$srcdir/mozilla-central-$_mozilla_ver"

  mkdir -p "$pkgdir/usr/lib/bluegriffon"
  cp -LR bg/dist/bin/* "$pkgdir/usr/lib/bluegriffon"

  for i in 16x16 32x32 48x48; do
      install -Dm644 bg/dist/branding/default${i/x*/}.png \
        "$pkgdir/usr/share/icons/hicolor/$i/apps/bluegriffon.png"
  done

  install -Dm644 bg/dist/branding/mozicon128.png \
    "$pkgdir/usr/share/icons/hicolor/128x128/apps/bluegriffon.png"

  install -Dm644 "$srcdir/bluegriffon.desktop" \
    "$pkgdir/usr/share/applications/bluegriffon.desktop"

  rm -rf "$pkgdir"/usr/lib/bluegriffon/{dictionaries,hyphenation}
  ln -sf /usr/share/hunspell "$pkgdir/usr/lib/bluegriffon/dictionaries"
  ln -sf /usr/share/hyphen "$pkgdir/usr/lib/bluegriffon/hyphenation"

  mkdir -p "$pkgdir/usr/bin"
  ln -sf /usr/lib/bluegriffon/bluegriffon "$pkgdir/usr/bin/bluegriffon"

  # Remove stub extensions
  rm "$pkgdir"/usr/lib/bluegriffon/distribution/extensions/{csseditor,eyedropper,fullscreen,mathml,snippets,tablelayout,templatesManager,thumbnailer}@bluegriffon.com.xpi
}

# Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgname=dockmanager
pkgver=0.1.0
pkgrel=4
pkgdesc="Dock-independent helper scripts for compatible docks"
url="https://launchpad.net/dockmanager"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libdesktop-agnostic' 'gconf' 'dbus-python' 'python2-gobject2' 'pyxdg')
makedepends=('intltool' 'vala')
optdepends=('pygtk: Banshee control, Rhythmbox control Helper'
            'deluge: Deluge badge Helper'
            'python-mpd: Mpd control Helper'
            'python2-gconf: Open terminal here Helper'
            'python2-libgnome: Zeitgeist journal Helper'
            'zeitgeist: Zeitgeist journal, Zeitgeist menus Helper')
install=$pkgname.install
source=(http://launchpad.net/dockmanager/trunk/$pkgver/+download/$pkgname-$pkgver.tar.gz)
md5sums=('52a1c833a8f3599156dc00cd52ca624a')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' scripts/*.py

  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/$pkgname \
              --disable-static --disable-debug --enable-release \
              PYTHON=python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir" install

  mkdir -p "$pkgdir/usr/share/gconf/schemas"
  gconf-merge-schema "$pkgdir/usr/share/gconf/schemas/dockmanager.schemas" "$pkgdir"/etc/gconf/schemas/*.schemas
  rm -r "$pkgdir/etc"
}

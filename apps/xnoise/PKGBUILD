# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=xnoise
pkgver=0.1.29
pkgrel=1
pkgdesc="Media player for Gtk+ with a slick GUI, great speed and lots of features"
arch=(i686 x86_64)
url="http://xnoise-media-player.com/"
license=(GPL)
depends=(gstreamer0.10-base-plugins sqlite3 taglib libunique libsoup libnotify desktop-file-utils hicolor-icon-theme xdg-utils)
makedepends=(vala intltool)
optdepends=('gstreamer0.10-good-plugins: Extra media codecs'
            'gstreamer0.10-ugly-plugins: Extra media codecs'
            'gstreamer0.10-bad-plugins: Extra media codecs'
            'gstreamer0.10-ffmpeg: Extra media codecs')
options=(!libtool)
install=$pkgname.install
source=(http://xnoise.googlecode.com/files/$pkgname-$pkgver.tar.bz2
        xnoise-0.1.29-g_const_return.patch)
md5sums=(a97113ed8d8f9229f2274fd886a3286b
         fc0b6bef68fe19aade4b5e1fc3d97c14)

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i "$srcdir/xnoise-0.1.29-g_const_return.patch"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

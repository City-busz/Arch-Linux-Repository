# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=covergloobus
pkgver=1.7.3
pkgrel=1
pkgdesc="A simple application to show the cover and the info of what is playing in your favorite player"
arch=(any)
url="http://gloobus.net/"
license=(GPL)
depends=(dbus-python pygtk pyxdg python-xlib hicolor-icon-theme xdg-utils)
options=(!emptydirs)
install=$pkgname.install
source=(https://launchpad.net/~gloobus-dev/+archive/$pkgname/+files/${pkgname}_$pkgver.tar.gz)
md5sums=(17ee6304597cb84018853c61c178a57e)

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' src/covergloobus{,-config,-tester}.py
  sed -i 's|^python @prefix@/share/covergloobus/|python2 @prefix@/share/covergloobus/|' src/scripts/covergloobus{,-config}{,.in}
  sed -i 's|^python /usr/share/covergloobus/|python2 /usr/share/covergloobus/|' src/scripts/covergloobus{,-config}

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              PYTHON=python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

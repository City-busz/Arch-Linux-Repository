# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=giver
pkgver=0.1.8
pkgrel=4
pkgdesc="A simple file sharing desktop application"
arch=('any')
url="http://code.google.com/p/giver/"
license=('MIT')
depends=('avahi' 'notify-sharp' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('intltool' 'gnome-common')
install=$pkgname.install
source=(http://$pkgname.googlecode.com/files/$pkgname-$pkgver.tar.gz
        drop-gnome-sharp-dependency.patch)
sha1sums=('969e794f61a7ef94396a830b1fa623b93679c8c3'
          '4ba6ccbc542c675eddf8907a053f4eb77c1df06c')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i "$srcdir/drop-gnome-sharp-dependency.patch"
  sed -i 's/pkglib_DATA/pkgdata_DATA/' Makefile.am

  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --disable-schemas-install --disable-debug --enable-release \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

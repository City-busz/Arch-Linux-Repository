# Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Doehni <dohny@gmx.de>

pkgname=gnome-do
pkgver=0.8.5
pkgrel=4
pkgdesc="A launcher application for gnome (like Launchy or Katapult)"
arch=('i686' 'x86_64')
url="http://do.davebsd.com/"
license=('GPL3')
depends=('gconf-sharp' 'libgnome-desktop-sharp' 'gnome-keyring-sharp' 'mono-addins' 'notify-sharp' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('intltool' 'libgnome-sharp' 'gio-sharp')
options=('!libtool')
install=$pkgname.install
source=(http://launchpad.net/do/trunk/$pkgver/+download/$pkgname-$pkgver.tar.gz
        remove-unused-dependencies.patch
        use-dbus-sharp.patch
        use-gio-sharp.patch)
md5sums=('71e0e653021f895f151a6f46a97f75ba'
         '768bfa843fffa7e2335992ba1453e4cb'
         'aed74e6ae3ec77dae07603edf7f01288'
         '1867d725d950cc4bfab44ae56e858bf8')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i "$srcdir/remove-unused-dependencies.patch"
  patch -Np1 -i "$srcdir/use-dbus-sharp.patch"
  patch -Np1 -i "$srcdir/use-gio-sharp.patch"

  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc \
              --disable-schemas-install \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

# Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Timm Preetz <timm@preetz.us>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=avant-window-navigator
pkgver=0.4.0
pkgrel=12
pkgdesc="Fully customisable dock-like window navigator for GNOME"
arch=('i686' 'x86_64')
url="https://launchpad.net/awn"
license=('GPL')
depends=('libdesktop-agnostic' 'libxcomposite' 'libwnck' 'libgtop' 'gconf' 'bzr' 'dbus-python' 'pygtk' 'pyxdg' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('gtk-doc' 'vala' 'intltool')
options=(!libtool)
install=$pkgname.install
source=(http://launchpad.net/awn/0.4/$pkgver/+download/$pkgname-$pkgver.tar.gz
        fix-dso-linking.patch
        fix-unitialized-pointer.patch)
md5sums=('03654b45dd95cbb83fa7e112bd00523c'
         '25aa951e36b17ba2471c18afb7ba4acb'
         '2e53c76a28a49f793f92b673e4f56539')

build() {
  cd $srcdir/$pkgname-$pkgver
  # LP#561492
  patch -Np1 -i "$srcdir/fix-dso-linking.patch"
  # FS#19084
  patch -Np0 -i "$srcdir/fix-unitialized-pointer.patch"

  # Python2 fix
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' awn-settings/{awnSettings.py.in,awnSettingsHelper.py}
  sed -i 's/with ("python"/with ("python2"/' applet-activation/main.c

  ./configure --prefix=/usr --sysconfdir=/etc \
              --disable-static --enable-gtk-doc \
              PYTHON=/usr/bin/python2
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver

  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir" install

  install -m755 -d "$pkgdir/usr/share/gconf/schemas"
  gconf-merge-schema "$pkgdir/usr/share/gconf/schemas/awn.schemas" "$pkgdir"/etc/gconf/schemas/*.schemas
  rm -r "$pkgdir/etc"
}

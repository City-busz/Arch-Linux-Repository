# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=banshee-community-extensions
pkgver=2.2.0
pkgrel=2
pkgdesc="Community Extensions for Banshee"
arch=('i686' 'x86_64')
url="http://banshee.fm/download/extensions/"
license=('GPL')
depends=('banshee>=2.2.0' 'fftw' 'libsamplerate' 'lirc-utils' 'notify-sharp' 'taoframework' 'zeitgeist-sharp')
makedepends=('intltool' 'gnome-doc-utils')
optdepends=('avant-window-navigator: Awn Integration extension'
            'empathy: Telepathy extension')
options=('!libtool')
source=(http://download.banshee.fm/$pkgname/$pkgver/$pkgname-$pkgver.tar.bz2)
sha256sums=('3ac0c7e9e00b8ab7b91a8942cb5b1fc360977b15ec303756d4a440704a22bb15')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --disable-scrollkeeper --disable-schemas-install \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas \
              --with-vendor-build-id=ArchLinux
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

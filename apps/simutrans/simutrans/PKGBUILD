# Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Gilles Gagniard <gilles@gagniard.org>
# Contributor: JD Steffen <jd at steffennet dot org>

pkgname=simutrans
pkgver=110.0.1
_pkgver=110-0-1
pkgrel=1
pkgdesc="An open source transportation simulation game"
arch=(i686 x86_64)
url="http://simutrans.com/"
license=('custom:Artistic License')
depends=(gcc-libs zlib sdl_mixer 'simutrans-pak64>=110.0.1')
makedepends=(unzip imagemagick)
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-src-$_pkgver.zip
        http://downloads.sourceforge.net/$pkgname/simulinux-$_pkgver.zip
        revert-svn-1937.patch
        path-for-game-data.patch
        config.patch
        0001-jspaces-fix-compiling-with-gcc-4.6.0.patch
        simutrans.desktop)
md5sums=(348e1ae7319426b173f1ca42c2227681
         29bb6eb522bcba76e1057ba80d1929dd
         54b06838b2720d6fe7cdd9775242b6d4
         603c1a361ba50c2cb52293442a2692ad
         efb9124b7d7d854d7c2e8576e4f4223f
         01035c5a371c8785c3933a916f3fdd09
         f41f7a08ad517ef2b60412859eb49963)

build() {
  cd "$srcdir"

  cp config.template config.default
  patch -Np1 -i "$srcdir/revert-svn-1937.patch"
  patch -Np1 -i "$srcdir/path-for-game-data.patch"
  patch -Np1 -i "$srcdir/config.patch"
  patch -Np1 -i "$srcdir/0001-jspaces-fix-compiling-with-gcc-4.6.0.patch"
  convert simutrans.ico -alpha on simutrans.png

  make
}

package() {
  cd "$srcdir"

  #binary
  install -Dm755 sim "$pkgdir/usr/bin/simutrans"

  #data
  mkdir -p "$pkgdir/usr/share/games/$pkgname"
  cp -r "$pkgname"/{config,font,music,skin,text} "$pkgdir/usr/share/games/$pkgname"

  #desktop file and icon
  install -Dm644 simutrans.png "$pkgdir/usr/share/pixmaps/simutrans.png"
  install -Dm644 simutrans.desktop "$pkgdir/usr/share/applications/simutrans.desktop"

  #license
  install -Dm644 "$pkgname"/licence.txt "$pkgdir/usr/share/licenses/$pkgname/licence.txt"
}

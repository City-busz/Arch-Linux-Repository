# Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Gilles Gagniard <gilles@gagniard.org>
# Contributor: JD Steffen <jd at steffennet dot org>

pkgname=simutrans
pkgver=111.0
_pkgver=111-0
pkgrel=1
pkgdesc="An open source transportation simulation game"
arch=(i686 x86_64)
url="http://simutrans.com/"
license=('custom:Artistic License')
depends=(gcc-libs zlib sdl_mixer 'simutrans-pak64>=110.0.1')
makedepends=(imagemagick)
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-src-$_pkgver.zip
        http://downloads.sourceforge.net/$pkgname/simulinux-$_pkgver.zip
        settings-folder.patch
        path-for-game-data.patch
        config.patch
        simutrans.desktop)
md5sums=(685d456b8869b73332e87f86612ada71
         44c869fe59e355420ff9ab942a1795e6
         00a5e0f6f197d26d3c203a68cbf474d1
         603c1a361ba50c2cb52293442a2692ad
         cffda40eef46624daf47533fdd4417cc
         f41f7a08ad517ef2b60412859eb49963)

build() {
  cd "$srcdir"

  cp config.template config.default
  patch -Np0 -i "$srcdir/settings-folder.patch"
  patch -Np1 -i "$srcdir/path-for-game-data.patch"
  patch -Np0 -i "$srcdir/config.patch"
  convert simutrans.ico -alpha on simutrans.png

  make
}

package() {
  cd "$srcdir"

  #binary
  install -Dm755 build/default/sim "$pkgdir/usr/bin/simutrans"

  #data
  mkdir -p "$pkgdir/usr/share/games/$pkgname"
  cp -r "$pkgname"/{config,font,music,skin,text} "$pkgdir/usr/share/games/$pkgname"

  #desktop file and icon
  install -Dm644 simutrans.png "$pkgdir/usr/share/pixmaps/simutrans.png"
  install -Dm644 simutrans.desktop "$pkgdir/usr/share/applications/simutrans.desktop"

  #license
  install -Dm644 "$pkgname"/licence.txt "$pkgdir/usr/share/licenses/$pkgname/licence.txt"
}

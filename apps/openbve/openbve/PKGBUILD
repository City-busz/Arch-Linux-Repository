# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=openbve
pkgver=1.4.0.0
pkgrel=1
pkgdesc="Free-as-in-freedom train simulator placed in the public domain"
arch=('any')
url="http://trainsimframework.org/"
license=('custom:public domain')
depends=('mono' 'taoframework' 'sdl' 'openal')
source=(http://trainsimframework.org/common/openbve_stable_source.zip
        http://trainsimframework.org/common/openbve_stable.zip
        openbve.sh
        openbve.desktop)
md5sums=('d306c160bd58373be7926e5585463e97'
         '4e8f47832f5b341ca08c52ffe094c88b'
         'fec92946f773f56588a71de310847ce0'
         'acb0dd3ea602910181b8cdb8375c794e')

build() {
  cd "$srcdir/openBVE"
 
  xbuild /p:Configuration=Release OpenBve/OpenBve.csproj
}

package() {
  cd "$srcdir"

  #binaries
  install -d $pkgdir/usr/lib/$pkgname/
  install -Dm644 openBVE/OpenBveApi/bin/Release/OpenBveApi.dll $pkgdir/usr/lib/$pkgname/
  install -Dm644 openBVE/OpenBve/bin/Release/OpenBve.exe $pkgdir/usr/lib/$pkgname/

  #data
  mkdir -p $pkgdir/usr/share/games/$pkgname
  cp -r Data $pkgdir/usr/share/games/$pkgname/
  ln -s ../../share/games/$pkgname/Data $pkgdir/usr/lib/$pkgname/Data

  #executable
  install -Dm755 $pkgname.sh $pkgdir/usr/bin/$pkgname

  #desktop file and icon
  install -Dm644 $pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop
  mkdir -p $pkgdir/usr/share/pixmaps
  ln -s ../games/$pkgname/Data/Menu/logo.png $pkgdir/usr/share/pixmaps/$pkgname.png

  #license
  install -Dm644 Readme.txt $pkgdir/usr/share/licenses/$pkgname/Readme.txt
}

# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=kompozer
pkgver=0.8b3
pkgrel=1
pkgdesc="Complete web authoring system that combines web file management and easy-to-use WYSIWYG web page editing"
arch=('i686' 'x86_64')
url="http://kompozer.sourceforge.net/"
license=('MPL' 'GPL' 'LGPL')
depends=('gtk2' 'libxt' 'nss' 'hunspell' 'desktop-file-utils')
makedepends=('zip' 'libgnomeui' 'libidl2')
optdepends=('libgnomeui: GNOME integration and MIME handling')
install=$pkgname.install
source=(http://downloads.sourceforge.net/kompozer/$pkgname-$pkgver-src.tar.bz2
        kompozer-png14.patch
        mozconfig
        kompozer.desktop)
md5sums=('cd4664ecda908666b19ef0607010c627'
         'd5b77df4723cf27131055ed8cecbc4ab'
         '8d89c61a76c511f4d6608c034b75a4c1'
         'b743513b3f075d59bc8062f23226779e')
build() {
  cd "$srcdir/mozilla"
  patch -Np0 -i "$srcdir"/kompozer-png14.patch
  cp $srcdir/mozconfig .mozconfig

  make -f client.mk build
}

package() {
  cd "$srcdir/obj-kompozer"

  make DESTDIR="$pkgdir/" install
  install -d $pkgdir/usr/share/{applications,pixmaps}
  ln -s ../../lib/kompozer/icons/mozicon256.png $pkgdir/usr/share/pixmaps/kompozer.png
  install -Dm644 $srcdir/kompozer.desktop $pkgdir/usr/share/applications/kompozer.desktop
}

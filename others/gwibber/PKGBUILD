# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=gwibber
pkgver=3.0.0.1
pkgrel=1
pkgdesc="Microblogging client for GNOME, which supports Twitter, Identi.ca, StatusNet, Facebook, Flickr, Digg, FriendFeed and Qaiku"
arch=('any')
url="http://gwibber.com/"
license=('GPL')
depends=('dbus-python>=0.80.2' 'python2-feedparser>=4.1' 'python2-gconf>=2.18.0' 'python-gnomekeyring' 'gnome-keyring' 'python-wnck' 'python-mako>=0.2.2' 'python-egenix-mx-base>=3.0.0' 'python2-oauth' 'python-imaging>=1.1.6' 'python-pycurl' 'python-simplejson>=1.9.1' 'pywebkitgtk>=1.0.1' 'pyxdg>=0.15')
makedepends=('python-distutils-extra')
optdepends=('python2-gtkspell: spell-checking support'
            'python-notify: notification support'
            'libappindicator: application indicator support'
            'libindicate: integrate to Message Menu')
options=(!emptydirs)
source=(http://launchpad.net/$pkgname/${pkgver%.?.*}/${pkgver%.?.*}/+download/$pkgname-$pkgver.tar.gz
        launchpad-export.tar.gz
        gwibber.desktop)
md5sums=('77ebabf036621d3b35fff57e530017ba'
         '961f7a9bbf7bfe0bb8dd5db60071450f'
         '6375e4874fa720f900c9407348998716')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  rename $pkgname- '' ../po/$pkgname-*.po
  mv -f -t po ../po/*

  find . -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'

  python2 setup.py build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 setup.py install --root=$pkgdir/ --optimize=1

  install -D -m644 $srcdir/gwibber.desktop $pkgdir/etc/xdg/autostart/gwibber.desktop
}

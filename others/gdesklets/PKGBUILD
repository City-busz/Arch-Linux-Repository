# Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: nifan <carlos@nifan.tk>
# Contributor: Roman Kyrylych <roman@archlinux.org>

pkgname=gdesklets
pkgver=0.36.3
pkgrel=1
pkgdesc="A system for bringing mini programs (desklets) onto your desktop, where they are sitting there in a symbiotic relationship of eye candy and usefulness"
arch=('i686' 'x86_64')
url="http://gdesklets.de/"
license=('GPL') 
depends=('libgtop>=2.10.0' 'librsvg>=2.8.0' 'python2-bonobo' 'python2-gconf' 'python2-gnomevfs' 'desktop-file-utils' 'shared-mime-info' 'gnome-icon-theme' 'xdg-utils')
makedepends=('gnome-python>=2.12.0' 'intltool')
options=(!libtool)
install=$pkgname.install
source=(http://gdesklets.de/files/$pkgname-$pkgver.tar.bz2)
md5sums=('60c0c4e7301b3e653702b6fe5bf7e698')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  find . -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'
  sed -i 's/python -c/$PYTHON -c/' configure
  sed -i 's/python/python2/' shell/plugins/PackageInstaller/__init__.py

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static \
              PYTHON=python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # install missing desktop icon
  install -Dm644 data/$pkgname.png "$pkgdir/usr/share/pixmaps/$pkgname.png"

  # remove unnecessary .in files
  rm "$pkgdir"/usr/share/{mime/packages/gdesklets.xml,applications/gdesklets.desktop}.in
}

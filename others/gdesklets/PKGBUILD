# Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: nifan <carlos@nifan.tk>
# Contributor: Roman Kyrylych <roman@archlinux.org>

pkgname=gdesklets
pkgver=0.36.3
pkgrel=2
pkgdesc="A system for bringing mini programs (desklets) onto your desktop, where they are sitting there in a symbiotic relationship of eye candy and usefulness"
arch=('i686' 'x86_64')
url="http://gdesklets.de/"
license=('GPL') 
depends=('libgtop>=2.10.0' 'librsvg>=2.8.0' 'python2-bonobo' 'python2-gconf' 'python2-gnomevfs' 'desktop-file-utils' 'shared-mime-info' 'gnome-icon-theme' 'xdg-utils')
makedepends=('gnome-python>=2.12.0' 'intltool')
options=(!libtool)
install=$pkgname.install
source=(http://gdesklets.de/files/$pkgname-$pkgver.tar.bz2
        gdesklets-core-0.36.3-.in-files.patch)
md5sums=('60c0c4e7301b3e653702b6fe5bf7e698'
         '27839bbefc2f6664ff31cff2d48dfeed')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np0 -i "$srcdir/gdesklets-core-0.36.3-.in-files.patch"
  find . -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'
  sed -i 's/python -c/$PYTHON -c/' configure
  sed -i 's/python/python2/' shell/plugins/PackageInstaller/__init__.py

  automake
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static \
              PYTHON=python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

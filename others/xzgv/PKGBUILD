# Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: Alexander Baldeck <alexander@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=xzgv
pkgver=0.9.1
pkgrel=2
pkgdesc="A picture viewer for X, with a thumbnail-based file selector"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/xzgv/"
license=('GPL')
depends=('gtk2')
install=$pkgname.install
source=(http://downloads.sourceforge.net/sourceforge/xzgv/${pkgname}-${pkgver}.tar.gz
        xzgv-infoinstall.patch
        xzgv.desktop)
md5sums=('d6452ebd4911d986fa1176086afb6e33'
         '5578f2d2521f060fec127cc07f69b578'
         'fa2c04aa696e23e28f2dec0ef89f5493')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i "$srcdir/xzgv-infoinstall.patch"

  unset LDFLAGS
  make all
}

package() {   
  cd "$srcdir/$pkgname-$pkgver"

  make BINDIR="$pkgdir/usr/bin" \
       MANDIR="$pkgdir/usr/share/man/man1" \
       INFODIR="$pkgdir/usr/share/info" \
       install

  #install desktop file and icon
  install -Dm644 "$srcdir/xzgv.desktop" "$pkgdir/usr/share/applications/xzgv.desktop"
  install -Dm644 src/icon-64.xpm "$pkgdir/usr/share/pixmaps/xzgv.xpm"
}


# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=('backintime-common' 'backintime-gnome')
pkgbase=backintime
pkgver=1.0.6
pkgrel=1
pkgdesc="Simple backup tool for Linux inspired from flyback project and TimeVault"
arch=('any')
url="http://backintime.le-web.org/"
license=('GPL')
source=(http://backintime.le-web.org/download/backintime/$pkgbase-${pkgver}_src.tar.gz)
md5sums=('f66ff5979a5048ce2c85b22c788f765c')

build() {
  cd "$srcdir/$pkgbase-$pkgver/common"
  sed -i 's|^python |python2 |' backintime

  ./configure --prefix=/usr
  make

  cd "$srcdir/$pkgbase-$pkgver/gnome"
  sed -i 's|^python |python2 |' backintime-gnome

  ./configure --prefix=/usr
  make
}

package_backintime-common() {
  pkgdesc="Simple backup tool for Linux inspired from flyback project and TimeVault (common files)"
  depends=('python2' 'rsync')

  cd "$srcdir/$pkgbase-$pkgver/common"

  make DESTDIR="$pkgdir/" install
}

package_backintime-gnome() {
  pkgdesc="Simple backup tool for Linux inspired from flyback project and TimeVault (Gnome GUI)"
  depends=('backintime-common' 'python2-libgnome' 'python2-gnomevfs' 'python-notify' 'meld')

  cd "$srcdir/$pkgbase-$pkgver/gnome"

  make DESTDIR="$pkgdir/" install
}

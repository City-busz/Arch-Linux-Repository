# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=('backintime-common' 'backintime-gnome' 'backintime-kde4')
pkgbase=backintime
pkgver=1.0.8
pkgrel=1
pkgdesc="Simple backup tool for Linux inspired from flyback project and TimeVault"
arch=('any')
url="http://backintime.le-web.org/"
license=('GPL')
source=(http://backintime.le-web.org/download/backintime/$pkgbase-${pkgver}_src.tar.gz)
md5sums=('676c743cb64fdc646c290c62e45640ff')

build() {
  cd "$srcdir/$pkgbase-$pkgver/common"
  sed -i 's|^python |python2 |' backintime

  ./configure
  make

  cd "$srcdir/$pkgbase-$pkgver/gnome"
  sed -i 's|^python |python2 |' backintime-gnome

  ./configure --no-check
  make

  cd "$srcdir/$pkgbase-$pkgver/kde4"
  sed -i 's|^python |python2 |' backintime-kde4

  ./configure --no-check
  make
}

package_backintime-common() {
  pkgdesc="Simple backup tool for Linux inspired from flyback project and TimeVault (common files)"
  depends=('python2' 'rsync')

  cd "$srcdir/$pkgbase-$pkgver/common"

  make DESTDIR="$pkgdir/" install
}

package_backintime-gnome() {
  pkgdesc="Simple backup tool for Linux inspired from flyback project and TimeVault (Gnome GUI)"
  depends=('backintime-common' 'python2-libgnome' 'python-notify')

  cd "$srcdir/$pkgbase-$pkgver/gnome"

  make DESTDIR="$pkgdir/" install
}

package_backintime-kde4() {
  pkgdesc="Simple backup tool for Linux inspired from flyback project and TimeVault (KDE4 GUI)"
  depends=('backintime-common' 'kdebindings-python')

  cd "$srcdir/$pkgbase-$pkgver/gnome"

  make DESTDIR="$pkgdir/" install
}

# Maintainer: György Balló <ballogy@freestart.hu>
pkgbase=compiz-dev
_pkgbase=compiz
pkgname=(compiz-core-dev compiz-decorator-gtk-dev compiz-decorator-kde-dev)
pkgver=0.9.5.92.1
pkgrel=1
arch=('i686' 'x86_64')
url="http://www.compiz.org/"
license=('MIT')
makedepends=(
# Build depends
'cmake' 'boost>=1.34.0' 'automoc4' 'intltool'
# Core depends
'libxcomposite' 'libxrandr' 'libxinerama' 'libxslt' 'glibmm' 'startup-notification>=0.7' 'mesa' 'dbus-core' 'librsvg' 'libsigc++' 'boost-libs'
# GTK+ depends
'metacity' 'gnome-control-center'
# KDE depends
'kdebase-workspace')
source=(http://releases.compiz.org/$pkgver/$_pkgbase-$pkgver.tar.bz2)
md5sums=('c45dd77bd1937b75c850f80a411e46cb')

build() {
  cd "$srcdir/$_pkgbase-$pkgver"

  [[ -d build ]] || mkdir build
  cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -DCOMPIZ_DESTDIR="$pkgdir/$pkgname"
  make
}

package_compiz-core-dev() {
  pkgdesc="Composite manager for Aiglx and Xgl"
  depends=('glibmm' 'startup-notification' 'librsvg' 'libxcomposite' 'dbus-core' 'libsigc++' 'libxinerama' 'boost-libs' 'mesa' 'libxrandr' 'libxslt')
  provides=(compiz-core=0.9.5.92.1)
  conflicts=(compiz-core)

  cd "$srcdir/$_pkgbase-$pkgver/build"

  make install
  make findcompiz_install

  install -Dm644 "$srcdir/$_pkgbase-$pkgver/COPYING.MIT" "$pkgdir/usr/share/licenses/${pkgname[0]}/COPYING.MIT"

  # Split gtk and kde stuff
  [[ -d $srcdir/gtk ]] && rm -r "$srcdir/gtk/"
  mkdir "$srcdir/gtk/"
  mv "$pkgdir"/usr/{share/{gconf,glib-2.0},bin/gtk-window-decorator} \
    "$srcdir/gtk/"
  [[ -d $srcdir/kde ]] && rm -r "$srcdir/kde/"
  mkdir "$srcdir/kde/"
  mv "$pkgdir"/usr/{bin/kde4-window-decorator,lib/compiz/libkde.so,share/compiz/kde.xml} \
    "$srcdir/kde/"
}

package_compiz-decorator-gtk-dev() {
  pkgdesc="Compiz decorator for GNOME"
  depends=('compiz-core' 'metacity' 'gnome-control-center')
  provides=(compiz-decorator-gtk=0.9.5.92.1)
  conflicts=(compiz-decorator-gtk)
  install=compiz-decorator-gtk.install

  cd "${srcdir}/gtk"
  mkdir -p "$pkgdir"/usr/{share/gconf/schemas,bin}

  mv glib-2.0             "$pkgdir/usr/share/"
  mv gtk-window-decorator "$pkgdir/usr/bin/"

  gconf-merge-schema ${pkgdir}/usr/share/gconf/schemas/compiz-decorator-gtk.schemas \
        $srcdir/gtk/gconf/schemas/*.schemas

  install -Dm644 "$srcdir/$_pkgbase-$pkgver/COPYING.MIT" "$pkgdir/usr/share/licenses/${pkgname[1]}/COPYING.MIT"
}

package_compiz-decorator-kde-dev() {
  pkgdesc="Compiz decorator for KDE"
  depends=('compiz-core' 'kdebase-workspace')
  provides=(compiz-decorator-kde=0.9.5.92.1)
  conflicts=(compiz-decorator-kde)

  cd "${srcdir}/kde"
  mkdir -p "$pkgdir"/usr/{bin,lib/compiz,share/compiz}

  mv kde4-window-decorator "$pkgdir/usr/bin/"
  mv libkde.so             "$pkgdir/usr/lib/compiz/"
  mv kde.xml               "$pkgdir/usr/share/compiz/"

  install -Dm644 "$srcdir/$_pkgbase-$pkgver/COPYING.MIT" "$pkgdir/usr/share/licenses/${pkgname[2]}/COPYING.MIT"
}

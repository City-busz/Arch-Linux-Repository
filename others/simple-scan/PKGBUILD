# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=simple-scan
pkgver=2.32.0.1
pkgrel=1
pkgdesc="Simple Scanning Utility"
arch=('i686' 'x86_64')
url="https://launchpad.net/simple-scan"
license=('GPL')
depends=('gconf' 'sane')
makedepends=('intltool>=0.35.0' 'gnome-doc-utils>=0.3.2')
install=$pkgname.install
source=(http://launchpad.net/$pkgname/trunk/$pkgver/+download/$pkgname-$pkgver.tar.gz)
md5sums=('2fd23b9ccb41b4b99c42d8ac242fabfc')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-schemas-install --disable-scrollkeeper \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

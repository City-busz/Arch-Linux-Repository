# Maintainer: D. Can Celasun <dcelasun[at]gmail[dot]com>

pkgname=indicator-feed
pkgver=1.01
pkgrel=1
pkgdesc="RSS feed updates indicator"
arch=('any')
url="http://code.google.com/p/feedindicator/"
license=('GPL3')
depends=('indicator-application' 'python-feedparser' 'python2-gconf' 'python-notify' 'hicolor-icon-theme' 'xdg-utils')
install=$pkgname.install
source=(http://feedindicator.googlecode.com/files/feedindicator-20110208.tar.gz)
md5sums=('fffbddf363825188da5a4ae7ad41ab00')

build() {
  # Fix the python shebang
  sed -i 's|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|' feedindicator.py

  # Fix the stupidly coded desktop file
  sed -i 's|Exec=python /usr/bin/feedindicator.py|Exec=/usr/bin/feedindicator|' Feedindicator.desktop
}

package() {
  # Install icons
  install -Dm644 dark/indicator-feedindicator.png "$pkgdir/usr/share/icons/ubuntu-mono-dark/16x16/apps/indicator-feedindicator.png"
  install -Dm644 dark/indicator-feedindicator-attention.png "$pkgdir/usr/share/icons/ubuntu-mono-dark/16x16/apps/indicator-feedindicator-attention.png"
  install -Dm644 light/indicator-feedindicator.png "$pkgdir/usr/share/icons/ubuntu-mono-light/16x16/apps/indicator-feedindicator.png"
  install -Dm644 light/indicator-feedindicator-attention.png "$pkgdir/usr/share/icons/ubuntu-mono-light/16x16/apps/indicator-feedindicator-attention.png"
  install -Dm644 light/indicator-feedindicator.png "$pkgdir/usr/share/icons/hicolor/16x16/apps/indicator-feedindicator.png"
  install -Dm644 light/indicator-feedindicator-attention.png "$pkgdir/usr/share/icons/hicolor/16x16/apps/indicator-feedindicator-attention.png"

  # Install desktop file
  install -Dm644 Feedindicator.desktop "$pkgdir/usr/share/applications/feedindicator.desktop"

  # Install logo and icon
  install -Dm644 feedindicator-icon.png "$pkgdir/usr/share/feedindicator/feedindicator-icon.png"
  install -Dm644 feedindicator-logo.png "$pkgdir/usr/share/feedindicator/feedindicator-logo.png"

  # Install the program
  install -Dm755 feedindicator.py "$pkgdir/usr/bin/feedindicator"
}


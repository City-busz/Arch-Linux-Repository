# Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: Konrad <konrad AT knauber DOT name>

pkgname=pinta
pkgver=0.7
pkgrel=1
pkgdesc="A drawing/editing program modeled after Paint.NET. It's goal is to provide a simplified alternative to GIMP for casual users"
arch=('any')
url="http://pinta-project.com/"
license=('MIT')
depends=('gtk-sharp-2' 'desktop-file-utils' 'hicolor-icon-theme' 'xdg-utils')
install=$pkgname.install
source=(https://github.com/downloads/jpobst/Pinta/$pkgname-$pkgver.tar.gz)
md5sums=('e3fee39eb81ba992cce5a220f71ad351')

build() {
  cd "$srcdir"/$pkgname-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver

  make DESTDIR="$pkgdir/" install

  install -dm755 $pkgdir/usr/share/licenses/$pkgname
  install -m644 {license-*,readme}.txt $pkgdir/usr/share/licenses/$pkgname/
}

# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=pinta
pkgver=0.5
pkgrel=3
pkgdesc="A drawing/editing program modeled after Paint.NET. It's goal is to provide a simplified alternative to GIMP for casual users"
arch=('any')
url="http://pinta-project.com/"
license=('MIT')
depends=('gtk-sharp-2' 'hicolor-icon-theme' 'xdg-utils')
install=$pkgname.install
source=($pkgname-$pkgver.tar.gz::https://github.com/jpobst/Pinta/tarball/$pkgver)
md5sums=('c95faaa7e5735675586d8c3d98e866fc')

build() {
  cd "$srcdir"/jpobst-Pinta-*

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd "$srcdir"/jpobst-Pinta-*

  make DESTDIR="$pkgdir/" install

  install -dm755 $pkgdir/usr/share/licenses/$pkgname
  install -m644 {license-*,readme}.txt $pkgdir/usr/share/licenses/$pkgname/
}

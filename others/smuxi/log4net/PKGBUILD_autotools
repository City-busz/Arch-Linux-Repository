# Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: Frane Bandov <frane@offbyte.com>
# Contributor: Thijs Vermeir <thijsvermeir@gmail.com>

pkgname=log4net
pkgver=1.2.10
pkgrel=3
pkgdesc="A tool to help the programmer output log statements to a variety of output targets"
arch=('any')
url="http://logging.apache.org/log4net/"
license=('APACHE')
depends=('mono')
makedepends=('monodevelop' 'unzip')
source=(http://archive.apache.org/dist/incubator/$pkgname/$pkgver/incubating-$pkgname-$pkgver.zip)
md5sums=('0085970684888a5dfb1d59b2831f8d7e')

build() {
  cd "$srcdir/$pkgname-$pkgver/src"
  mdtool generate-makefiles -d:Release $pkgname.sln

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver/src"

  make DESTDIR="$pkgdir/" install
}

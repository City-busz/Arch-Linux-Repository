# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=openbve
pkgver=1.2.10.0
pkgrel=1
pkgdesc="Free-as-in-freedom train simulator placed in the public domain"
arch=('any')
url="http://openbve.trainsimcentral.co.uk/"
license=('custom:public domain')
depends=('mono' 'taoframework' 'sdl' 'openal')
makedepends=('unzip')
source=(http://openbve.trainsimcentral.co.uk/common/openbve_stable_source.zip
        http://openbve.trainsimcentral.co.uk/common/openbve_stable.zip
        openbve.sh
        openbve.desktop)
md5sums=('4b9b6c7025069ff420857df4b2d9bf07'
         'b06843f48d902438138605f87021cc54'
         'fec92946f773f56588a71de310847ce0'
         'acb0dd3ea602910181b8cdb8375c794e')

build() {
  cd "$srcdir/openBVE"
 
  xbuild /p:Configuration=Release OpenBve/OpenBve.csproj
}

package() {
  cd "$srcdir"

  #binaries
  install -d $pkgdir/usr/lib/$pkgname/
  install -Dm644 openBVE/OpenBveApi/bin/Release/OpenBveApi.dll $pkgdir/usr/lib/$pkgname/
  install -Dm644 openBVE/OpenBve/bin/Release/OpenBve.exe $pkgdir/usr/lib/$pkgname/

  #data
  mkdir -p $pkgdir/usr/share/games/openbve
  cp -r Data $pkgdir/usr/share/games/openbve/

  #executable
  install -Dm755 openbve.sh $pkgdir/usr/bin/openbve

  #desktop file and icon
  install -Dm644 openbve.desktop $pkgdir/usr/share/applications/openbve.desktop
  mkdir -p $pkgdir/usr/share/pixmaps
  ln -s ../games/openbve/Data/Menu/logo.png $pkgdir/usr/share/pixmaps/openbve.png

  #license
  install -Dm644 Readme.txt $pkgdir/usr/share/licenses/$pkgname/Readme.txt
}

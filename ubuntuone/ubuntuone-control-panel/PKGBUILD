# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=ubuntuone-control-panel
pkgver=2.99.1
pkgrel=2
pkgdesc="Graphical user interface that allows the users to manage their Ubuntu One subscription and preferences"
arch=('any')
url="https://launchpad.net/ubuntuone-control-panel"
license=('GPL')
depends=('libsoup-gnome' 'python2-gobject' 'ubuntuone-client')
makedepends=('python-distutils-extra' 'python2-pyqt')
optdepends=('python2-qt4reactor: Qt GUI for Ubuntu One')
source=(http://launchpad.net/ubuntuone-control-panel/stable-3-0/$pkgver/+download/$pkgname-$pkgver.tar.gz
        ubuntuone-control-panel-gtk.desktop
        noapt.patch
        launchpad-export.tar.gz)
md5sums=('0655c609444e3431dfd7c3c456051a6c'
         '938fa3c782f926b63687deb33a3c2243'
         '8e389db6568d739c07a366618c624967'
         'a86d98fe989005e8b5af78ef558f9fd2')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' bin/ubuntuone-control-panel-backend
  sed -i 's|ubuntuone-installer.desktop|ubuntuone-control-panel-gtk.desktop|' ubuntuone-control-panel.in
  patch -Np1 -i "$srcdir/noapt.patch"

  echo 'am ar ast be bg bn bs ca ca@valencia cs da de el en_AU en_GB eo es et eu fi fr fy gd gl he hi hr hu hy id it ja kk ko ku ky lt lv mr ms nb ne nl nn oc pa pl pt pt_BR ro ru si sk sl sq sr sv ta te th tr ug uk vi zh_CN zh_HK zh_TW' >po/LINGUAS
  rename $pkgname- '' ../po/$pkgname-*.po
  mv -f -t po ../po/*

  python2 setup.py build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 setup.py install --root=$pkgdir/ --optimize=1

  # Install desktop file and icon
  install -dm755 "$pkgdir/usr/share/pixmaps"
  ln -s ../ubuntuone-control-panel/u1icon.png "$pkgdir/usr/share/pixmaps/ubuntuone.png"
  install -Dm644 "$srcdir/ubuntuone-control-panel-gtk.desktop" "$pkgdir/usr/share/applications/ubuntuone-control-panel-gtk.desktop"
}

# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=ubuntuone-client
pkgver=2.99.1
pkgrel=1
pkgdesc="Ubuntu One helps you store, sync and share files. Sync your files, share your work with others or work remotely, all with your Ubuntu computer"
arch=('i686' 'x86_64')
url="https://launchpad.net/ubuntuone-client"
license=('GPL')
depends=('python2-configglue' 'ubuntu-sso-client' 'python-notify' 'python2-pyinotify' 'python2-ubuntuone-storageprotocol' 'xdg-utils')
makedepends=('intltool' 'imake' 'gobject-introspection' 'vala')
options=('!libtool')
install=$pkgname.install
source=(http://launchpad.net/ubuntuone-client/stable-3-0/$pkgver/+download/$pkgname-$pkgver.tar.gz
        launchpad-export.tar.gz)
md5sums=('607edee0c51a9db7ee8ad92d3a0924e9'
         '03756a686babcad6fe290f40058b7441')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' bin/*
  echo 'ar ast be bg bn bo bs ca ca@valencia ckb cy csb da de el en_AU en_GB eo es et eu fi fo fr ga gd gl gu he hi hr hu hy id is it ja jv ka ko ku ky lo lt lv ml ms my nb nds nl nn oc pa pl pt pt_BR ro ru sd si sk sl sr sv te th tr uk uz vi zh_CN zh_HK zh_TW' >po/LINGUAS
  rename $pkgname- '' ../po/$pkgname-*.po
  mv -f -t po ../po/*

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static \
              PYTHON=python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # We don't have Apport in Arch Linux
  rm -r "$pkgdir"/{etc/apport,usr/share/apport}
}

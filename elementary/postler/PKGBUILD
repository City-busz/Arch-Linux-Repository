# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=postler
pkgver=0.1.0
pkgrel=1
pkgdesc="A super sexy, ultra simple desktop mail client built in vala"
arch=('i686' 'x86_64')
url="https://launchpad.net/postler"
license=('GPL')
depends=('libunique>=0.9' 'libwebkit>=1.1.18' 'libnotify' 'libcanberra' 'libindicate' 'desktop-file-utils' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('python2' 'vala>=0.10.0' 'intltool' 'msmtp')
optdepends=('dexter: provides contact completion and allows adding of contacts from Postler'
            'lynx: enables HTML to text conversion')
install=$pkgname.install
source=(http://git.xfce.org/apps/$pkgname/snapshot/$pkgname-$pkgver.tar.bz2)
md5sums=('0247cdf1ab8e2d919c9299a52646f47a')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 ./waf configure --prefix=/usr
  python2 ./waf build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 ./waf install --destdir=$pkgdir

  mkdir -p "$pkgdir/usr/share/indicators/messages/applications"
  echo '/usr/share/applications/postler.desktop' > "$pkgdir/usr/share/indicators/messages/applications/postler"
}

# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=dexter
_pkgname=dexter-rolodex
pkgver=0.18
pkgrel=1
pkgdesc="A sexy, simple address book with end users in mind"
arch=('any')
url="https://launchpad.net/dexter-rolodex"
license=('GPL')
depends=('dbus-python' 'python2-pyenchant' 'pygtk' 'python-storm' 'python-vobject' 'pyxdg' 'desktop-file-utils')
makedepends=('python-distutils-extra')
options=(!emptydirs)
install=$pkgname.install
source=(http://launchpad.net/$_pkgname/0.x/$pkgver/+download/${pkgname}_$pkgver.tar.gz)
md5sums=('740a7781477afad1492f9aa362ff2cee')

build() {
  cd "$srcdir/$_pkgname"
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' dexter/backend/models.py

  python2 setup.py build
}

package() {
  cd "$srcdir/$_pkgname"

  python2 setup.py install --root=$pkgdir/ --optimize=1

  # We don't have apport on Arch Linux
  rm -r "$pkgdir/etc"
  rm -r "$pkgdir/usr/share/apport"
}

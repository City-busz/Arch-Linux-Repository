# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=purple
pkgver=11.02.1
pkgrel=1
pkgdesc="The sexiest dictionary on Earth and Jupiter"
arch=('any')
url="https://launchpad.net/purple"
license=('GPL')
depends=('dbus-python' 'pygtk' 'python-simplejson' 'python-storm' 'desktop-file-utils')
makedepends=('python-distutils-extra')
options=(!emptydirs)
source=(http://launchpad.net/purple/trunk/11.02.1/+download/${pkgname}_$pkgver.tar.gz)
md5sums=('64bbc4036b7fb0965755f676d63b7884')

build() {
  cd "$srcdir/$pkgname"
  # Remove unrequired file association
  sed -i '/MimeType=/ d' purple.desktop.in

  python2 setup.py build
}

package() {
  cd "$srcdir/$pkgname"

  python2 setup.py install --root=$pkgdir/ --optimize=1

  # We don't have apport in Arch Linux
  rm -r "$pkgdir/etc"
  rm -r "$pkgdir/usr/share/apport"
}

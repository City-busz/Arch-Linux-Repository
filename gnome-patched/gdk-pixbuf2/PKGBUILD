# $Id: PKGBUILD 114338 2011-03-12 18:07:51Z ibiru $
# Maintainer: Ionut Biru <ibiru@archlinux.org>
pkgname=gdk-pixbuf2
pkgver=2.22.1
pkgrel=2.1
pkgdesc="An image loading library for gtk2"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
license=('GPL2')
depends=('glib2>=2.26.0' 'libpng>=1.4.3' 'libtiff>=3.9.4' 'libjpeg' 'libx11')
makedepends=('gtk-doc' 'gobject-introspection')
options=('!libtool')
install=gdk-pixbuf2.install
source=(http://download.gnome.org/sources/gdk-pixbuf/2.22/gdk-pixbuf-${pkgver}.tar.bz2
        065_gir_set_packages.patch)
sha256sums=('6ce87eda24af9362307b2593c154d0b660f4e26d0abf2e71d46d0ddd55fd953d'
            '5b20ca104dcb87ba314dca932cfbb9253b6b5910dc822fa6d0e39b95a5df271f')

build() {
  cd "${srcdir}/gdk-pixbuf-${pkgver}"
  patch -Np1 -i "$srcdir/065_gir_set_packages.patch"

  automake
  ./configure --prefix=/usr \
    --without-libjasper \
    --with-included-loaders=png
  make
}

package() {
  cd "${srcdir}/gdk-pixbuf-${pkgver}"

  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:

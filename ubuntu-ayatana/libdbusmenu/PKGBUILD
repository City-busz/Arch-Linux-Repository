# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=libdbusmenu
pkgver=0.3.16
pkgrel=1
pkgdesc="A small little library that was created by pulling out some comon code out of indicator-applet"
arch=('i686' 'x86_64')
url="https://launchpad.net/dbusmenu"
license=('LGPL')
depends=('dbus-glib>=0.76' 'gtk2>=2.16' 'json-glib>=0.6.0' 'dbus-python')
makedepends=('intltool' 'vala')
options=('!libtool')
source=(http://launchpad.net/dbusmenu/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.gz)
md5sums=('9b53dd4db3f92fd717a1e8132bc35ee0')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' tools/dbusmenu-bench

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib \
              --disable-static --disable-introspection
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

# Maintainer: Balló György <ballogy@freestart.hu>
pkgname=indicator-sound
pkgver=0.5.0.1
pkgrel=3
pkgdesc="A unified sound menu"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-sound"
license=('GPL')
groups=('ayatana')
depends=('libindicator>=0.3.6' 'ido>=0.1.11' 'libpulse>=0.9.19' 'libindicate>=0.4.1' 'libgee' 'gconf>=2.0')
makedepends=('intltool>=0.35.0' 'vala>=0.11.2')
options=(!libtool)
install=$pkgname.install
source=(http://launchpad.net/$pkgname/sound-menu-v2/$pkgver/+download/$pkgname-$pkgver.tar.gz
        launchpad-export.tar.gz
        vala-0.12.patch)
md5sums=('b06c87c57358c58f8c07caa74fcb7389'
         '1685d672a20603a06f1d85e9615da8ad'
         '87fc450c589e73f5aa379ed0ecae0be2')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  rename $pkgname- '' ../po/$pkgname-*.po
  mv -f -t po ../po/*
  echo 'ace af am an ar ast be bg bn br bs ca crh cy cs csb da de el en_AU en_CA en_GB eo es et eu fa fi fil fr fy gl gv he hi hr hu hy id is it ja ka kk km kn ko ku lb lo lt lv mk ml mr ms nb ne nl nn oc pl pt pt_BR ro ru sc sd si sk sl sml sn sq sr sv ta th tr ug uk ur vi zh_CN zh_HK zh_TW' >po/LINGUAS

  patch -Np0 -i "$srcdir/vala-0.12.patch"

  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/indicator-sound \
              --disable-static --disable-schemas-install \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas \
              CFLAGS="$CFLAGS -I/usr/include/gio-unix-2.0/"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

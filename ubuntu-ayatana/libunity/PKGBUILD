# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=libunity
pkgver=4.0.0
pkgrel=1
pkgdesc="A library for instrumenting and integrating with all aspects of the Unity shell"
arch=('i686' 'x86_64')
url="https://launchpad.net/libunity"
license=('LGPL')
depends=('libgee>=0.6.0' 'dee>=0.5.12' 'libdbusmenu>=0.3.93' 'dbus-glib')
makedepends=('vala>=0.11.3' 'python2' 'gobject-introspection>=0.10.0')
options=(!libtool)
source=(http://launchpad.net/$pkgname/$pkgver/$pkgver/+download/$pkgname-$pkgver.tar.gz)
md5sums=('cfaeb371f9ff75d6de7cf7cb5a560d22')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static \
              PYTHON=python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

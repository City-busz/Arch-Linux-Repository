# Maintainer: György Balló <ballogy@freestart.hu>
pkgbase=libindicate-gtk
_pkgbase=libindicate
pkgname=(libindicate-gtk2 libindicate-gtk3)
pkgver=0.5.91
pkgrel=2
pkgdesc="A small library for applications to raise 'flags' on DBus for other components of the desktop to pick up and visualize"
arch=('i686' 'x86_64')
url="https://launchpad.net/libindicate"
license=('LGPL')
makedepends=("libindicate=$pkgver" 'gtk2>=2.12' 'gtk3>=3.0' 'gobject-introspection>=0.6.7' 'vala' 'gtk-sharp-2>=2.12.1' 'pygtk>=2.14.0' 'gnome-doc-utils>=0.3.2')
options=(!libtool)
source=(http://launchpad.net/$_pkgbase/0.6/$pkgver/+download/$_pkgbase-$pkgver.tar.gz)
md5sums=('81a2b1b5df3767938795c44b270fe167')

build() {
  cd "$srcdir/$_pkgbase-$pkgver"
  sed -i 's/-lpyglib-2\.0-python$PYTHON_VERSION/-lpyglib-2.0-python2/' configure{,.ac}

  [[ -d build-gtk2 ]] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static --with-gtk=2 \
              PYTHON=python2
  make
  popd

  [[ -d build-gtk3 ]] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static \
              PYTHON=python2
  make
  popd
}

package_libindicate-gtk2() {
  pkgdesc+=" (GTK+ 2 library)"
  depends=("libindicate=$pkgver" 'gtk2>=2.12')

  cd "$srcdir/$_pkgbase-$pkgver/build-gtk2"

  make -C libindicate-gtk DESTDIR="$pkgdir/" install
  make -C bindings/python DESTDIR="$pkgdir/" install
  make -C bindings/mono/indicate-gtk DESTDIR="$pkgdir/" install

  rm -r "$pkgdir/usr/include"
}

package_libindicate-gtk3(){
  pkgdesc+=" (GTK+ 3 library)"
  depends=("libindicate=$pkgver" 'gtk3>=3.0')

  cd "$srcdir/$_pkgbase-$pkgver/build-gtk3"

  make -C libindicate-gtk DESTDIR="$pkgdir/" install

  rm -r "$pkgdir/usr/include"
}

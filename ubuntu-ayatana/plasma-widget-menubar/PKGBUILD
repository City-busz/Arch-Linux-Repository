# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=plasma-widget-menubar
pkgver=0.1.16
pkgrel=1
pkgdesc="A Plasma widget to display menubar of application windows"
arch=('i686' 'x86_64')
url="https://launchpad.net/plasma-widget-menubar"
license=('GPL')
depends=('kdelibs>=4.4' 'qjson')
makedepends=('cmake>=2.6' 'automoc4')
optdepends=('appmenu-gtk2: support for GTK+ 2 apps'
            'appmenu-gtk3: support for GTK+ 3 apps'
            'appmenu-qt: support for Qt apps'
            'libreoffice-extension-menubar: support for LibreOffice')
source=(http://launchpad.net/$pkgname/trunk/$pkgver/+download/$pkgname-$pkgver.tar.bz2)
md5sums=('ee8566cd175b552726bc6d74dbb8756b')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  mkdir build
  cd build
  cmake .. -DCMAKE_BUILD_TYPE=Release \
           -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver/build"

  make DESTDIR="$pkgdir/" install
}


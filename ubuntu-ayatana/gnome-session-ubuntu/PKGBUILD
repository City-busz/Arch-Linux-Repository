# Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-session-ubuntu
_pkgname=gnome-session
pkgver=3.2.1
pkgrel=1
pkgdesc="The GNOME Session Handler with Ubuntu's modifications"
arch=(i686 x86_64)
license=('GPL' 'LGPL')
depends=('upower' 'gtk3' 'gconf' 'json-glib' 'startup-notification' 'hicolor-icon-theme' 'libxtst' 'polkit-gnome' 'libgl' 'gsettings-desktop-schemas' 'consolekit' 'libsm' 'dconf')
makedepends=('intltool' 'mesa' 'xtrans')
provides=("gnome-session=$pkgver")
conflicts=('gnome-session')
options=('!emptydirs')
install=gnome-session.install
url="http://www.gnome.org"
groups=('gnome')
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgname}/${pkgver%.*}/${_pkgname}-${pkgver}.tar.xz
        95_dbus_request_shutdown.patch)
sha256sums=('a87dcf57fec83cc8fad4e7b84542c5005a70a2107d184f247f0b112f07a043b3'
            '49cbbbeecf1ae3adae42c3a34d7b5769ed90d1cbe1303d01637a02f63ca5f089')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  # Add "RequestShutdown" and "RequestReboot" DBus methods (required for indicator-session)
  patch -Np1 -i "${srcdir}/95_dbus_request_shutdown.patch"

  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --libexecdir=/usr/lib/gnome-session \
      --disable-schemas-compile
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

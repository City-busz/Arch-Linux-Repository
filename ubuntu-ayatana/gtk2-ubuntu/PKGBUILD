# Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=gtk2-ubuntu
pkgname=('gtk2-ubuntu' 'gtk-update-icon-cache-ubuntu')
pkgver=2.24.7
pkgrel=1
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
makedepends=('gtk-doc' 'atk' 'pango' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage'
             'shared-mime-info' 'cairo' 'libcups' 'gdk-pixbuf2' 'gobject-introspection')
options=('!libtool' '!docs')
license=('LGPL')
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/2.24/gtk+-${pkgver}.tar.xz
        xid-collision-debug.patch
        010_make_bg_changes_queue_repaint.patch
        012_ubuntu-set-grab-add.patch
        043_ubuntu_menu_proxy.patch
        044_grips.patch
        062_dnd_menubar.patch
        071_no_offscreen_widgets_grabbing.patch
        100_overlay_scrollbar_loading.patch)
sha256sums=('1fdc7605501bd2932ef9a3c4c3c9a406e505f18667aea9e73af650da28fff994'
            'd758bb93e59df15a4ea7732cf984d1c3c19dff67c94b957575efea132b8fe558'
            '910feb3b592c5699f7b5b11ca0b5a33b950824eec9ddd063106cad676a627e86'
            '32e6765f7e0bebb135eb715561be05ecdc178b1bd24b6ae6526fef8c815e5b00'
            '83280aa0f3ac1e2456656d3e97d804ec5f891737f93799fcb1cc1f1dd0d9a41a'
            '6d128b78f978452fd719fce8a6ec2f589366c7e2d0276c83573e649617ce6193'
            '606e5e6365649a50e3b5736e3031c07edf56b7dfe7b4ac2788f6b8a59a4ad483'
            'e2e2ed1ee10a9ef7672eaa4b44c66c7422e24ca55f50cf4989967ed3392b1fa8'
            '14c658b025c06fb832abf8a5fd5ae911e22276f95051f4f8453cd983880a24ca')

build() {
    cd "${srcdir}/gtk+-${pkgver}"
    patch -Np1 -i "${srcdir}/xid-collision-debug.patch"

    # Make background changes queue a repaint (fix for overlay-scrollbar)
    patch -Np1 -i "${srcdir}/010_make_bg_changes_queue_repaint.patch"

    # Add ubuntu-set-grab-add signal (required for ido)
    patch -Np1 -i "${srcdir}/012_ubuntu-set-grab-add.patch"

    # Add Ubuntu menuproxy module (required for appmenu-gtk)
    patch -Np1 -i "${srcdir}/043_ubuntu_menu_proxy.patch"

    # Add resize grips to GtkWindow (backport from gtk3)
    patch -Np1 -i "${srcdir}/044_grips.patch"

    # Allow windows to be dragged by clicking on empty areas (backport from gtk3)
    patch -Np1 -i "${srcdir}/062_dnd_menubar.patch"

    # Don't let offscreen widget do grabbing (fix for ido)
    patch -Np1 -i "${srcdir}/071_no_offscreen_widgets_grabbing.patch"

    # Add support for the ayatana overlay scrollbars (required for overlay-scrollbar)
    patch -Np1 -i "${srcdir}/100_overlay_scrollbar_loading.patch"

    automake
    CXX=/bin/false ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-xinput=yes

    #https://bugzilla.gnome.org/show_bug.cgi?id=655517
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package_gtk2-ubuntu() {
    pkgdesc="GTK+ is a multi-platform toolkit (v2) with Ubuntu's modifications"
    install=gtk2.install
    depends=('atk' 'pango' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage' 'shared-mime-info' 'cairo' 'libcups' 'gtk-update-icon-cache')
    provides=("gtk2=$pkgver")
    conflicts=('gtk2')
    backup=(etc/gtk-2.0/gtkrc)

    cd "${srcdir}/gtk+-${pkgver}"

    make DESTDIR="${pkgdir}" install
    sed -i "s#env python#env python2#" $pkgdir/usr/bin/gtk-builder-convert
    echo 'gtk-fallback-icon-theme = "gnome"' > "${pkgdir}/etc/gtk-2.0/gtkrc"
    #split this out to use with gtk3 too
    rm ${pkgdir}/usr/bin/gtk-update-icon-cache
}
package_gtk-update-icon-cache-ubuntu() {
    pkgdesc="The GTK+ update icon cache tool with Ubuntu's modifications"
    depends=('gdk-pixbuf2')
    provides=("gtk-update-icon-cache=$pkgver")
    conflicts=('gtk-update-icon-cache')

    cd "${srcdir}/gtk+-${pkgver}/gtk"

    install -D -m755 gtk-update-icon-cache ${pkgdir}/usr/bin/gtk-update-icon-cache
}

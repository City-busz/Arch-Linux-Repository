# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=utouch-geis
pkgver=2.1.2
pkgrel=1
pkgdesc="An implementation of the GEIS (Gesture Engine Interface and Support) interface."
arch=('i686' 'x86_64')
url="https://launchpad.net/utouch-geis"
license=('LGPL')
depends=('dbus-core>=1.2.16' 'utouch-grail>=2.0.0' 'libxi>=1.3' 'python2>=2.7')
makedepends=('xorg-server-devel')
options=(!libtool)
source=(http://launchpad.net/$pkgname/trunk/$pkgver/+download/$pkgname-$pkgver.tar.gz)
md5sums=('c200ebd0cef596599601fea796a709d0')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' tools/geisview/{geisview,__init__.py} python/pygeis
  sed -i 's/python >= 2\.7/python-2.7 >= 2.7/' configure{,.ac}

  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static \
              PYTHON=python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

# Maintainer: György Balló <ballogy@freestart.hu>
pkgbase=appmenu-gtk
pkgname=(appmenu-gtk2 appmenu-gtk3)
pkgver=0.3.0
pkgrel=2
arch=(i686 x86_64)
url="https://launchpad.net/appmenu-gtk"
license=(LGPL)
makedepends=('gtk2-ubuntu>=2.18' 'gtk3-ubuntu>=3.0' 'libdbusmenu-gtk2>=0.3.101' 'libdbusmenu-gtk3>=0.3.101')
options=(!libtool)
source=(http://launchpad.net/appmenu-gtk/0.3/0.3.0/+download/$pkgbase-$pkgver.tar.gz)
md5sums=('2379672a7d00fa1ef5b15c67f76886be')

build() {
  cd "$srcdir/$pkgbase-$pkgver"

  [[ -d build-gtk2 ]] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
               --disable-static --with-gtk2
  make
  popd

  [[ -d build-gtk3 ]] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
               --disable-static
  make
  popd
}

package_appmenu-gtk2() {
  pkgdesc="Adds appmenu support for GTK+ 2 apps"
  depends=('gtk2-ubuntu>=2.18' 'libdbusmenu-gtk2>=0.3.101')

  cd "$srcdir/$pkgbase-$pkgver/build-gtk2"

  make DESTDIR="$pkgdir/" install
  install -Dm755 80appmenu "$pkgdir/etc/profile.d/80appmenu.sh"
  rm -r "$pkgdir/etc/X11"
}

package_appmenu-gtk3() {
  pkgdesc="Adds appmenu support for GTK+ 3 apps"
  depends=('gtk3-ubuntu>=3.0' 'libdbusmenu-gtk3>=0.3.101')

  cd "$srcdir/$pkgbase-$pkgver/build-gtk3"

  make DESTDIR="$pkgdir/" install
  install -Dm755 80appmenu-gtk3 "$pkgdir/etc/profile.d/80appmenu-gtk3.sh"
  rm -r "$pkgdir/etc/X11"
}

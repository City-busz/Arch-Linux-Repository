# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=libappindicator
pkgver=0.3.91
pkgrel=1
pkgdesc="A library to allow applications to export a menu into the Unity Menu bar"
arch=('i686' 'x86_64')
url="https://launchpad.net/libappindicator"
license=('LGPL')
depends=('libindicator>=0.3.5' 'libdbusmenu>=0.3.94')
makedepends=('gobject-introspection>=0.10' 'vala>=0.11.0' 'gtk-sharp-2>=2.12.1' 'pygtk>=2.14.0' 'perl-xml-libxml')
conflicts=('indicator-application<0.2.91')
options=('!libtool')
source=(http://launchpad.net/$pkgname/0.4/$pkgver/+download/$pkgname-$pkgver.tar.gz)
md5sums=('81d93955fb0e09c1817357e112867561')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's/nunit >= 2\.4\.7/mono-nunit >= 2.4.7/' configure{,.ac}

  # Fix mono bindings location
  sed -i 's|assemblydir = $(libdir)/cli/appindicator-sharp-0.1|assemblydir = $(libdir)/appindicator-sharp-0.1|' bindings/mono/Makefile{.am,.in}
  sed -i 's|assemblies_dir=${libdir}/cli/appindicator-sharp-0.1|assemblies_dir=${libdir}/appindicator-sharp-0.1|' bindings/mono/appindicator-sharp-0.1.pc.in

  # Fix headers location
  sed -i 's|libdbusmenu-gtk/|libdbusmenu-gtk3/|' src/app-indicator.c
  sed -i 's|Cflags: -I${includedir}/libappindicator-0.1|Cflags: -I${includedir}/libappindicator3-0.1|' src/appindicator3-0.1.pc.in

  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static \
              --with-gtk=3 \
              PYTHON=python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

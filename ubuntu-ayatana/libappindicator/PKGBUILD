# Maintainer: György Balló <ballogy@freestart.hu>
pkgbase=libappindicator
pkgname=(libappindicator libappindicator3)
pkgver=0.4.1
pkgrel=2
pkgdesc="A library to allow applications to export a menu into the Unity Menu bar"
arch=('i686' 'x86_64')
url="https://launchpad.net/libappindicator"
license=('LGPL')
makedepends=('libindicator>=0.3.5' 'libindicator3>=0.3.5' 'libdbusmenu-gtk2>=0.3.94' 'libdbusmenu-gtk3>=0.3.94' 'gobject-introspection>=0.10' 'vala>=0.11.0' 'gtk-sharp-2>=2.12.1' 'pygtk>=2.14.0' 'perl-xml-libxml' 'gtk2-ubuntu')
options=('!libtool')
source=(http://launchpad.net/$pkgbase/0.4/$pkgver/+download/$pkgbase-$pkgver.tar.gz
        fix-fallback-icons.patch)
md5sums=('4288eae28869e896d984094c1049ea09'
         '41ef0ee7abc6549d83aef94947fd43a7')

build() {
  cd "$srcdir/$pkgbase-$pkgver"
  patch -Np0 -i "$srcdir/fix-fallback-icons.patch"

  sed -i 's/nunit >= 2\.4\.7/mono-nunit >= 2.4.7/' configure{,.ac}
  sed -i 's/-lpyglib-2\.0-python2\.6/-lpyglib-2\.0-python2/' bindings/python/Makefile.{am,in}

  # Fix mono bindings location
  sed -i 's|assemblydir = $(libdir)/cli/appindicator-sharp-0.1|assemblydir = $(libdir)/appindicator-sharp-0.1|' bindings/mono/Makefile.{am,in}
  sed -i 's|assemblies_dir=${libdir}/cli/appindicator-sharp-0.1|assemblies_dir=${libdir}/appindicator-sharp-0.1|' bindings/mono/appindicator-sharp-0.1.pc.in

  # Fix headers location
  sed -i 's|Cflags: -I${includedir}/libappindicator-0.1|Cflags: -I${includedir}/libappindicator3-0.1|' src/appindicator3-0.1.pc.in

  autoreconf -fi

  [[ -d build-gtk2 ]] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --with-gtk=2
  make
  popd

  [[ -d build-gtk3 ]] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static \
              --with-gtk=3
  make
  popd
}

package_libappindicator() {
  pkgdesc+=" (GTK+ 2 library)"
  depends=('libindicator>=0.3.5' 'libdbusmenu-gtk2>=0.3.94')
  conflicts=('indicator-application<0.2.91')

  cd "$srcdir/$pkgbase-$pkgver/build-gtk2"

  make DESTDIR="$pkgdir/" install
}

package_libappindicator3(){
  pkgdesc+=" (GTK+ 3 library)"
  depends=('libindicator3>=0.3.5' 'libdbusmenu-gtk3>=0.3.94')

  cd "$srcdir/$pkgbase-$pkgver/build-gtk3"

  make -C src DESTDIR="$pkgdir/" install
  make -C bindings/vala DESTDIR="$pkgdir/" install
}

# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=indicator-messages
pkgver=0.5.0
pkgrel=2
pkgdesc="A place on the user's desktop that collects messages that need a response"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-messages"
license=('GPL')
depends=('libindicator3>=0.3.19' 'libindicate>=0.4.90' 'libdbusmenu-gtk3>=0.3.94' 'telepathy-glib>=0.9.0')
makedepends=('intltool>=0.35.0')
options=(!libtool)
source=(http://launchpad.net/$pkgname/0.5/$pkgver/+download/$pkgname-$pkgver.tar.gz
        launchpad-export.tar.gz)
md5sums=('48ab966fa16a8890e53c2ae7582ac918'
         '6f18013b8e6145e149b5a64689f2e6bf')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Install language files
  echo 'ace af am an ar ast az be bem bg bn bo br bs ca ca@valencia crh cv cy cs csb da de dv el en_AU en_CA en_GB eo es et eu fa fi fil fr fur fy ga gd gl gu guc gv he hi hr ht hu hy id is it ja ka kk km kn ko ku ky lb lt ltg lv mg mhr mi mk ml mr ms my nan nb ne nl nn nso ny oc or os pa pl ps pt pt_BR ro ru sc sd si sk sl sq sr sv sw ta ta_LK te th tr tt ug uk ur uz vec vi zh_CN zh_HK zh_TW' >po/LINGUAS
  rename $pkgname- '' ../po/$pkgname-*.po
  mv -f -t po ../po/*

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

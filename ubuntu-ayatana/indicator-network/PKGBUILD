# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=indicator-network
pkgver=0.2.14
pkgrel=1
pkgdesc="Network Menu for controlling network connections. Uses Connection Manager."
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-network"
license=('GPL')
groups=('ayatana')
depends=('libindicator>=0.3.0' 'libdbusmenu>=0.2.2' 'ido>=0.1.2' 'libnotify>=0.4.5' 'connman' 'dbus-python' 'pygtk')
makedepends=('intltool')
options=(!libtool)
source=(http://launchpad.net/$pkgname/0.2/$pkgver/+download/$pkgname-$pkgver.tar.gz
        indicator-network-agent.xml)
md5sums=('507bd8ded155f84583857a5ea37e9921'
         'd7d71dae81e9a33ff2e4048238b8c1cb')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  mv ../indicator-network-agent.xml src/
  sed -i 's@^#!.*python$@#!/usr/bin/python2@'  scripts/*

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib \
              --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

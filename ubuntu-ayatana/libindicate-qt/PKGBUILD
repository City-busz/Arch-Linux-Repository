# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=libindicate-qt
pkgver=0.2.5.91
pkgrel=1
pkgdesc="This project provides a set of Qt bindings for libindicate, the indicator system developed by Canonical Desktop Experience team"
arch=('i686' 'x86_64')
url="https://launchpad.net/libindicate-qt"
license=('GPL')
depends=('qt' 'libindicate>=0.5.90')
makedepends=('cmake')
source=(http://launchpad.net/$pkgname/libindicate-0.5/$pkgver/+download/$pkgname-$pkgver.tar.bz2
        01_visibility_hidden.diff
        02_libindicate_06.diff)
md5sums=('67e474d55c8ab0d7d2fd3f9da651eba3'
         '5243a29270a4bedbf0d9edcbe757bccb'
         '74878653d69bc627d2ddb50a3947383e')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i "$srcdir/01_visibility_hidden.diff"
  patch -Np1 -i "$srcdir/02_libindicate_06.diff"

  # Disable building examples and tests
  sed -i '/examples/ d
          /tests/ d' CMakeLists.txt

  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  rm -r "$pkgdir/usr/include/libindicate-qt/CMakeFiles"
}

# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=libindicate
pkgver=0.4.4
pkgrel=4
pkgdesc="A small library for applications to raise "flags" on DBus for other components of the desktop to pick up and visualize"
arch=('i686' 'x86_64')
url="https://launchpad.net/libindicate"
license=('LGPL')
depends=('libdbusmenu>=0.2.2')
makedepends=('vala' 'gtk-sharp-2>=2.12.1' 'pygtk>=2.14.0' 'gobject-introspection' 'gtk-doc' 'gnome-common')
options=(!libtool)
source=(http://launchpad.net/$pkgname/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.gz
        02_gir_fixes.patch)
md5sums=('6d42a710ddd5466263d244e05a273c45'
         '80658fc5227629115d9abbc3b1f07b8c')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i $srcdir/02_gir_fixes.patch

  sed -i 's/-lpyglib-2\.0-python2\.6/-lpyglib-2.0-python2/' bindings/python/Makefile.am bindings/python/Makefile.in
  sed -i 's/-lpython2\.6/-lpython2.7/' bindings/python/Makefile.am bindings/python/Makefile.in
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' examples/*

  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/libindicate \
              --disable-static \
              PYTHON=python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

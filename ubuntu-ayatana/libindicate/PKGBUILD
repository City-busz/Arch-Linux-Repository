# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=libindicate
pkgver=0.5.91
pkgrel=2
pkgdesc="A small library for applications to raise 'flags' on DBus for other components of the desktop to pick up and visualize"
arch=('i686' 'x86_64')
url="https://launchpad.net/libindicate"
license=('LGPL')
depends=('libdbusmenu>=0.3.97' 'dbus-glib>=0.76')
makedepends=('gtk3>=3.0' 'gobject-introspection>=0.6.7' 'vala' 'gtk-sharp-2>=2.12.1' 'pygtk>=2.14.0' 'gnome-doc-utils>=0.3.2')
options=(!libtool)
source=(http://launchpad.net/$pkgname/0.6/$pkgver/+download/$pkgname-$pkgver.tar.gz)
md5sums=('81a2b1b5df3767938795c44b270fe167')

build() {
  cd "$srcdir/$pkgbase-$pkgver"
  sed -i 's/-lpyglib-2\.0-python$PYTHON_VERSION/-lpyglib-2.0-python2/' configure{,.ac}

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static \
              PYTHON=python2
  make
}

package() {

  cd "$srcdir/$pkgbase-$pkgver"

  make -C libindicate DESTDIR="$pkgdir/" install
  make -C libindicate-gtk DESTDIR="$pkgdir/" install-libindicate_gtk3includeHEADERS
  make -C bindings/mono/indicate DESTDIR="$pkgdir/" install
  make -C examples DESTDIR="$pkgdir/" install
  make -C tests DESTDIR="$pkgdir/" install
  make -C docs DESTDIR="$pkgdir/" install
}

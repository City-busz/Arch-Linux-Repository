# Maintainer: György Balló <ballogy@freestart.hu>
pkgbase=libindicate
pkgname=(libindicate libindicate-gtk2 libindicate-gtk3)
pkgver=0.5.91
pkgrel=3
pkgdesc="A small library for applications to raise 'flags' on DBus for other components of the desktop to pick up and visualize"
arch=('i686' 'x86_64')
url="https://launchpad.net/libindicate"
license=('LGPL')
makedepends=('libdbusmenu>=0.3.97' 'dbus-glib>=0.76' 'gtk2>=2.12' 'gtk3>=3.0' 'gobject-introspection>=0.6.7' 'vala' 'gtk-sharp-2>=2.12.1' 'pygtk>=2.14.0' 'gnome-doc-utils>=0.3.2')
options=(!libtool)
source=(http://launchpad.net/$pkgbase/0.6/$pkgver/+download/$pkgbase-$pkgver.tar.gz)
md5sums=('81a2b1b5df3767938795c44b270fe167')

build() {
  cd "$srcdir/$pkgbase-$pkgver"
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' examples/{im-client,listen-and-print}.py
  sed -i 's/-lpyglib-2\.0-python$PYTHON_VERSION/-lpyglib-2.0-python2/' configure{,.ac}

  # Modify headers location to avoid conflict between gtk2 and gtk3 packages
  sed -i 's|libindicate_gtkincludedir=$(includedir)/libindicate-0.6/libindicate-gtk|libindicate_gtkincludedir=$(includedir)/libindicate$(VER)-0.6/libindicate-gtk|' libindicate-gtk/Makefile.am
  sed -i 's|libindicate_gtkincludedir = $(includedir)/libindicate-0.6/libindicate-gtk|libindicate_gtkincludedir = $(includedir)/libindicate$(VER)-0.6/libindicate-gtk|' libindicate-gtk/Makefile.in
  sed -i 's|Cflags: -I${includedir}/libindicate-0.6|Cflags: -I${includedir}/libindicate3-0.6|' libindicate-gtk/indicate-gtk3-0.6.pc.in

  # Fix build of libindicate-gtk without libindicate installed
  sed -i 's|$(LIBINDICATEGTK_CFLAGS)|$(LIBINDICATEGTK_CFLAGS) -L../libindicate/.libs|' libindicate-gtk/Makefile.{am,in}

  [[ -d build-gtk2 ]] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static --with-gtk=2 \
              PYTHON=python2
  make
  popd

  [[ -d build-gtk3 ]] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static \
              PYTHON=python2
  make
  popd
}

package_libindicate() {
  depends=('libdbusmenu>=0.3.97' 'dbus-glib>=0.76')

  cd "$srcdir/$pkgbase-$pkgver/build-gtk2"

  make -C libindicate DESTDIR="$pkgdir/" install
  make -C bindings/mono/indicate DESTDIR="$pkgdir/" install
  make -C tests DESTDIR="$pkgdir/" install
  make -C docs DESTDIR="$pkgdir/" install
}

package_libindicate-gtk2() {
  pkgdesc+=" (GTK+ 2 library)"
  depends=("libindicate=$pkgver" 'gtk2>=2.12')

  cd "$srcdir/$pkgbase-$pkgver/build-gtk2"

  make -C libindicate-gtk DESTDIR="$pkgdir/" install
  make -C bindings/python DESTDIR="$pkgdir/" install
  make -C bindings/mono/indicate-gtk DESTDIR="$pkgdir/" install
  make -C examples DESTDIR="$pkgdir/" install
}

package_libindicate-gtk3(){
  pkgdesc+=" (GTK+ 3 library)"
  depends=("libindicate=$pkgver" 'gtk3>=3.0')

  cd "$srcdir/$pkgbase-$pkgver/build-gtk3"

  make -C libindicate-gtk DESTDIR="$pkgdir/" install
}

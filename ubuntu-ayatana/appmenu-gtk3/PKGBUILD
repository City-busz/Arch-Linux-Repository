# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=appmenu-gtk3
_pkgname=appmenu-gtk
pkgver=0.3.0.135
_pkgver=0.3.0~r135
pkgrel=1
pkgdesc="Adds appmenu support for GTK+ 3 apps"
arch=('i686' 'x86_64')
url="https://launchpad.net/appmenu-gtk"
license=('LGPL')
depends=('gtk3-ubuntu>=3.0' 'libdbusmenu>=0.3.101')
options=(!libtool)
source=(https://launchpad.net/ubuntu/+archive/primary/+files/${_pkgname}_$_pkgver.orig.tar.gz)
md5sums=('3d3cc18370bd12a20363bab558cf268d')

build() {
  cd "$srcdir/$_pkgname-$_pkgver"
  sed -i 's/-Wall -Werror/-Wall/' src/Makefile.{in,am}
  sed -i 's|@moduledir@/menuproxies/libappmenu.so|@moduledir@/libappmenu.so|' 80appmenu.in

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static
  make
}

package() {
  cd "$srcdir/$_pkgname-$_pkgver"

  make DESTDIR="$pkgdir/" install
  install -Dm755 "$pkgdir/etc/X11/Xsession.d/80appmenu-gtk3" "$pkgdir/etc/X11/xinit/xinitrc.d/80appmenu-gtk3"
  rm -r "$pkgdir/etc/X11/Xsession.d"
}

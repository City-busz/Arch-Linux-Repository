# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=nux
pkgver=1.4.0
pkgrel=2
pkgdesc="Graphical user interface toolkit for applications that mixes OpenGL hardware acceleration with high quality visual rendering"
arch=('i686' 'x86_64')
url="https://launchpad.net/nux"
license=('LGPL')
depends=('gdk-pixbuf2' 'glew' 'pango' 'libsigc++' 'libxcomposite')
makedepends=('boost' 'glproto' 'dri2proto')
options=('!libtool')
source=(http://launchpad.net/$pkgname/1.0/$pkgver/+download/$pkgname-$pkgver.tar.gz
        44_43.diff
        45_44.diff)
md5sums=('839b6eac2a01537558c5068de8a62a6e'
         'c815185a3238f4e9e91482cc3292cfc7'
         'a3f854be17c4e492fef2ae13058ef396')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np0 -i "$srcdir/44_43.diff"
  patch -Np0 -i "$srcdir/45_44.diff"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static --disable-tests
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

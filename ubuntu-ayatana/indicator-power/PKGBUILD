# Maintainer: Balló György <ballogy@freestart.hu>
pkgname=indicator-power
pkgver=0.7
pkgrel=2
pkgdesc="An indicator to show the power status of your devices"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-power"
license=('GPL')
depends=('upower>=0.9.5' 'libindicator3>=0.3.90' 'gnome-settings-daemon>=3.0.0')
makedepends=('intltool>=0.41.0')
options=(!libtool)
install=$pkgname.install
source=(http://launchpad.net/$pkgname/trunk/$pkgver/+download/$pkgname-$pkgver.tar.gz)
md5sums=('17ebd65f3a013028bdb20f975e5c54ec')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's/name="show-label"/name="show-time"/' data/org.ubuntu.indicator-power.gschema.xml
  sed -i 's/GSD_REQUIRED_VERSION=3\.1\.4/GSD_REQUIRED_VERSION=3.0.0/' configure{,.ac}

  autoreconf -f
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static --disable-schemas-compile
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

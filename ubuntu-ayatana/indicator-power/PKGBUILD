# Maintainer: Balló György <ballogy@freestart.hu>
pkgname=indicator-power
pkgver=0.10
pkgrel=2
pkgdesc="An indicator to show the power status of your devices"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-power"
license=('GPL')
depends=('libindicator3>=0.3.90' 'gnome-settings-daemon>=3.0.0')
makedepends=('intltool>=0.41.0')
options=(!libtool)
install=$pkgname.install
source=(http://launchpad.net/$pkgname/trunk/$pkgver/+download/$pkgname-$pkgver.tar.gz
        launchpad-export.tar.gz)
md5sums=('b3ef853edfacbd6949435c058901109c'
         'fbb1b7e838ff185f82c89e2532fb8f01')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Install language files
  echo 'am an ar ast az be bg bn bo bs ca ca@valencia cy cs da de el en_AU en_GB eo es et eu fi fil fr fy gd gl he hi hr hu id it ja kk km ko ku lt lv mhr ms nb nl nn oc os pl ps pt pt_BR ro ru sd si sk sl sq sr sv ta te th tr ug uk uz vi zh_CN zh_HK zh_TW' >po/LINGUAS
  rename $pkgname- '' ../po/$pkgname-*.po
  mv -f -t po ../po/*

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static --disable-schemas-compile
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=libreoffice-extension-menubar
_pkgname=lo-menubar
pkgver=0.1.0
pkgrel=1
pkgdesc="A small plugin for LibreOffice to export the menus from the application into Unity's menubar"
arch=(i686 x86_64)
url="https://launchpad.net/lo-menubar"
license=(LGPL)
depends=(libreoffice-common libdbusmenu-gtk2)
makedepends=(libreoffice-sdk python2)
source=(http://launchpad.net/lo-menubar/trunk/$pkgver/+download/$_pkgname-$pkgver.tar.bz2
        01-adapt-paths-for-libreoffice-3.4.patch)
md5sums=(2a06c73562c3bc21e2916ae1125a799f
         02068b409eab2c75c9cf3e667353816f)

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  patch -Np1 -i "$srcdir/01-adapt-paths-for-libreoffice-3.4.patch"

  export LD_LIBRARY_PATH=/usr/lib/libreoffice/ure/lib
  python2 ./waf configure --prefix=/usr \
                          --libreoffice-prefix=/usr/lib/libreoffice \
                          --ure-prefix=/usr/lib/libreoffice/ure
  python2 ./waf build
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  python2 ./waf install --destdir=$pkgdir
}

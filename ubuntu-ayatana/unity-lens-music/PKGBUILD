# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=unity-lens-music
pkgver=0.2.6
pkgrel=2
pkgdesc="Music, in the dash"
arch=('i686' 'x86_64')
url="https://launchpad.net/unity-place-music"
license=('GPL')
depends=('json-glib' 'libunity>=4.0.2' 'banshee')
makedepends=('vala>=0.11.3' 'intltool>=0.40.0')
source=(http://launchpad.net/$pkgname/0.2/$pkgver/+download/$pkgname-$pkgver.tar.gz
        launchpad-export.tar.gz)
md5sums=('7fc46bcef43aaaaf26d5919d70e96a54'
         'd32f36dae53e43619936a59e0825a87b')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Install language files
  echo 'am ar ast az be bg bn bo bs ca ca@valencia cs da de el en_AU en_GB eo es et eu fi fil fr fy gd gl gu he hi hr hu id it ja kk km ko lb lt lv mg mhr ml ms my nb nl nn oc pl ps pt pt_BR ro ru sd si sk sl sq sr sv ta te th ti tr ug uk uz vi zh_CN zh_HK zh_TW' >po/LINGUAS
  rename $pkgname- '' ../po/$pkgname-*.po
  mv -f -t po ../po/*

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

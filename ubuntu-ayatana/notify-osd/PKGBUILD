# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=notify-osd
pkgver=0.9.29
pkgrel=1
pkgdesc="Canonical's on-screen-display notification agent, implementing the freedesktop.org Desktop Notifications Specification with semi-transparent click-through bubbles"
arch=('i686' 'x86_64')
url="https://launchpad.net/notify-osd"
license=('GPL')
groups=('ayatana')
depends=('gconf' 'libwnck' 'libnotify>=0.4.5')
provides=('notification-daemon')
conflicts=('notification-daemon')
source=(http://launchpad.net/$pkgname/lucid/ubuntu-10.04-beta-2/+download/$pkgname-$pkgver.tar.gz
        3sec.patch dynamic.patch fix_dropshadow.patch)
md5sums=('13df20eb3c6d06c17eef07400e1650d8'
         '6e9b159bf29219d08c7c73cb7f5e93db'
         '5d866067d9416cfe083c4e86a90e9f2e'
         '17d44d698322d66c86e3df95b9f0fcec')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i $srcdir/3sec.patch
  patch -Np1 -i $srcdir/dynamic.patch
  patch -Np0 -i $srcdir/fix_dropshadow.patch

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib \
              --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

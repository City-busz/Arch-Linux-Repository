# Maintainer: Balló György <ballogy@freestart.hu>
_pkgname=indicator-sound
pkgname=indicator-sound-gtk2
pkgver=0.8.0.0
pkgrel=1
pkgdesc="A unified sound menu (GTK+ 2 library for Xfce/LXDE)"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-sound"
license=('GPL')
depends=('indicator-sound' 'libindicator>=0.3.19' 'libdbusmenu-gtk2>=0.3.101' 'ido>=0.1.11')
makedepends=('intltool>=0.35.0' 'vala>=0.11.2')
options=(!libtool)
source=(http://launchpad.net/$_pkgname/fifth/$pkgver/+download/$_pkgname-$pkgver.tar.gz)
md5sums=('32ba67340776153e376fc8ee06cda03f')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$_pkgname \
              --disable-static --disable-schemas-compile --with-gtk=2
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  make -C src DESTDIR="$pkgdir/" install-soundmenulibLTLIBRARIES
}

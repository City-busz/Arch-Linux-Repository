# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=overlay-scrollbar3
_pkgname=overlay-scrollbar
pkgver=0.2.6
pkgrel=1
pkgdesc="Use an overlay to ensure that scrollbars take up no active screen real-estate"
arch=('i686' 'x86_64')
url="http://launchpad.net/ayatana-scrollbar/"
license=('LGPL')
depends=('gtk3-ubuntu>=3.0.9')
options=(!libtool)
source=(http://launchpad.net/ayatana-scrollbar/${pkgver%.*}/$pkgver/+download/$_pkgname-$pkgver.tar.gz)
md5sums=('11e9bc6647ac3c269efa771f86b84319')

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  # Fix headers location
  sed -i 's|/include/overlay-scrollbar-@OS_MAJORMINOR@|/include/overlay-scrollbar3-@OS_MAJORMINOR@|' pkgconfig/overlay-scrollbar3.pc.in

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
  install -Dm755 "$pkgdir/etc/X11/Xsession.d/81overlay-scrollbar" "$pkgdir/etc/X11/xinit/xinitrc.d/81overlay-scrollbar"
  rm -r "$pkgdir/etc/X11/Xsession.d"
}

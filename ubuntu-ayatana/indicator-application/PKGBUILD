# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=indicator-application
pkgver=0.2.9
pkgrel=1
pkgdesc="A library and indicator to take menus from applications and place them in the panel."
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-application"
license=('GPL')
groups=('ayatana')
depends=('libindicator>=0.3.5' 'libdbusmenu>=0.2.2')
makedepends=('vala' 'gtk-sharp-2>=2.12.1' 'pygtk>=2.14.0')
options=('!libtool')
source=(http://launchpad.net/$pkgname/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.gz)
md5sums=('e63c8f0570d9debadea0f8d86c33c258')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's/-lpyglib-2\.0-python2\.6/-lpyglib-2\.0-python2/' bindings/python/Makefile.am bindings/python/Makefile.in
  sed -i 's/nunit >= 2\.4\.7/mono-nunit >= 2\.4\.7/' configure configure.ac

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib \
              --disable-static --disable-introspection \
              PYTHON=python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

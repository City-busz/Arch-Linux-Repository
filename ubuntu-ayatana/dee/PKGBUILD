# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=dee
pkgver=0.5.20
pkgrel=1
pkgdesc="A library that uses DBus to provide objects allowing you to create Model-View-Controller type programs across DBus"
arch=('i686' 'x86_64')
url="https://launchpad.net/dee"
license=('LGPL')
depends=('glib2>=2.26')
makedepends=('python2' 'gobject-introspection>=0.6.3' 'vala')
options=(!libtool)
source=(http://launchpad.net/$pkgname/trunk/$pkgver/+download/$pkgname-$pkgver.tar.gz)
md5sums=('773e62a951be3b35eefe2d4d1c9e6bae')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static \
              PYTHON=python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

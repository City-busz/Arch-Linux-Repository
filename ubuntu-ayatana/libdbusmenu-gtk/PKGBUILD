# Maintainer: György Balló <ballogy@freestart.hu>
pkgbase=libdbusmenu-gtk
_pkgbase=libdbusmenu
pkgname=(libdbusmenu-gtk2 libdbusmenu-gtk3)
pkgver=0.4.93
pkgrel=3
arch=('i686' 'x86_64')
url="https://launchpad.net/dbusmenu"
license=('LGPL')
makedepends=("libdbusmenu=$pkgver" 'gtk2-ubuntu>=2.16' 'gtk3>=2.91' 'intltool>=0.35.0' 'gnome-doc-utils>=0.3.2' 'gobject-introspection>=0.10.0' 'vala' 'python2')
options=(!libtool)
source=(http://launchpad.net/dbusmenu/0.5/$pkgver/+download/$_pkgbase-$pkgver.tar.gz)
md5sums=('1d687ac79fcec4070dbe9a8154b3c275')

build() {
  cd "$srcdir/$_pkgbase-$pkgver"
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' tools/dbusmenu-bench
  sed -i 's/JSON_GLIB_REQUIRED_VERSION=0.13.4/JSON_GLIB_REQUIRED_VERSION=0.12.6/' configure{,.ac}

  [[ -d build-gtk2 ]] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static --disable-scrollkeeper --with-gtk=2
  make
  popd

  [[ -d build-gtk3 ]] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static --disable-scrollkeeper
  make
  popd
}

package_libdbusmenu-gtk2() {
  pkgdesc="A small little library that was created by pulling out some comon code out of indicator-applet (GTK+ 2 library)"
  depends=('gtk2>=2.16' "libdbusmenu=$pkgver")

  cd "$srcdir/$_pkgbase-$pkgver/build-gtk2"

  make -C libdbusmenu-gtk DESTDIR="$pkgdir/" install

  rm -r "$pkgdir/usr/include"
}

package_libdbusmenu-gtk3(){
  pkgdesc="A small little library that was created by pulling out some comon code out of indicator-applet (GTK+ 3 library)"
  depends=('gtk3>=2.91' "libdbusmenu=$pkgver")

  cd "$srcdir/$_pkgbase-$pkgver/build-gtk3"

  make -C libdbusmenu-gtk DESTDIR="$pkgdir/" install

  rm -r "$pkgdir/usr/include"
}

# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=lxpanel-indicator-applet-plugin
_pkgname=lxpanel
pkgver=0.5.8
pkgrel=1
pkgdesc="Indicator applet plugin for LXPanel"
arch=('i686' 'x86_64')
license=('GPL2')
url="https://launchpad.net/lxpanel"
depends=('lxpanel' 'libindicator')
optdepends=('indicator-application-gtk2: take menus from applications and place them in the panel'
            'indicator-messages-gtk2: a place on the users desktop that collects messages that need a response'
            'indicator-sound-gtk2: a unified sound menu')
options=(!libtool)
source=(http://downloads.sourceforge.net/lxde/$_pkgname-$pkgver.tar.gz
        indicator-support.patch
        99_refresh_autotools.patch)
md5sums=('129fae75d1cd3983fd94542c573a70a1'
         'a76d097d2da713234c2e06f1b1c48c58'
         '274eff66dec1fd5e28267665e56cd7ee')

build() {
	cd "$srcdir/$_pkgname-$pkgver"
	patch -Np1 -i "$srcdir/indicator-support.patch"
	patch -Np1 -i "$srcdir/99_refresh_autotools.patch"

	./configure --sysconfdir=/etc --prefix=/usr --enable-indicator-support
	make -C src/plugins/indicator
}

package() {
	cd "$srcdir/$_pkgname-$pkgver"
	make -C src/plugins/indicator DESTDIR="$pkgdir" install
}

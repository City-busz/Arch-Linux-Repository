# Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gtk3-ubuntu
pkgver=3.2.1
pkgrel=1
pkgdesc="GTK+ is a multi-platform toolkit (v3) with Ubuntu's modifications"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
install=gtk3.install
depends=('atk' 'cairo' 'gtk-update-icon-cache' 'libcups' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage' 'pango' 'shared-mime-info' 'colord')
makedepends=('gobject-introspection')
provides=("gtk3=$pkgver")
conflicts=('gtk3')
options=('!libtool' '!docs')
backup=(etc/gtk-3.0/settings.ini)
license=('LGPL')
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/3.2/gtk+-${pkgver}.tar.xz
        settings.ini
        043_ubuntu_menu_proxy.patch
        062_ubuntu-set-grab-add.patch
        071_no_offscreen_widgets_grabbing.patch
        075_expose_gdkwidget_for_gtkrange.patch
        100_overlay_scrollbar_loading.patch
        111_no_offscreen_device_grabs.patch)
sha256sums=('f1989f183700cd5f46681cfabc2253e2f526b19b56e4b631dcee2594dddb0ef3'
            'c214d3dcdcadda3d642112287524ab3e526ad592b70895c9f3e3733c23701621'
            '522fb01728ff6aab54b39fe1f66451c58baed0ba513f298c7086356864c9f33b'
            '5a89bd63afb2d97b98eb3b7acb5a568c388aedd94af38a1106233ce033982354'
            'f3c8e907e85195ea868065d66fd3a37390fd124e67a58a533ad063fd9eb84e88'
            '9ee240b355502d6ba449983f794205e6e5401c8acca2a3ce513df7f0502dc988'
            'd6ba760175a01bb2339bc6878d7fbded3a4ddb5ade8c69ad7a1704dff5993895'
            'a37fe378919a5c243a46f1262f3bd62856092faeef12fd613ba58a7c2f72b05d')

build() {
    cd "${srcdir}/gtk+-${pkgver}"

    # Add Ubuntu menuproxy module (required for appmenu-gtk)
    patch -Np1 -i "${srcdir}/043_ubuntu_menu_proxy.patch"

    # Add ubuntu-set-grab-add signal (required for ido)
    patch -Np1 -i "${srcdir}/062_ubuntu-set-grab-add.patch"

    # Don't let offscreen widget do grabbing (fix for ido)
    patch -Np1 -i "${srcdir}/071_no_offscreen_widgets_grabbing.patch"

    # Add gtk_range_get_event_window to get the event_window for a GtkRange (fix for ido)
    patch -Np1 -i "${srcdir}/075_expose_gdkwidget_for_gtkrange.patch"

    # Add support for the ayatana overlay scrollbars (required for overlay-scrollbar)
    patch -Np1 -i "${srcdir}/100_overlay_scrollbar_loading.patch"

    # Do not allow widgets in an offscreen hierarchy to take grabs (fix for ido)
    patch -Np1 -i "${srcdir}/111_no_offscreen_device_grabs.patch"

    automake
    CXX=/bin/false ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --enable-gtk2-dependency \
        --disable-schemas-compile
    #https://bugzilla.gnome.org/show_bug.cgi?id=655517
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
    make
}

package() {
    cd "${srcdir}/gtk+-${pkgver}"
    make DESTDIR="${pkgdir}" install

    install -Dm644 "${srcdir}/settings.ini" "${pkgdir}/etc/gtk-3.0/settings.ini"
}

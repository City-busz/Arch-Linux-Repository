# Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gtk3-ubuntu
pkgver=3.2.0
pkgrel=2
pkgdesc="The GTK+ Toolkit (v3) with Ubuntu's modifications"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
install=gtk3.install
depends=('atk' 'cairo' 'gtk-update-icon-cache' 'libcups' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage' 'pango' 'shared-mime-info' 'colord')
makedepends=('gobject-introspection')
provides=("gtk3=$pkgver")
conflicts=('gtk3')
options=('!libtool' '!docs')
backup=(etc/gtk-3.0/settings.ini)
license=('LGPL')
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/3.2/gtk+-${pkgver}.tar.xz
        settings.ini
        a11y.patch::http://git.gnome.org/browse/gtk+/patch/?id=e248c6812e8e33150d61074471ef0330668aed45
        043_ubuntu_menu_proxy.patch
        062_ubuntu-set-grab-add.patch
        063_resize_grip_default.patch
        072_indicator_menu_update.patch
        075_expose_gdkwidget_for_gtkrange.patch
        100_overlay_scrollbar_loading.patch
        111_no_offscreen_device_grabs.patch)
sha256sums=('bce3c1a9be6afd7552c795268656d8fdd09c299765a7faaf5a76498bb82ed44c'
            'c214d3dcdcadda3d642112287524ab3e526ad592b70895c9f3e3733c23701621'
            '0ae5f9c3553f9fc6c515343de96046c17544654936b17c09330443ed44778cb2'
            'c929573703d1986b17e9719ad367ea0383c431f6c7e92920330b385fb24bd10e'
            '5a89bd63afb2d97b98eb3b7acb5a568c388aedd94af38a1106233ce033982354'
            '306cd286c01a07fbf726b75ca11fd627ddfa6838e1064a956aa9ead0074d3563'
            'b2f445b52c8f68273d0b2c255f16263d582aa9010724d23c8a4cea1667adeeec'
            '9ee240b355502d6ba449983f794205e6e5401c8acca2a3ce513df7f0502dc988'
            'd6ba760175a01bb2339bc6878d7fbded3a4ddb5ade8c69ad7a1704dff5993895'
            'a37fe378919a5c243a46f1262f3bd62856092faeef12fd613ba58a7c2f72b05d')

build() {
    cd "${srcdir}/gtk+-${pkgver}"
    patch -Np1 -i "${srcdir}/a11y.patch"
    patch -Np1 -i "${srcdir}/043_ubuntu_menu_proxy.patch"
    patch -Np1 -i "${srcdir}/062_ubuntu-set-grab-add.patch"
    patch -Np1 -i "${srcdir}/063_resize_grip_default.patch"
    patch -Np1 -i "${srcdir}/072_indicator_menu_update.patch"
    patch -Np1 -i "${srcdir}/075_expose_gdkwidget_for_gtkrange.patch"
    patch -Np1 -i "${srcdir}/100_overlay_scrollbar_loading.patch"
    patch -Np1 -i "${srcdir}/111_no_offscreen_device_grabs.patch"

    automake
    CXX=/bin/false ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --enable-gtk2-dependency \
        --disable-schemas-compile
    #https://bugzilla.gnome.org/show_bug.cgi?id=655517
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
    make
}

package() {
    cd "${srcdir}/gtk+-${pkgver}"
    make DESTDIR="${pkgdir}" install

    install -Dm644 "${srcdir}/settings.ini" "${pkgdir}/etc/gtk-3.0/settings.ini"
}

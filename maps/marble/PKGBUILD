# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=marble
_pkgbase=kdeedu
pkgver=1.0.0
_pkgver=4.6.0
pkgrel=1
pkgdesc="Desktop Globe"
arch=('i686' 'x86_64')
url="http://edu.kde.org/marble/"
license=('LGPL')
depends=('qt>=4.6' 'gpsd' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('cmake>=2.6.4')
conflicts=('kdeedu-marble')
provides=('kdeedu-marble')
install=$pkgname.install
source=("ftp://ftp.kde.org/pub/kde/stable/$_pkgver/src/$_pkgbase-$_pkgver.tar.bz2")
sha1sums=('790acbf29ad76d25c1156ce31c28b4fbd8baaa7e')

build() {
        cd $srcdir/$_pkgbase-$_pkgver
        mkdir $pkgname-build
        cd $pkgname-build
        cmake ../$pkgname \
                -DCMAKE_BUILD_TYPE=Release \
                -DCMAKE_INSTALL_PREFIX=/usr \
                -DQTONLY=ON
        make
}

package() {
	cd "$srcdir/$_pkgbase-$_pkgver/$pkgname-build"
	make DESTDIR=$pkgdir install
	install -D -m644 "$srcdir/$_pkgbase-$_pkgver/cmake/modules/FindMarbleWidget.cmake" \
		"$pkgdir/usr/share/apps/cmake/modules/FindMarbleWidget.cmake"
}

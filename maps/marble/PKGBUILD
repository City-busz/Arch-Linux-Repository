# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=marble
pkgver=1.1.0
pkgrel=1
pkgdesc="Virtual Globe and World Atlas that you can use to learn more about Earth"
arch=('i686' 'x86_64')
url="http://edu.kde.org/marble/"
license=('LGPL')
depends=('qt>=4.6' 'gpsd>=2.96' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('cmake>=2.6.4')
conflicts=('kdeedu-marble')
install=$pkgname.install
source=("http://files.kde.org/marble/downloads/sources/$pkgname-$pkgver-source.tar.gz"
        gpsd-2.96.patch)
sha1sums=('a362bb734720b7ca1264b7f733d76e7c3095277b'
          'fd6b6b81945d951bb6ad0bb735df33c11bb2008e')

build() {
        cd $srcdir
        patch -Np2 -i gpsd-2.96.patch

        mkdir build
        cd build
        cmake .. \
                -DCMAKE_BUILD_TYPE=Release \
                -DCMAKE_INSTALL_PREFIX=/usr \
                -DQTONLY=ON
        make
}

package() {
	cd "$srcdir/build"
	make DESTDIR=$pkgdir install
}

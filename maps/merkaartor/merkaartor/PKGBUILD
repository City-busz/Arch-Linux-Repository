# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=merkaartor
pkgver=0.17.2
pkgrel=2
pkgdesc="Map editor for OpenStreetMap.org"
arch=('i686' 'x86_64')
url="http://merkaartor.be/"
license=('GPL')
depends=('qt' 'exiv2' 'gdal' 'libxslt' 'zbar' 'libproxy' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('boost')
optdepends=('inkscape: generating PNGs from the rendered SVGs')
install=$pkgname.install
source=(http://merkaartor.be/attachments/download/253/$pkgname-$pkgver.tar.bz2)
md5sums=('44c49f73efa589a455bb9d3d0a284d09')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's|libproxy/proxy.h|proxy.h|' src/Preferences/MerkaartorPreferences.h

  lrelease translations/*.ts
  qmake Merkaartor.pro NODEBUG=1 \
                       RELEASE=1 \
                       PREFIX=/usr \
                       GEOIMAGE=1 \
                       NVIDIA_HACK=1 \
                       PROJ=1 \
                       GDAL=1 \
                       USE_BUILTIN_BOOST=0 \
                       GPSDLIB=0 \
                       ZBAR=1 \
                       LIBPROXY=1
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make INSTALL_ROOT="$pkgdir" install
}

# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=gosmore-svn
pkgver=24484
pkgrel=1
pkgdesc="Openstreetmap.org viewer with searching and routing capabilities"
arch=('i686' 'x86_64')
url="http://wiki.openstreetmap.org/wiki/Gosmore"
license=('public domain')
depends=('gtk2' 'curl')
optdepends=('gpsd: connect to GPS receiver'
            'unzip: unzip downloaded maps'
            'libgnome: open external links')
makedepends=('subversion')
provides=('gosmore')
conflicts=('gosmore')

_svntrunk=http://svn.openstreetmap.org/applications/rendering/gosmore/
_svnmod=gosmore

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  #
  # BUILD
  #

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make prefix=/usr
}

package() {
  cd "$srcdir/$_svnmod-build"

  make DESTDIR="$pkgdir/" prefix=/usr install
}

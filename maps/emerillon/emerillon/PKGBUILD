# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=emerillon
pkgver=0.1.2
pkgrel=1
pkgdesc="Map viewer for OpenStreetMap."
arch=('i686' 'x86_64')
url="http://www.novopia.com/emerillon/"
license=('GPL')
depends=('libchamplain>=0.4' 'geoclue>=0.11.1' 'ethos>=0.2' 'librest>=0.6')
makedepends=('vala' 'intltool>=0.40.0' 'gtk-doc>=1.14' 'gnome-common')
options=('!libtool')
install=$pkgname.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2
        emerillon-hu.po)
sha256sums=('91cbf31247a831f4d64ecf7a26d13f59ed47e95ea18a1ee345acc60158aacabe'
            'a7afeef1322da8d49c9a2dca2d26723f8a0ce5e7384ba87c165871cc854126cb')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  mv -f $srcdir/emerillon-hu.po po/hu.po
  sed -i '$ a hu' po/LINGUAS
  find . -type f | xargs sed -i 's/champlain-0.6/champlain-0.8/'
  find . -type f | xargs sed -i 's/champlain-gtk-0.6/champlain-gtk-0.8/'
  find . -type f | xargs sed -i 's/rest-0.6/rest-0.7/'

  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-introspection --disable-static --disable-scrollkeeper --disable-schemas-install \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

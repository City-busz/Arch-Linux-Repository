# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=ethos
pkgver=0.2.2
pkgrel=3
pkgdesc="Plugin framework that is written in C using the GLib and GObject libraries."
arch=('i686' 'x86_64')
url="http://git.dronelabs.com/ethos/about/"
license=('LGPL')
depends=('pygtk>=2.12.0')
makedepends=('intltool>=0.40.0' 'vala>=0.11.0' 'gobject-introspection')
options=(!libtool)
source=(http://ftp.dronelabs.com/sources/ethos/0.2/$pkgname-$pkgver.tar.gz)
md5sums=('36cf1ef444a224556bba4d441c400300')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's/vala-1.0/libvala-0.12/' configure configure.ac

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --enable-introspection \
              PYTHON=python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

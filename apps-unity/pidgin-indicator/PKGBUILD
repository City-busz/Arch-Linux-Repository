# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=pidgin-indicator
_pkgname=pidgin-libnotify
pkgver=0.14
pkgrel=3
pkgdesc="Pidgin plugin that enables popups when someone logs in or messages you"
arch=(i686 x86_64)
url="https://launchpad.net/ubuntu/+source/pidgin-libnotify"
license=(GPL)
depends=(libnotify libindicate-gtk2 pidgin)
makedepends=(intltool)
options=(!libtool)
provides=("pidgin-libnotify=$pkgver")
conflicts=(pidgin-libnotify)
source=(http://downloads.sourceforge.net/sourceforge/gaim-libnotify/$_pkgname-$pkgver.tar.gz
        indicate.patch
        force-load.patch
        best_name.patch
        libnotify_0_7.patch
        launchpad-export.tar.gz)
md5sums=(bfb5368b69c02d429b2b17c00a6673c0
         2e84c892834fd6178cecf05b942c81f1
         642c0ee124b06f1f3614745010715a37
         f3be577381e7ddcc8ea2a2a00032918a
         d38396566fc8debd00dff922d3348f56
         ce1356bc2d619e0d8407657be82b52f3)

build() {
  cd $srcdir/$_pkgname-$pkgver
  patch -Np1 -i "$srcdir/indicate.patch"
  patch -Np1 -i "$srcdir/force-load.patch"
  patch -Np1 -i "$srcdir/best_name.patch"
  patch -Np1 -i "$srcdir/libnotify_0_7.patch"

  rename $_pkgname- '' ../po/$_pkgname-*.po
  mv -f -t po ../po/*

  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  mkdir -p "$pkgdir/usr/share/indicators/messages/applications"
  echo '/usr/share/applications/pidgin.desktop' > "$pkgdir/usr/share/indicators/messages/applications/pidgin"
}

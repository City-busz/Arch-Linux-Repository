# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=indicator-weather
pkgver=11.05.31
pkgrel=1
pkgdesc="A fully featured weather notification applet for use on the GNOME panel"
arch=(any)
url="https://launchpad.net/weather-indicator"
license=(GPL)
depends=(libappindicator dbus-python python2-gconf python-notify python-pywapi dconf)
makedepends=(python-distutils-extra)
install=$pkgname.install
source=(http://launchpad.net/weather-indicator/2.0/$pkgver/+download/${pkgname}_$pkgver.tar.gz)
md5sums=(0248df51d121022067d4b0c810b0ff2c)

build() {
  cd "$srcdir/${pkgname}_$pkgver"

  python2 setup.py build
}

package() {
  cd "$srcdir/${pkgname}_$pkgver"

  python2 setup.py install --root=$pkgdir/ --optimize=1

  # Remove apport config
  rm -R "$pkgdir/etc"

  # Move GSchema and autostart file to it's correct place
  install -dm755 "$pkgdir"/{etc/xdg/autostart,usr/share/glib-2.0/schemas}
  mv "$pkgdir"/{usr/share/indicator-weather,etc/xdg/autostart}/indicator-weather.desktop
  mv "$pkgdir"/usr/share/{indicator-weather,glib-2.0/schemas}/indicator-weather.gschema.xml
}

# Maintainer: György Balló <ballogy@freestart.hu>

pkgname=python-keyring
_pkgname=keyring
pkgver=0.6.2
pkgrel=1
pkgdesc="Store and access your passwords safely"
arch=(any)
url="http://pypi.python.org/pypi/keyring"
license=(PSF)
depends=(python2)
optdepends=('pycrypto: CryptedFileKeyring backend'
            'python-gnomekeyring: GnomeKeyring backend'
            'kdebindings-python: KDEKWallet backend')
source=(http://pypi.python.org/packages/source/k/$_pkgname/$_pkgname-$pkgver.tar.gz)
md5sums=(b21b5783c375fc09abdc060b4edc062f)

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' keyring/cli.py

  python2 setup.py build
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  python2 setup.py install --root=$pkgdir/ --optimize=1
}

# Maintainer: D. Can Celasun <dcelasun[at]gmail[dot]com>

pkgname=feedindicator
pkgver=1.03
_pkgver=20110603
pkgrel=1
pkgdesc="RSS feed updates indicator"
arch=(any)
url="http://code.google.com/p/feedindicator/"
license=(GPL3)
depends=(libappindicator python2-feedparser python2-gconf python-notify hicolor-icon-theme xdg-utils)
install=$pkgname.install
source=(http://feedindicator.googlecode.com/files/$pkgname-$_pkgver.tar.gz)
sha1sums=(f4266705a5179c8ab7a606f2b5c2ebb6c039f7d5)

build() {
  # Fix the python shebang
  sed -i 's|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|' feedindicator
}

package() {
  # Install icons
  install -Dm644 dark/indicator-feedindicator.png "$pkgdir/usr/share/icons/ubuntu-mono-dark/apps/22/indicator-feedindicator.png"
  install -Dm644 dark/indicator-feedindicator-attention.png "$pkgdir/usr/share/icons/ubuntu-mono-dark/apps/22/indicator-feedindicator-attention.png"
  install -Dm644 light/indicator-feedindicator.png "$pkgdir/usr/share/icons/ubuntu-mono-light/apps/22/indicator-feedindicator.png"
  install -Dm644 light/indicator-feedindicator-attention.png "$pkgdir/usr/share/icons/ubuntu-mono-light/apps/22/indicator-feedindicator-attention.png"
  install -Dm644 hicolor/indicator-feedindicator.png "$pkgdir/usr/share/icons/hicolor/22x22/apps/indicator-feedindicator.png"
  install -Dm644 hicolor/indicator-feedindicator-attention.png "$pkgdir/usr/share/icons/hicolor/22x22/apps/indicator-feedindicator-attention.png"
  install -Dm644 feedindicator-logo.png "$pkgdir/usr/share/icons/hicolor/128x128/apps/feedindicator.png"
  install -Dm644 feedindicator-48x48.png "$pkgdir/usr/share/icons/hicolor/48x48/apps/feedindicator.png"

  # Install desktop file
  install -Dm644 feedindicator.desktop "$pkgdir/usr/share/applications/feedindicator.desktop"

  # Install logo and icon
  install -Dm644 feedindicator-icon.png "$pkgdir/usr/share/feedindicator/feedindicator-icon.png"
  install -Dm644 feedindicator-logo.png "$pkgdir/usr/share/feedindicator/feedindicator-logo.png"

  # Install the program
  install -Dm755 feedindicator "$pkgdir/usr/bin/feedindicator"
}


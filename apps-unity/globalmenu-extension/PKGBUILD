# Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: Tom Kuther <gimpel@sonnenkinder.org>

pkgbase=globalmenu-extension
pkgname=(globalmenu-extension-firefox globalmenu-extension-thunderbird)
pkgver=264
pkgrel=1
arch=('i686' 'x86_64')
url="https://launchpad.net/globalmenu-extension"
license=('GPL' 'LGPL' 'MPL')
makedepends=('bzr' 'autoconf2.13' 'yasm' 'xulrunner' 'zip' 'unzip')

_bzrtrunk="lp:globalmenu-extension/2.0"
_bzrmod="globalmenu-extension"

build() {
  cd "$srcdir"
  msg "Connecting to Bazaar server...."

  if [ -d $_bzrmod ] ; then
    cd ${_bzrmod} && bzr pull ${_bzrtrunk} -r ${pkgver}
    msg "The local files are updated."
  else
    bzr branch ${_bzrtrunk} ${_bzrmod} -r ${pkgver}
  fi

  msg "Bazaar checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_bzrmod-build"
  cp -r "$srcdir/$_bzrmod" "$srcdir/$_bzrmod-build"
  cd "$srcdir/$_bzrmod-build"

  #
  # BUILD HERE
  #

  autoconf-2.13
  ./configure --enable-application=extensions \
              --enable-extensions=globalmenu \
              --with-libxul-sdk=`pkg-config --variable=sdkdir libxul` \
              --with-system-libxul \
              --with-system-nspr \
              --with-system-nss \
              --disable-tests
  make
}

package_globalmenu-extension-firefox() {
  pkgdesc="Firefox extension that exports the standard menu bar via libdbusmenu"
  depends=('libdbusmenu-gtk2' 'firefox')

  cd "$srcdir/$pkgbase-build"
  emid=$(sed -n '/.*<em:id>\(.*\)<\/em:id>.*/{s//\1/p;q}' extensions/globalmenu/install.rdf)
  install -d "$pkgdir/usr/lib/firefox/extensions/$emid"
  unzip -d "$pkgdir/usr/lib/firefox/extensions/$emid" dist/xpi-stage/globalmenu.xpi
}

package_globalmenu-extension-thunderbird() {
  pkgdesc="Thunderbird extension that exports the standard menu bar via libdbusmenu"
  depends=('libdbusmenu-gtk2' 'thunderbird')

  cd "$srcdir/$pkgbase-build"
  emid=$(sed -n '/.*<em:id>\(.*\)<\/em:id>.*/{s//\1/p;q}' extensions/globalmenu/install.rdf)
  install -d "$pkgdir/usr/lib/thunderbird/extensions/$emid"
  unzip -d "$pkgdir/usr/lib/thunderbird/extensions/$emid" dist/xpi-stage/globalmenu.xpi
}

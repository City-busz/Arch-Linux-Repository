# $Id: PKGBUILD 77532 2010-04-15 13:26:42Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgbase=enchant
pkgname=('enchant' 'enchant-aspell' 'enchant-hspell' 'enchant-ispell' 'enchant-voikko')
pkgver=1.6.0
pkgrel=1
pkgdesc="A wrapper library for generic spell checking"
arch=('i686' 'x86_64')
url="http://www.abisource.com/enchant/"
license=('LGPL')
makedepends=('dbus-glib' 'hunspell' 'aspell' 'hspell' 'ispell' 'libvoikko')
options=('!libtool')
source=("http://www.abisource.com/downloads/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
md5sums=('de11011aff801dc61042828041fb59c7')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
    --disable-static \
    --with-myspell-dir=/usr/share/myspell
  make
}

package_enchant() {
  depends=('dbus-glib' 'hunspell')

  cd "${srcdir}/${pkgbase}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  rm $pkgdir/usr/lib/enchant/libenchant_aspell.so
  rm $pkgdir/usr/lib/enchant/libenchant_hspell.so
  rm $pkgdir/usr/lib/enchant/libenchant_ispell.so
  rm $pkgdir/usr/lib/enchant/libenchant_voikko.so
}

package_enchant-aspell() {
  depends=('enchant' 'aspell')

  cd "${srcdir}/${pkgbase}-${pkgver}/src/aspell"

  make DESTDIR="${pkgdir}" install
}

package_enchant-hspell() {
  depends=('enchant' 'hspell')

  cd "${srcdir}/${pkgbase}-${pkgver}/src/hspell"

  make DESTDIR="${pkgdir}" install
}

package_enchant-ispell() {
  depends=('enchant' 'ispell')

  cd "${srcdir}/${pkgbase}-${pkgver}/src/ispell"

  make DESTDIR="${pkgdir}" install
}

package_enchant-voikko() {
  depends=('enchant' 'libvoikko')

  cd "${srcdir}/${pkgbase}-${pkgver}/src/voikko"

  make DESTDIR="${pkgdir}" install
}


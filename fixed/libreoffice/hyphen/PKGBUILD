# $Id: PKGBUILD 101756 2010-12-01 17:04:13Z andyrtr $
# Maintainer: AndyRTR <andyrtr@archlinux.org>

pkgname=('hyphen')
pkgver=2.7.1
pkgrel=1
pkgdesc="library for high quality hyphenation and justification"
arch=('i686' 'x86_64')
url="http://hunspell.sf.net"
license=('GPL' 'LGPL' 'MPL')
depends=('glibc' 'perl')
makedepends=('glibc' 'perl')
options=('!libtool')
source=(http://downloads.sourceforge.net/hunspell/${pkgname}-${pkgver}.tar.gz)
md5sums=('48a9f787f43a09c0a9b7b00cd1fddbbf')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install-libLTLIBRARIES
  make DESTDIR="$pkgdir/" install-binSCRIPTS
  make DESTDIR="$pkgdir/" install-includeHEADERS
  
  # license
  install -D -m644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

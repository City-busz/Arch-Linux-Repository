# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=gio-sharp
pkgver=0.2
_pkgver=0.2-0-g07ac6ea
_git=4159f82
pkgrel=1
pkgdesc="a branch of the official gtk-sharp/gio to get gio-sharp building on gtk-sharp 2.12"
arch=('any')
url="http://github.com/mono/gio-sharp"
license=('MIT')
depends=('gtk-sharp-2>=2.12')
source=(http://download.github.com/mono-$pkgname-$_pkgver.tar.gz)
md5sums=('f619624cad668b5c5c7f0f0f51a4616c')

build() {
  cd "$srcdir/mono-$pkgname-$_git"

  ./autogen-2.22.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd "$srcdir/mono-$pkgname-$_git"

  make DESTDIR="$pkgdir/" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}

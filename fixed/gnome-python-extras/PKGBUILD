# $Id: PKGBUILD 94470 2010-10-07 11:04:18Z ibiru $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=gnome-python-extras
pkgname=('gnome-python-extras' 'python2-egg' 'python2-gda' 'python2-gdl' 'python2-gksu2' 'python2-gtkhtml2' 'python2-gtkmozembed' 'python2-gtkspell')
pkgver=2.25.3
pkgrel=7.1
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
makedepends=('libgtkhtml' 'gtkspell' 'gdl>=2.28.1' 'xulrunner>=1.9.2' 'libgksu' 'libgnomeui' 'libgda')
options=('!libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/gnome-python-extras/2.25/gnome-python-extras-${pkgver}.tar.bz2
	gnome-python-extras-2.25.3-update-for-2.27.2.patch)
url="http://www.daa.com.au/~james/pygtk/"
md5sums=('9f3b7ec5c57130b96061cb486b79c076'
         '0b0a4d93016d14f2306682301de3bda3')

build() {
    cd "${srcdir}/gnome-python-extras-${pkgver}"
    patch -Np1 -i "${srcdir}"/gnome-python-extras-2.25.3-update-for-2.27.2.patch
    ./configure --prefix=/usr
    make MOZILLA_HOME=/usr/lib/xulrunner-1.9.2
}

package_gnome-python-extras() {
    pkgdesc="Gnome Python interfaces for libraries not part of the core platform"
    depends=('python2-egg' 'python2-gda' 'python2-gdl' 'python2-gksu2' 'python2-gtkhtml2' 'python2-gtkmozembed' 'python2-gtkspell')

    cd "${srcdir}/gnome-python-extras-${pkgver}"
    make install-pkgconfigDATA DESTDIR="${pkgdir}"
}

package_python2-egg(){
    pkgdesc="Python bindings for interacting with egg"
    depends=('libgnomeui' 'gnome-python')

    cd "${srcdir}/gnome-python-extras-${pkgver}"
    make -C egg install DESTDIR="${pkgdir}"
}

package_python2-gda(){
    pkgdesc="Python bindings for interacting with gda"
    depends=('libgda' 'pygtk')

    cd "${srcdir}/gnome-python-extras-${pkgver}"
    make -C gda install DESTDIR="${pkgdir}"
}

package_python2-gdl() {
    pkgdesc="Python bindings for gdl"
    depends=('gdl>=2.28.1' 'pygtk')

    cd "${srcdir}/gnome-python-extras-${pkgver}"
    make -C gdl install DESTDIR="${pkgdir}"
}

package_python2-gksu2() {
    pkgdesc="Python bindings for the gksu2"
    depends=('libgksu' 'pygtk')

    cd "${srcdir}/gnome-python-extras-${pkgver}"
    make -C gksu2 install DESTDIR="${pkgdir}"
}

package_python2-gtkhtml2() {
    pkgdesc="Python bindings for the gtkhtml2"
    depends=('libgtkhtml' 'pygtk')

    cd "${srcdir}/gnome-python-extras-${pkgver}"
    make -C gtkhtml2 install DESTDIR="${pkgdir}"
}

package_python2-gtkmozembed() {
    pkgdesc="Python bindings for the gtkmozembed"
    depends=('xulrunner>=1.9.2' 'pygtk')

    cd "${srcdir}/gnome-python-extras-${pkgver}"
    make -C gtkmozembed install DESTDIR="${pkgdir}"
}

package_python2-gtkspell() {
    pkgdesc="Python bindings for the gtkspell"
    depends=('gtkspell' 'pygtk')

    cd "${srcdir}/gnome-python-extras-${pkgver}"
    make -C gtkspell install DESTDIR="${pkgdir}"
}

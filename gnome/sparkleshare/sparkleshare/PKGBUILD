# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=sparkleshare-git
pkgver=20101216
pkgrel=1
pkgdesc="A file sharing and collaboration tool inspired by Dropbox"
arch=('any')
url="http://sparkleshare.org/"
license=('GPL')
depends=('notify-sharp-svn' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('git' 'intltool>=0.40.6' 'gnome-doc-utils>=0.17.3' 'nant')
install=$pkgname.install

_gitroot="git://github.com/hbons/SparkleShare.git"
_gitname="sparkleshare"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  #
  # BUILD HERE
  #

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
               --disable-scrollkeeper --disable-debug --enable-release
  make
}

package() {
  cd "$srcdir/$_gitname-build"

  make DESTDIR="$pkgdir/" install
} 

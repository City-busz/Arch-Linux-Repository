# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=byzanz
pkgver=0.2.3
pkgrel=1
pkgdesc="Record what's happening on your desktop"
arch=('i686' 'x86_64')
url="http://blogs.gnome.org/otte/2009/08/30/byzanz-0-2-0/"
license=('GPL')
depends=('gnome-panel-bonobo>=2.10.0' 'gstreamer0.10-base>=0.10.24' 'gconf' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('intltool')
install=$pkgname.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('688505712c961a3ce3812188cb490e6f3bd1a60bb4528bbdcb7ffcf976424d66')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static --disable-schemas-install \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

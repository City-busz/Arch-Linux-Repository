# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=gwget
pkgver=1.0.4
pkgrel=1
pkgdesc="Download manager for Gnome"
arch=('i686' 'x86_64')
url="http://projects.gnome.org/gwget/"
license=('GPL')
depends=('libgnomeui>=2.0.0')
makedepends=('intltool>=0.35.0')
install=$pkgname.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('a65b2b2b3befb6cf34a697a13111b35498a6d63f9a9b048a22b8654f35ef643f')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --disable-schemas-install \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

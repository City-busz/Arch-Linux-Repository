# Maintainer: György Balló <ballogy@freestart.hu>
_pkgname=xchat-gnome
pkgname=$_pkgname-git
pkgver=20111114
pkgrel=1
pkgdesc="GNOME frontend to the popular X-Chat IRC client"
arch=('i686' 'x86_64')
url="http://live.gnome.org/Xchat-Gnome"
license=('GPL')
depends=('gconf>=2.8.0' 'libsexy>=0.1.11' 'libnotify>=0.7.0' 'libcanberra>=0.3' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('git' 'gnome-common' 'intltool>=0.40.0' 'gnome-doc-utils' 'python2' 'tcl')
optdepends=('python2: plugin support'
            'tcl: plugin support')
provides=('xchat-gnome')
conflicts=('xchat-gnome')
options=('!libtool')
install=$_pkgname.install

_gitroot="git://git.gnome.org/xchat-gnome"
_gitname="xchat-gnome"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  #
  # BUILD HERE
  #

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
               --disable-static --disable-scrollkeeper --disable-schemas-install \
               --with-gconf-schema-file-dir=/usr/share/gconf/schemas \
               PYTHON=python2
  make
}

package() {
  cd "$srcdir/$_gitname-build"

  make DESTDIR="$pkgdir/" install

  gconf-merge-schema "$pkgdir/usr/share/gconf/schemas/$_pkgname.schemas" "$pkgdir"/usr/share/gconf/schemas/*.schemas
}

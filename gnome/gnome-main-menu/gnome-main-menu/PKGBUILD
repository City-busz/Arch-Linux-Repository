# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=('gnome-application-browser' 'gnome-main-menu')
pkgbase=gnome-main-menu
pkgver=0.9.15
pkgrel=1
arch=('i686' 'x86_64')
url="http://git.gnome.org/browse/gnome-main-menu/"
license=('GPL')
depends=('libslab>=2.29.0' 'libunique' 'gnome-panel-bonobo' 'libgtop' 'networkmanager>=0.7.0')
makedepends=('nautilus>=2.6.0' 'intltool>=0.35.0')
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgbase-$pkgver.tar.bz2)
sha256sums=('d9d720afd32febd24809eef47d9ba1765f0302fb108d44c65e9fb56896921e71')

build() {
  cd "$srcdir/$pkgbase-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib \
              --disable-static --disable-schemas-install \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas
  make
}

package_gnome-application-browser() {
  pkgdesc="An application browser for the GNOME Desktop"
  install=${pkgname[0]}.install
  depends=('libslab>=2.29.0' 'libunique' 'gconf')
  cd "$srcdir/$pkgbase-$pkgver/application-browser/etc"
  make DESTDIR="$pkgdir/" install
  cd "$srcdir/$pkgbase-$pkgver/application-browser/src"
  make DESTDIR="$pkgdir/" install
  cd "$srcdir/$pkgbase-$pkgver/po"
  make DESTDIR="$pkgdir/" install

}

package_gnome-main-menu() {
  pkgdesc="Alternative applications/documents menu for the GNOME Panel"
  install=${pkgname[1]}.install
  depends=('libslab>=2.29.0' 'gnome-panel-bonobo' 'libgtop' 'networkmanager>=0.7.0' 'gnome-application-browser')

  cd "$srcdir/$pkgbase-$pkgver/main-menu/etc"
  make DESTDIR="$pkgdir/" install
  cd "$srcdir/$pkgbase-$pkgver/main-menu/src"
  make DESTDIR="$pkgdir/" install

}

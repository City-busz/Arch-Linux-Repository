# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=transmageddon
pkgver=0.20
pkgrel=1
pkgdesc="A video transcoder for Linux and Unix systems built using GStreamer"
arch=('any')
url="http://linuxrising.org/"
license=('LGPL')
depends=('gstreamer0.10-python>=0.10.6' 'pygtk>=2.8.0' 'gstreamer0.10-base-plugins>=0.10.23')
makedepends=('intltool>=0.35.0')
optdepends=('gstreamer0.10-good-plugins: Extra media codecs'
            'gstreamer0.10-ugly-plugins: Extra media codecs'
            'gstreamer0.10-bad-plugins: Extra media codecs'            
            'gstreamer0.10-ffmpeg: Extra media codecs')
source=(http://www.linuxrising.org/files/$pkgname-$pkgver.tar.bz2)
md5sums=('ce1cdc366d10c8b3bcc7b73afb08b627')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  find . -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'
  sed -i 's/python transmageddon.py/python2 transmageddon.py/' bin/transmageddon.in

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-maintainer-mode
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

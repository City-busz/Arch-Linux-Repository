# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=blam
pkgver=1.8.9
pkgrel=2
pkgdesc="A simple newsreader for GNOME"
arch=('any')
url="http://blam.relatitude.org/"
license=('GPL')
depends=('webkit-sharp' 'gconf-sharp' 'libgnome-sharp' 'notify-sharp' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('intltool')
install=$pkgname.install
source=(http://blam.relatitude.org/download/$pkgname-$pkgver.tar.bz2
        blam-mono-2.10.patch)
md5sums=('3ce13cf7c78e9c4fbd746e96e81e3ae9'
         '87efd34e5f136d4c740d2fd22b8f0314')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i "$srcdir/blam-mono-2.10.patch"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-schemas-install \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

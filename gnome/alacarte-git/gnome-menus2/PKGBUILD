# Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: Jan de Groot <jan@archlinux.org>

pkgname=gnome-menus2
_pkgname=gnome-menus
pkgver=3.0.1
pkgrel=1
pkgdesc="GNOME menu specifications"
arch=('i686' 'x86_64')
depends=('glib2' 'python2')
makedepends=('intltool' 'gobject-introspection')
options=('!libtool')
license=('GPL' 'LGPL')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgname}/${pkgver%.*}/${_pkgname}-${pkgver}.tar.bz2
        menus.patch)
sha256sums=('579c119c26f37781f66708e867ea45b3c37589b3b69e5b32d33e9bdb944165f0'
            'df2779777906b038fa911dc745534564ede9524a64ab368e2f6f8e38d54e7acc')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  patch -Np0 -i "$srcdir/menus.patch"

  PYTHON=/usr/bin/python2 ./configure \
      --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var  --disable-static
  make
}

package(){
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make -C libmenu DESTDIR="${pkgdir}" install
  make -C python DESTDIR="${pkgdir}" install
}

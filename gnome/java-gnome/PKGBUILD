# Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Timm Preetz <timm@preetz.us>
pkgname=java-gnome
pkgver=4.1.1
pkgrel=1
pkgdesc="Java bindings to the GNOME platform (including gtk, glib and glade)"
arch=('i686' 'x86_64')
url="http://java-gnome.sourceforge.net"
license=('GPL')
depends=('java-runtime' 'gtksourceview3' 'librsvg' 'libunique3' 'libnotify' 'enchant')
makedepends=('java-environment' 'junit' 'python2')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/4.1/$pkgname-$pkgver.tar.xz)
sha256sums=('b013020d3da99740bae17e60f29b94c1d999183128612eac1f19a4570b045db2')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  #python2
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' build/faster src/util/demux/demux.py

  # adapt to the new build-system
  unset CLASSPATH
  if [ -d "/usr/lib/jvm/java-6-openjdk/" ]; then
    ./configure jdk=/usr/lib/jvm/java-6-openjdk/ prefix=/usr
  else
    ./configure
  fi
  make
  DISPLAY= make doc
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR=$pkgdir install

  install -d "$pkgdir/usr/share/gtk-doc/html/$pkgname"
  cp -R "$srcdir"/$pkgname-$pkgver/doc/api/* "$pkgdir/usr/share/gtk-doc/html/$pkgname"
}

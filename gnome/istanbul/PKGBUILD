# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=istanbul
pkgver=0.2.2
pkgrel=2
pkgdesc="A desktop session recorder for the Free Desktop."
arch=('i686' 'x86_64')
url="http://live.gnome.org/Istanbul"
license=('GPL')
depends=('gstreamer0.10-python>=0.10.0' 'python2-egg' 'python2-gconf' 'python2-gnomevfs' 'pygtk>=2.6.3' 'python-xlib' 'gstreamer0.10-base-plugins>=0.10.0' 'xdg-utils')
makedepends=('gnome-python' 'gnome-python-extras>=2.11.3' 'intltool>=0.35.0')
options=('!libtool')
source=(http://zaheer.merali.org/$pkgname-$pkgver.tar.bz2)
md5sums=('8ddcfd5a29dcd10fdafc10af9f66848b')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' bin/istanbul.in

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --disable-schemas-install \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas \
              PYTHON=python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

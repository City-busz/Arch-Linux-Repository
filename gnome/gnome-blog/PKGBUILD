# Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Kritoke <kritoke@nospam.gmail.com>

pkgname=gnome-blog
pkgver=0.9.2
pkgrel=7
pkgdesc="A gnome application that allows you to post entries to many different blog formats"
arch=('any')
license=('GPL')
depends=('python2-gconf' 'python-gdata' 'python2-gtkspell' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('intltool' 'desktop-file-utils')
install=$pkgname.install
url="http://projects.gnome.org/gnome-blog/"
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/0.9/$pkgname-$pkgver.tar.bz2
        drop-gnomeapplet.patch
        drop-gnomevfs.patch
        drop-gnomeui.patch)
md5sums=('39ec54600a6b8dd786729c401fcfdf27'
         '4f6c286bad2f27a0c024e23cc56b1e77'
         '930e8783f5238af36b695e02a8b5617c'
         'f1221002c8e6b669bbe1bea6c9c2f452')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i "$srcdir/drop-gnomeapplet.patch"
  patch -Np1 -i "$srcdir/drop-gnomevfs.patch"
  patch -Np1 -i "$srcdir/drop-gnomeui.patch"
  find . -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'

  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-schemas-install \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas \
              PYTHON=/usr/bin/python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  mv "$pkgdir"/usr/share/gconf/schemas/{gnomeblog.schemas,$pkgname.schemas}
}

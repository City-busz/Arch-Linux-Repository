# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=eog-plugins
pkgver=2.30.2
pkgrel=1
pkgdesc="A set of plugins for Eye of GNOME"
arch=('i686' 'x86_64')
url="http://live.gnome.org/EyeOfGnome/Plugins"
license=('GPL')
depends=('eog>=2.19.0' 'pygtk>=2.9.7' 'libgdata>=0.6.0' 'libexif>=0.6.16')
makedepends=('intltool>=0.40.0' 'gnome-python>=2.18.2' 'postr')
optdepends=('postr: Flickr support')
options=(!libtool)
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('3a43466ffd954851503207228045d4df3c11e5bebaf41f31cb85724d1eadf6fd')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static \
              PYTHON=python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

# Maintainer: György Balló <ballogy@freestart.hu>
pkgbase='packagekit'
pkgname=('packagekit' 'packagekit-python' 'packagekit-qt' 'packagekit-backend-alpm' 'packagekit-backend-pacman')
_pkgname=PackageKit
pkgver=0.6.12
pkgrel=1
arch=('i686' 'x86_64')
url="http://packagekit.org/"
license=('GPL')
makedepends=('sqlite3' 'dbus-glib>=0.74' 'polkit>=0.97' 'xz>=5.0.0' 'python2>=2.7' 'qt>=4.4.0' 'intltool>0.35.0' 'gtk-doc>=1.9' 'gobject-introspection>=0.6.7' 'pacman-glib>=3.4.0')
options=('!libtool')
source=(http://www.packagekit.org/releases/$_pkgname-$pkgver.tar.bz2)
md5sums=('9ffcda702aba3334d5799e7698e29abb')

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' lib/python/packagekit/*

  ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --libexecdir=/usr/lib/PackageKit \
      --disable-static \
      --disable-gtk-doc \
      --disable-gtk-doc-html \
      --disable-gtk-doc-pdf \
      --enable-qt \
      --disable-service-packs \
      --enable-man-pages \
      --disable-tests \
      --disable-local \
      --disable-browser-plugin \
      --disable-gstreamer-plugin \
      --disable-gtk-module \
      --disable-command-not-found \
      --disable-cron \
      --disable-debuginfo-install \
      --disable-device-rebind \
      --disable-pm-utils \
      --enable-alpm \
      --enable-pacman \
      --disable-dummy \
      PYTHON=python2
  make
  ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --libexecdir=/usr/lib/PackageKit \
      --disable-static \
      --disable-gtk-doc \
      --disable-gtk-doc-html \
      --disable-gtk-doc-pdf \
      --disable-qt \
      --disable-service-packs \
      --enable-man-pages \
      --disable-tests \
      --disable-local \
      --disable-browser-plugin \
      --disable-gstreamer-plugin \
      --disable-gtk-module \
      --disable-command-not-found \
      --disable-cron \
      --disable-debuginfo-install \
      --disable-device-rebind \
      --disable-pm-utils \
      --enable-alpm \
      --enable-pacman \
      --disable-dummy \
      PYTHON=python2
}

package_packagekit() {
pkgdesc="A system designed to make installing and updating software on your computer easier"
depends=('pacman>=3.4.1' 'sqlite3' 'dbus-glib>=0.74' 'polkit>=0.92' 'xz>=5.0.0' 'shared-mime-info' 'packagekit-backend-alpm')
optepends=('networkmanager>=0.6.4' 'connman')
install=$pkgname.install
backup=('etc/PackageKit/PackageKit.conf'
        'etc/PackageKit/pacman.d/pacman.conf'
        'etc/PackageKit/pacman.d/repos.list')

  cd "$srcdir/$_pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # clean up unneeded stuff
  rm -rf "${pkgdir}/usr/share/PackageKit/website"
  rm -rf ${pkgdir}/usr/share/gtk-doc
  rmdir "${pkgdir}/var/log"

  # remove empty transactions db
  rm -f "${pkgdir}/var/lib/PackageKit/transactions.db"

  # rename bash completion script
  mv "${pkgdir}/etc/bash_completion.d/pk-completion.bash" "${pkgdir}/etc/bash_completion.d/pkcon"

  # will be in packagekit-*
  rm -rf ${pkgdir}/usr/lib/python*
  [ -d ${srcdir}/packagekit-backend-install ] && rm -rf ${srcdir}/packagekit-backend-install
  mv ${pkgdir}/usr/lib/packagekit-backend ${srcdir}/packagekit-backend-install
}

package_packagekit-python() {
	arch=('any')
	depends=('packagekit' 'python2>=2.7')
	pkgdesc='PackageKit python bindings'
	cd "$srcdir/$_pkgname-$pkgver/lib/python/"
	make DESTDIR="${pkgdir}" install
}

package_packagekit-qt() {
	depends=('packagekit' 'qt>=4.4.0')
	pkgdesc='PackageKit qt bindings'
	cd "$srcdir/$_pkgname-$pkgver/lib/packagekit-qt/"
	make DESTDIR="${pkgdir}" install
}

package_packagekit-backend-alpm() {
	depends=('pacman>=3.4.1' 'packagekit=0.6.12')
	pkgdesc='PackageKit alpm backend'
	install -Dm755 ${srcdir}/packagekit-backend-install/libpk_backend_alpm.so \
		${pkgdir}/usr/lib/packagekit-backend/libpk_backend_alpm.so
}

package_packagekit-backend-pacman() {
	depends=('pacman-glib>=3.4.0' 'packagekit=0.6.12')
	pkgdesc='PackageKit pacman-glib backend'
	provides=('packagekit-backend-alpm')
	conflicts=('packagekit-backend-alpm')
	install=$pkgname-backend-pacman.install
	install -Dm755 ${srcdir}/packagekit-backend-install/libpk_backend_pacman.so \
		${pkgdir}/usr/lib/packagekit-backend/libpk_backend_pacman.so
}


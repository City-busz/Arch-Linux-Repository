# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=gnome-packagekit
pkgver=2.32.0
pkgrel=1
pkgdesc="Gnome tool for PackageKit, with pacman support"
arch=('i686' 'x86_64')
url="http://packagekit.org/"
license=('GPL')
depends=('packagekit>=0.5.5' 'libnotify>=0.4.3' 'gnome-menus>=2.24.1' 'libunique>=1.0.0' 'upower>=0.9.0' 'libcanberra>=0.10' 'gconf>=0.22' 'desktop-file-utils' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('intltool>=0.35.0' 'gnome-doc-utils')
options=('!libtool')
install=$pkgname.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2
        arch.patch)
sha256sums=('bc68dad11e5c9db89dcd2548a29736456063b38aa99633f9cfc4427a77a4525e'
            '16784b0d1c473a21d1ac962efda138b5d6854ee5b78eebce1f8126f32c0568e1')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' python/enum-convertor.py
  sed -i 's/python enum-convertor.py/python2 enum-convertor.py/' python/packagekit/Makefile.in
  patch -Np1 -i $srcdir/arch.patch

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --disable-scrollkeeper --disable-schemas-install \
              PYTHON=python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
  mv $pkgdir/etc/gconf $pkgdir/usr/share/gconf
}

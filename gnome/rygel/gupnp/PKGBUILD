# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=gupnp
pkgver=0.16.1
pkgrel=1
pkgdesc="An object-oriented open source framework for creating UPnP devices and control points, written in C using GObject and libsoup"
arch=('i686' 'x86_64')
url="http://gupnp.org/"
license=('LGPL')
depends=('gssdp>=0.9.2')
makedepends=('gobject-introspection>=0.6.4')
optdepends=('python2: for gupnp-binding-tool')
options=('!libtool')
source=(http://gupnp.org/sites/all/files/sources/$pkgname-$pkgver.tar.gz)
md5sums=('021bb237741532af4bca50157ff326e4')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' tools/gupnp-binding-tool

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

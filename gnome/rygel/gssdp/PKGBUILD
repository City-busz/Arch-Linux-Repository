# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=gssdp
pkgver=0.8.2
pkgrel=1
pkgdesc="A GObject-based API for handling resource discovery and announcement over SSDP"
arch=('i686' 'x86_64')
url="http://gupnp.org/"
license=('LGPL')
depends=('libsoup' 'gtk2>=2.12.0')
makedepends=('gobject-introspection')
options=('!libtool')
source=(http://gupnp.org/sites/all/files/sources/$pkgname-$pkgver.tar.gz)
md5sums=('7a3d3f421986a52f3df641297d9598b1')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=hamster-applet
pkgver=2.91.2
pkgrel=1
pkgdesc="GNOME time tracking applet"
arch=('any')
url="http://projecthamster.wordpress.com/"
license=('GPL')
depends=('dbus-python' 'python2-gconf' 'python-notify' 'python-wnck' 'pyxdg' 'desktop-file-utils' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('intltool' 'gnome-doc-utils>=0.20.1' 'gnome-control-center')
optdepends=('python-evolution: integration with evolution')
options=(!emptydirs)
install=$pkgname.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('e16cccbc3f9127b5fbf0aea72e2c3d42ff4ebff756e9ef9acad8f2ce492c2d14')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  find . -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'

  python2 ./waf configure --prefix=/usr
  python2 ./waf build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 ./waf install --destdir=$pkgdir

  mv "$pkgdir/etc/gconf" "$pkgdir/usr/share"
}

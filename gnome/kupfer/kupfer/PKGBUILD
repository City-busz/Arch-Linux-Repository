# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=kupfer
pkgver=207
pkgrel=1
pkgdesc="Convenient command and access tool for GNOME"
arch=('any')
url="http://kaizer.se/wiki/kupfer/"
license=('GPL')
depends=('dbus-python' 'libkeybinder' 'pygtk' 'pyxdg' 'desktop-file-utils' 'shared-mime-info' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('intltool' 'docutils' 'gnome-doc-utils')
optdepends=('libappindicator: Application Indicator support'
            'docutils: required for RenderView plugin'
            'python-gdata: required for Gmail and Google Picasa plugins'
            'python2-gconf: required for GNOME Terminal Profiles plugin'
            'python-gnomekeyring: optionally required for Gmail and Google Picasa plugins'
            'python-wnck: required for Documents and Window List plugins'
            'gwibber: required for Gwibber plugin'
            'python2-keyring: required for Gmail and Google Picasa plugins'
            'virtualbox-sdk: required for VirtualBox plugin')
install=$pkgname.install
source=(http://kaizer.se/publicfiles/$pkgname/$pkgname-v$pkgver.tar.xz)
md5sums=('8e2fd34f7f5c7cf6f9be688b4cfa59eb')

build() {
  cd "$srcdir/$pkgname-v$pkgver"

  export PYTHON=/usr/bin/python2
  python2 ./waf configure --prefix=/usr
  python2 ./waf
}

package() {
  cd "$srcdir/$pkgname-v$pkgver"

  python2 ./waf install -f --destdir=$pkgdir

  # Remove the nautilus extension which not compatible with nautilus 3
  rm -r "$pkgdir/usr/lib"
}

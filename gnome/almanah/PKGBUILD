# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=almanah
pkgver=0.7.3
pkgrel=1
pkgdesc="Small GTK+ application to allow you to keep a diary of your life"
arch=('i686' 'x86_64')
url="http://live.gnome.org/Almanah_Diary"
license=('GPL')
depends=('evolution-data-server>=2.32.0' 'gtkspell' 'seahorse' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('intltool')
install=$pkgname.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('2ed10f8dc4499ad5b703cdc9fa518e33f98e7c78fd70b44055fe6f80e6cda84a')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --disable-schemas-install \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas \
              CFLAGS="$CFLAGS -D_FILE_OFFSET_BITS=64"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

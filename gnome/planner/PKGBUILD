# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=planner
pkgver=0.14.5
pkgrel=1
pkgdesc="Gnome Project Management System"
arch=('i686' 'x86_64')
url="http://live.gnome.org/Planner"
license=('GPL')
depends=('libgnomeui>=2.10.0' 'libxslt>=1.1.23' 'evolution-data-server>=3.0.0' 'python2' 'desktop-file-utils' 'xdg-utils')
makedepends=('pygtk>=2.6.0' 'intltool>=0.35.5' 'gtk-doc')
options=(!libtool)
install=planner.install
source=(http://ftp.gnome.org/pub/GNOME/sources/planner/${pkgver%.*}/planner-${pkgver}.tar.xz)
sha256sums=('6289b12cf7ecad00900ff09c76214196436cf32e8bdd71defd207a32b3af54ce')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --disable-update-mimedb --disable-schemas-install \
              --enable-gtk-doc \
              --enable-python \
              --enable-python-plugin \
              --enable-eds \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas \
              PYTHON=/usr/bin/python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=planner
pkgver=0.14.6
pkgrel=1
pkgdesc="Gnome Project Management System"
arch=('i686' 'x86_64')
url="http://live.gnome.org/Planner"
license=('GPL')
depends=('libgnomeui' 'libxslt' 'python2' 'desktop-file-utils' 'xdg-utils')
makedepends=('pygtk' 'intltool' 'gtk-doc' 'evolution-data-server>=3.2.0')
optdepends=('evolution-data-server: evolution-data-server backend support')
options=('!libtool')
install=planner.install
source=(http://ftp.gnome.org/pub/GNOME/sources/planner/${pkgver%.*}/planner-${pkgver}.tar.xz)
sha256sums=('130d29e6d6b8c1994017deba2acca05a4bf83ba1e169635ec8c5c7878abe0696')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --disable-update-mimedb --disable-schemas-install \
              --enable-gtk-doc \
              --enable-python \
              --enable-python-plugin \
              --enable-eds \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas \
              PYTHON=/usr/bin/python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

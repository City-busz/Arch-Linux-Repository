# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=planner
pkgver=0.14.4
pkgrel=2
pkgdesc="Gnome Project Management System"
arch=('i686' 'x86_64')
url="http://live.gnome.org/Planner"
license=('GPL')
depends=('libgnomeui>=2.10.0' 'libxslt>=1.1.23' 'evolution-data-server>=3.0.0' 'python2' 'desktop-file-utils' 'xdg-utils')
makedepends=('pygtk>=2.6.0' 'intltool>=0.35.5' 'gtk-doc')
options=(!libtool)
install=planner.install
source=(http://ftp.gnome.org/pub/GNOME/sources/planner/${pkgver%.*}/planner-${pkgver}.tar.bz2)
md5sums=('8fcbd1b55dd037662f7710a28a96bb92')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --disable-update-mimedb --disable-schemas-install \
              --enable-gtk-doc \
              --enable-python \
              --enable-python-plugin \
              --enable-eds \
              --enable-simple-priority-scheduling \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

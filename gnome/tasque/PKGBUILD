# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=tasque
pkgver=0.1.9
pkgrel=1
pkgdesc="Easy quick task management"
arch=('any')
url="http://live.gnome.org/Tasque"
license=('MIT')
depends=( 'gnome-sharp' 'notify-sharp-svn' 'evolution-sharp>=0.18.1' 'sqlite3' 'gconf' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('intltool>=0.35')
install=$pkgname.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('1749b0c5a60d74f05f36193ff4aaf5b130ed3a47726f0b8e48c712805d1341af')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --enable-release
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/$pkgname/COPYING
}

# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=conduit
pkgver=0.3.17
pkgrel=3
pkgdesc="Synchronization for GNOME"
arch=('any')
url="http://live.gnome.org/Conduit"
license=('GPL')
depends=('python-gdata' 'dbus-python>=0.80.0' 'python-feedparser' 'python-flickrapi' 'pygoocanvas>=0.9.0' 'libgpod' 'gstreamer0.10-python' 'python-evolution' 'python2-gconf' 'python-imaging' 'python-pysqlite' 'python-simplejson' 'python-vobject' 'pywebkitgtk' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('intltool>=0.35.0' 'gnome-doc-utils>=0.3.2')
install=$pkgname.install
source=(http://ftp.gnome.org/pub/GNOME/sources/conduit/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=(dfbc00490b0170f22eb6e73b893da40c48556d8982b8400484e19fa617f5f934)

build() {
  cd "$srcdir/$pkgname-$pkgver"
  find . -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-scrollkeeper \
              PYTHON=python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

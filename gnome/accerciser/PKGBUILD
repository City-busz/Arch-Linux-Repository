# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=accerciser
pkgver=3.3.90
pkgrel=1
pkgdesc="Interactive Python accessibility explorer for GNOME"
arch=('any')
url="https://live.gnome.org/Accerciser"
license=('BSD')
depends=('python2-cairo' 'python2-gobject' 'pyatspi' 'at-spi2-atk' 'libwnck3' 'ipython2' 'dconf' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('intltool' 'gnome-doc-utils')
install=$pkgname.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('a58a897b7d68aaeb8f60a92f99ae5e112117e8d0c1bec87f5fa7fe463d956fe0')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' src/accerciser.in plugins/ipython_view.py

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-schemas-compile \
              PYTHON=python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=accerciser
pkgver=3.2.1
pkgrel=1
pkgdesc="Interactive Python accessibility explorer for GNOME"
arch=('any')
url="https://live.gnome.org/Accerciser"
license=('BSD')
depends=('python2-cairo' 'python2-gobject' 'pyatspi' 'at-spi2-atk' 'libwnck3' 'dconf' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('intltool' 'gnome-doc-utils')
optdepends=('ipython2: python console plugin')
install=$pkgname.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('5e6ed557386057ca4286b8b81e610188ec7bdfa6a099fa2159926e03c3b52e82')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' src/accerciser.in plugins/ipython_view.py

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-schemas-compile \
              PYTHON=python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Ben Mazer <blm@groknil.org>

pkgname=muine
pkgver=0.8.11
pkgrel=2
pkgdesc="A music player written in C Sharp"
arch=(i686 x86_64)
license=('GPL')
url="http://www.muine-player.org/"
depends=('gstreamer0.10-base-plugins' 'gstreamer0.10-good-plugins' 'libgnome-sharp' 'gconf-sharp' 'taglib-sharp' 'notify-sharp-svn' 'musicbrainz' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('intltool' 'gnome-common')
optdepends=('gstreamer0.10-ugly-plugins: Extra media codecs'
            'gstreamer0.10-bad-plugins: Extra media codecs'
            'gstreamer0.10-ffmpeg: Extra media codecs')
options=('!libtool')
install=muine.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/0.8/$pkgname-$pkgver.tar.bz2
        use-coloredcellrendererpixbuf.patch
        fix-multimedia-key-support.patch
        use-notify-sharp.patch
        fix-deprecations.patch
        use-standard-icons.patch)
md5sums=('f2a74519d9e9c6c4fca9c5217a13964a'
         '44d9b28b79b182e1efb1c56910994c5f'
         '983072d2aa9b64baa8fc185e5bc6459b'
         '28fbe637f970980ae71114e2e97500f8'
         '1344a8a188cd89f6e7c1a38f3a52d277'
         '322768fd2df44fc635c09df4fa82641b')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -R -i "$srcdir/use-coloredcellrendererpixbuf.patch"
  patch -Np1 -i "$srcdir/fix-multimedia-key-support.patch"
  patch -Np1 -i "$srcdir/use-notify-sharp.patch"
  patch -Np1 -i "$srcdir/fix-deprecations.patch"
  patch -Np1 -i "$srcdir/use-standard-icons.patch"

  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --disable-schemas-install \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

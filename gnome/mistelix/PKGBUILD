# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=mistelix
pkgver=0.32
pkgrel=1
pkgdesc="Author DVDs and slideshows"
arch=('i686' 'x86_64')
url="http://mistelix.org/"
license=('MIT')
depends=('gstreamer0.10-base-plugins>=0.10.3' 'gstreamer0.10-ffmpeg' 'gnome-sharp>=2.10' 'mono-addins>=0.3' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('intltool>=0.35')
optdepends=('gstreamer0.10-good-plugins: Extra media codecs'
            'gstreamer0.10-bad-plugins: Extra media codecs'
            'gstreamer0.10-ugly-plugins: Extra media codecs')
options=('!libtool')
install=$pkgname.install
source=(http://mistelix.org/files/$pkgname-$pkgver.tar.gz)
md5sums=('895886755d55e24c5dfaf6ee777ec3e9')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's/Application.Init ()/Gtk.Application.Init ()/' src/mistelix.cs

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/$pkgname/COPYING
}

# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=sabayon
pkgver=2.30.1
pkgrel=1
pkgdesc="A system administration tool to define and deploy desktop profiles for the GNOME desktop environment"
arch=('i686' 'x86_64')
url="http://live.gnome.org/Sabayon"
license=('GPL')
depends=('pyxdg' 'pessulus' 'xorg-server-xephyr' 'libsm' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('intltool>=0.40.0' 'gnome-doc-utils>=0.17.3')
options=('!libtool')
install=$pkgname.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('eff725ae2dad3d55a3580a83b821e2279787b5e3dfb280a1c38fa39d40dcc73a') #generate with 'makepkg -g'

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' admin-tool/sabayon{,-{apply,session}}
  sed -i 's/python -c/python2 -c/' configure

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir="/usr/lib/$pkgname" \
              --disable-static --disable-scrollkeeper \
              --with-distro=frugalware \
              PYTHON=python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=goobox
pkgver=3.0.0
pkgrel=1
pkgdesc="A CD player and ripper for the GNOME desktop"
arch=('i686' 'x86_64')
url="http://people.gnome.org/~paobac/goobox/"
license=('GPL')
depends=('brasero' 'libmusicbrainz3' 'libnotify' 'xdg-utils')
makedepends=('gconf' 'intltool>=0.35.0' 'gnome-doc-utils>=0.3.2')
optdepends=('gstreamer0.10-good-plugins: Extra media codecs'
            'gstreamer0.10-bad-plugins: Extra media codecs'            
            'gstreamer0.10-ugly-plugins: Extra media codecs'
            'gstreamer0.10-ffmpeg: Extra media codecs')
install=$pkgname.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('ab893c6263a06e74a5c73d3fd29e2f37ae8b9b7fd210711a99cc77e61d502aa1')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # DSO fix
  export LDFLAGS="$LDFLAGS -lm"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --disable-scrollkeeper --disable-schemas-compile
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=drwright
pkgver=3.2.0
pkgrel=2
pkgdesc="Typing monitor to force typing breaks"
arch=(i686 x86_64)
url="http://git.gnome.org/browse/drwright"
license=(GPL)
depends=(libxss 'gnome-settings-daemon>=2.91.8' xdg-utils)
makedepends=('intltool>=0.35.0')
optdepends=('gnome-control-center: settings panel')
options=(!libtool)
install=$pkgname.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
        default-false.patch)
sha256sums=('85167fb61c6df46ac81e62dc7112c43dc51efb460443400b615f5ea3b523a3ce'
            '110c87036f3ff5f0281f52921af82bacb68d333069985979ee7da8922752fabe')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i "$srcdir/default-false.patch"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --disable-schemas-compile
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

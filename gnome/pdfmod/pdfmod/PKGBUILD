# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=pdfmod
pkgver=0.9.0
pkgrel=1
pkgdesc="Quick, Easy PDF Modification"
arch=('any')
url="http://live.gnome.org/PdfMod"
license=('GPL')
depends=('gnome-sharp' 'hyena>=0.3' 'desktop-file-utils' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('intltool>=0.35.0' 'gnome-doc-utils>=0.3.2')
install=$pkgname.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2
        mono28.patch
        pdfmod-hu.po)
sha256sums=('eacf6cdd3f64a8cc37958a98c2cbc3a76de39a11b4d83307df51e7b6b861079c'
            '4b6b736c147f276631b6874ec947774ec27cfe0e19385630a1e639da4777aec3'
            '1a3f7fef8d4382e6fcb87b8303bb824f9c421ba1320dc67698285c1fa706e7e0')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  mv -f $srcdir/pdfmod-hu.po po/hu.po
  sed -i '$ a hu' po/LINGUAS
  sed -i 's/libpoppler-glib\.so\.4/libpoppler-glib\.so\.5/' lib/poppler-sharp/poppler-sharp/poppler-sharp.dll.config
  patch -Np1 -i $srcdir/mono28.patch

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-scrollkeeper
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

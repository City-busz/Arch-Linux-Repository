# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=pdfmod
pkgver=0.9.1
pkgrel=1
pkgdesc="Quick, Easy PDF Modification"
arch=('any')
url="http://live.gnome.org/PdfMod"
license=('GPL')
depends=('gnome-sharp' 'poppler-glib>=0.16' 'desktop-file-utils' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('hyena>=0.3' 'intltool>=0.35.0' 'gnome-doc-utils>=0.3.2')
install=$pkgname.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2
pdfmod-mono-2.10.patch)
sha256sums=('eb7c987514a053106ddf03f26544766c751c801d87762909b36415d46bc425c9'
'ed27e5acb7c9190622e27799b7dcc8c473f31edb9b37d1eda835db085ba7b29b')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p1 <$srcdir/pdfmod-mono-2.10.patch
  sed -i 's/libpoppler-glib\.so\.4/libpoppler-glib.so.6/' lib/poppler-sharp/poppler-sharp/poppler-sharp.dll.config

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-scrollkeeper
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

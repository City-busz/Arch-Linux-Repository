# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=gnac
pkgver=0.2.4
pkgrel=1
pkgdesc="Audio converter for GNOME"
arch=('i686' 'x86_64')
url="http://gnac.sourceforge.net/"
license=('GPL')
depends=('libnotify' 'libunique3' 'gstreamer0.10-base-plugins' 'dconf' 'desktop-file-utils' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('intltool' 'gnome-doc-utils')
optdepends=('gstreamer0.10-good-plugins: Extra media codecs'
            'gstreamer0.10-bad-plugins: Extra media codecs'            
            'gstreamer0.10-ugly-plugins: Extra media codecs'
            'gstreamer0.10-ffmpeg: Extra media codecs')
install=$pkgname.install
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2)
md5sums=('8794871d1abd8c186dd5b01d53f1e62e')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --disable-schemas-compile
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=gnome-color-manager
pkgver=2.32.0
pkgrel=2
pkgdesc="GNOME Color Profile Tools"
arch=('i686' 'x86_64')
url="http://live.gnome.org/GnomeColorManager"
license=('GPL')
depends=('libxxf86vm' 'gnome-desktop>=2.14.0' 'libunique>=1.0.0' 'vte>=0.22.2' 'lcms' 'libnotify>=0.7.0' 'sane' 'libcanberra>=0.10' 'cups' 'desktop-file-utils' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('intltool>=0.35.0' 'gnome-doc-utils>=0.3.2' 'gtk-doc')
install=$pkgname.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2
        gnome-color-manager-2.32.0-libnotify-0.7.patch)
sha256sums=('fa287ef12d329643d1f01b8f35c2b104dd293cd30e91a012f04929c203da54cf'
            '7326450f45bf7e5815671f380ab8c95215ef160b13b73bae9689af5d8a46decd')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i "$srcdir/gnome-color-manager-2.32.0-libnotify-0.7.patch"
  autoreconf -fi

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --disable-scrollkeeper --disable-schemas-install
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
  mv $pkgdir/etc/gconf $pkgdir/usr/share/gconf
}
